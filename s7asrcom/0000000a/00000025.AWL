FUNCTION_BLOCK "G301M_Code"
TITLE =G301M LOUVER HEATING GLASS TUBE HEAT EXCHANGER
//COPYRIGHT:    (C) 2009  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      WinCoS_BIW01_09
//
//TITLE:        LOUVER HEATING GLASS TUBE HEAT EXCHANGER
//                Machine group control
//                Maschinengruppensteuerung
// 
//--------------------------------------------------------------------------------
// 
//IDENT:        AUTHOR:             DEPT:                              DATE:
// 
//P01_FB401.1.0 K.Renz              CSB41                              07.Okt.2009
//             1st Release
// 
//--------------------------------------------------------------------------------
// 
//PURPOSE: 
// 
//MANUAL : 
// 
//RESTRICTIONS: 
// 
//--------------------------------------------------------------------------------
AUTHOR : KR
FAMILY : USW
VERSION : 9.9


VAR
  Gr : "GCS7_GrpCtrlSub";	//Standard interface to group
  InOutValue1 : DINT ;	//Data for user
  InOutValue2 : DINT ;	//Data for user
  OutWayConflict : BOOL ;	//Data for user
  OutSelectionOk : BOOL ;	//Data for user
  CmdNewValue : BOOL ;	//Data for user
  CmdNewJob : BOOL ;	//Data for user
  OutCodeOK : BOOL ;	//Data for user
  InEndOfJob : BOOL ;	//Data for user
  staWayOk : BOOL ;	//Data for user
  EdgeStartRelease : BOOL ;	//Data for user
  USW8 : BOOL ;	//Data for user
  USW9 : BOOL ;	//Data for user
  USW10 : BOOL ;	//Data for user
  USW11 : BOOL ;	//Data for user
  USW12 : BOOL ;	//Data for user
  USW13 : BOOL ;	//Data for user
  USW14 : BOOL ;	//Data for user
  USW15 : BOOL ;	//Data for user
  A_3258_MXZ11 : "GCS7_E_ValveControl";	// VLS 
  A_3259_MXZ11 : "GCS7_E_ValveControl";	// VLS 
END_VAR
VAR_TEMP
  tmpEnable : BOOL ;	
  tmpFlag : BOOL ;	
  tmpCmdNext : BOOL ;	
  tmpLocal : BOOL ;	
  tmpAspOk : BOOL ;	
  tmpHWStop : BOOL ;	
  tmpInstanzDBNo : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =Read command from group control
//Parameter
      SET   ; 
      S     "GCS7_GrCtrlDB".ParMGR[301]; 
      R     "GCS7_GrCtrlDB".ParWGR[301]; 
      R     "GCS7_GrCtrlDB".ParGGR[301]; 
      R     "GCS7_GrCtrlDB".ParFeed[301]; 
      R     "GCS7_GrCtrlDB".ParShare[301]; 
      S     #Gr.Par.ParMgr; 
      R     #Gr.Par.ParWgr; 
      R     #Gr.Par.ParGgr; 
//Command
      U     "GCS7_GrCtrlDB".OutEnable[301]; 
      =     #Gr.InEnableGr; 
      U     "GCS7_GrCtrlDB".OutOccupied[301]; 
      =     #Gr.InOccupied; 
      U     "GCS7_GrCtrlDB".CmdFaultReset[301]; 
      R     "GCS7_GrCtrlDB".CmdFaultReset[301]; 
      S     #Gr.CmdFaultReset; 
      U     "GCS7_GrCtrlDB".CmdNext[301]; 
      R     "GCS7_GrCtrlDB".CmdNext[301]; 
      S     #Gr.CmdNext; 

      U     "GCS7_GrCtrlDB".CmdManualOff[301]; 
      UN    #Gr.InManual; 
      S     #Gr.CmdManualOff; 
      U     "GCS7_GrCtrlDB".CmdManualOff[301]; 
      R     "GCS7_GrCtrlDB".CmdManualOff[301]; 

//      U     "GCS7_GrCtrlDB".CmdManualOff[301]
//      O     "GCS7_GrCtrlDB".OutOccupied[301]
//      R     "GCS7_GrCtrlDB".CmdManualOff[301]
//      S     #Gr.CmdManualOff

      U     "GCS7_GrCtrlDB".CmdContinue[301]; 
      R     "GCS7_GrCtrlDB".CmdContinue[301]; 
      S     #Gr.CmdContinue; 
//Init Line Number
      U     "GCS7_GrCtrlDB".ParShare[301]; 
      SPBN  WE00; 
      L     0; 
      T     #Gr.InLine; 
WE00: NOP   0; 
NETWORK
TITLE =Call Methode Control
// 1. Handle Interface PCControl      
// 2. Prepare mode --> ModePassive,ModeActive,ModeIdling      
// 3. Reset global flags of the group
      CALL #Gr (
           FcCode                   := 1);
NETWORK
TITLE =Restart Idling Time

      L     #Gr.Par.ParIdling; 
      L     2; 
      >=I   ; 
      SPB   IdTm; 
      T     #Gr.Par.ParIdling; 
IdTm: NOP   0; 
      UN    #A_3258_MXZ11.StLN; 
//      UN    #A_3259_MXZ11.StLN
      S     #Gr.CmdRestartIdling; 
NETWORK
TITLE =Prepare global flags

// Zu kalte Aussenluft
      U(    ; 
      L     0; // 0,00°C
      L     "G269M".A_3191_BTE11.OutValuePc; 
      >=I   ; 
      )     ; 
      U     "MAL1S033Par".Par.FlagProcess.ProcessActive; 
      S     "G301M".USW8; // Defrosterklappe AUF

// Aussenluft ausreichend warm
      U(    ; 
      L     200; // +2,00°C
      L     "G269M".A_3191_BTE11.OutValuePc; 
      <I    ; 
      )     ; 
      ON    "MAL1S033Par".Par.FlagProcess.ProcessActive; 
      R     "G301M".USW8; // Defrosterklappe AUF

NETWORK
TITLE =Set Pattern (Target position)



NETWORK
TITLE =Interlocking WayOk



NETWORK
TITLE =Interlocking Autostart

      CLR   ; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE =Interlocking Enable

      U     #Gr.InEnableGr; 
      =     #Gr.OutEnableEl; 

NETWORK
TITLE =Interlocking Next

      U     #Gr.CmdNext; 
      U     #Gr.OutAutostart; 
      UN    #Gr.OutNewWay; 
      =     #tmpCmdNext; 

NETWORK
TITLE =Interlocking Local / HWStop

      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #tmpHWStop; 

      U     #Gr.ModePassive; 
      O     #tmpHWStop; 
      =     #tmpLocal; 

NETWORK
TITLE ==A-3258-MXZ11 : LOUVER HEATING GLASS TUBE HEAT EXCHANGER

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      U     #A_3258_MXZ11.StLN; 
      =     #A_3258_MXZ11.InLocal; 
//
//-Interlocking---
      U     "G301M".USW8; // Defrosterklappe AUF
      =     #A_3258_MXZ11.InEnableHN; 
      NOT   ; 
      =     #A_3258_MXZ11.InEnableLN; 
//
      SET   ; 
      S     #A_3258_MXZ11.CmdNext; 
//
      U     #Gr.OutSeqStop; 
      =     #A_3258_MXZ11.InSeqStop; 
// 
//-Read inputs---
      U     "A_3258_BZS14_I1"; 
//      U     #A_3258_MXZ11.InEnableLN    //BRT01072010 - Klappe noch nicht eingebaut
      =     #A_3258_MXZ11.InpLN; 
      U     "A_3258_BZS15_I1"; 
//      U     #A_3258_MXZ11.InEnableHN    //BRT01072010 - Klappe noch nicht eingebaut
      =     #A_3258_MXZ11.InpHN; 
      X     "A_3258_MXZ11_O1"; 
      X     "A_3258_MXZ11_I1"; 
      O     ; 
      X     "A_3258_MXZ11_O2"; 
      X     "A_3258_MXZ11_I2"; 
      =     #A_3258_MXZ11.InpFaultDev; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #A_3258_MXZ11.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[101]; 
      =     #A_3258_MXZ11.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3258_MXZ11 ;
// 
//-Write outputs---
      U     #A_3258_MXZ11.OutpLN; 
      =     "A_3258_MXZ11_O1"; 
      U     #A_3258_MXZ11.OutpHN; 
      =     "A_3258_MXZ11_O2"; 
NETWORK
TITLE ==A-3259-MXZ11 : LOUVER HEATING GLASS TUBE HEAT EXCHANGER

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
//      U     #A_3259_MXZ11.StLN
      SET   ; 
      =     #A_3259_MXZ11.InLocal; 
//
//-Interlocking---
      U     "G301M".USW8; // Defrosterklappe AUF   
      CLR   ; 
      =     #A_3259_MXZ11.InEnableHN; 
      NOT   ; 
      =     #A_3259_MXZ11.InEnableLN; 
//
      SET   ; 
      S     #A_3259_MXZ11.CmdNext; 
//
      U     #Gr.OutSeqStop; 
//      U     #A_3259_MXZ11.StLN
      =     #A_3259_MXZ11.InSeqStop; 
// 
//-Read inputs---
      U     "A_3259_BZS14_I1"; 
      U     #A_3259_MXZ11.InEnableLN; //BRT01072010 - Klappe noch nicht eingebaut
      =     #A_3259_MXZ11.InpLN; 
      U     "A_3259_BZS15_I1"; 
      U     #A_3259_MXZ11.InEnableHN; //BRT01072010 - Klappe noch nicht eingebaut
      =     #A_3259_MXZ11.InpHN; 
      X     "A_3259_MXZ11_O1"; 
      X     "A_3259_MXZ11_I1"; 
      O     ; 
      X     "A_3259_MXZ11_O2"; 
      X     "A_3259_MXZ11_I2"; 
      =     #A_3259_MXZ11.InpFaultDev; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #A_3259_MXZ11.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[101]; 
      =     #A_3259_MXZ11.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3259_MXZ11 ;
// 
//-Write outputs---
      U     #A_3259_MXZ11.OutpLN; 
      =     "A_3259_MXZ11_O1"; 
      U     #A_3259_MXZ11.OutpHN; 
      =     "A_3259_MXZ11_O2"; 
NETWORK
TITLE =Test Fault

//BRT26062010 - noch nicht in Betrieb
      U     #A_3258_MXZ11.StFault; 
      O     #A_3259_MXZ11.StFault; 
      CLR   ; 
      =     #Gr.VarFault; 
NETWORK
TITLE =Test Warning

      CLR   ; 
      =     #Gr.OutWarning; 
NETWORK
TITLE =Test Stopped

//      U     #A_3258_MXZ11.StLN
//      U     #A_3259_MXZ11.StLN
//      UN    #Gr.VarFault
      UN    #Gr.VarStarted; 
      =     #Gr.VarStopped; 
NETWORK
TITLE =Test Started

//      U     #A_3258_MXZ11.StHN
//      U     #A_3259_MXZ11.StHN
//      UN    #Gr.VarStopped
      UN    #Gr.VarFault; 
      =     #Gr.VarStarted; 
NETWORK
TITLE =Enable Previous Group

      U     #Gr.VarStarted; 
      =     #Gr.OutEnablePrevGr; 
NETWORK
TITLE =Call Methode State
//1. Make State of the group: This function use the signals:
//        Gr.VarStopped, Gr.VarStarted, Gr.VarFault
//2.Reset Commands to group: e.g.CmdFaultReset
      CALL #Gr (
           FcCode                   := 2);
NETWORK
TITLE =Init

      SET   ; 
      R     #CmdNewValue; 
      R     #CmdNewJob; 
NETWORK
TITLE =Write data to group control

      U     #Gr.StStopped; 
      =     "GCS7_GrCtrlDB".InStopped[301]; 
      U     #Gr.StStarted; 
      =     "GCS7_GrCtrlDB".InStarted[301]; 
      U     #Gr.StFault; 
      =     "GCS7_GrCtrlDB".InFault[301]; 
      U     #Gr.OutWarning; 
      =     "GCS7_GrCtrlDB".InWarning[301]; 
      U     #Gr.OutManual; 
      =     "GCS7_GrCtrlDB".InManual[301]; 
      U     #Gr.OutHWStop; 
      =     "GCS7_GrCtrlDB".InHWStop[301]; 
      U     #Gr.OutDPFault; 
      =     "GCS7_GrCtrlDB".InDPFault[301]; 
      U     #Gr.OutLoaded; 
      =     "GCS7_GrCtrlDB".InLoaded[301]; 
      U     #Gr.OutAutostart; 
      =     "GCS7_GrCtrlDB".InAutoStart[301]; 
      U     #Gr.VarStartwarning; 
      =     "GCS7_GrCtrlDB".InStartWarning[301]; 
NETWORK
TITLE =End of block

      BE    ; 

END_FUNCTION_BLOCK

