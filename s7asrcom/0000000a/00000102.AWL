FUNCTION_BLOCK "G267M_Code"
TITLE =G267M UNLOADING / LOADING SCREW KILN 1
//COPYRIGHT:    (C) 2009  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      WinCoS_BIW01_09
//
//TITLE:        UNLOADING / LOADING SCREW KILN 1
//                Machine group control
//                Maschinengruppensteuerung
// 
//--------------------------------------------------------------------------------
// 
//IDENT:        AUTHOR:             DEPT:                              DATE:
// 
//P01_FB367.1.0 K.Renz              CSB41                              05.Okt.2009
//             1st Release
// 
//--------------------------------------------------------------------------------
// 
//PURPOSE: 
// 
//MANUAL : 
// 
//RESTRICTIONS: 
// 
//--------------------------------------------------------------------------------
AUTHOR : KR
FAMILY : USW
VERSION : 9.9


VAR
  Gr : "GCS7_GrpCtrlSub";	//Standard interface to group
  InOutValue1 : DINT ;	//Data for user
  InOutValue2 : DINT ;	//Data for user
  OutWayConflict : BOOL ;	//Data for user
  OutSelectionOk : BOOL ;	//Data for user
  CmdNewValue : BOOL ;	//Data for user
  CmdNewJob : BOOL ;	//Data for user
  OutCodeOK : BOOL ;	//Data for user
  InEndOfJob : BOOL ;	//Data for user
  staWayOk : BOOL ;	//Data for user
  EdgeStartRelease : BOOL ;	//Data for user
  USW8 : BOOL ;	//Data for user
  USW9 : BOOL ;	//Data for user
  USW10 : BOOL ;	//Data for user
  USW11 : BOOL ;	//Data for user
  USW12 : BOOL ;	//Data for user
  USW13 : BOOL ;	//Data for user
  USW14 : BOOL ;	//Data for user
  USW15 : BOOL ;	//Data for user
  A_3111_MXZ11 : "GCS7_E_Motor";	// Motor 
  A_3111_UM011VA_PD1 : "GCS7_E_AnalogInput";	// AnaInp 
  A_3111_UM011VA_PD2 : "GCS7_E_AnalogInput";	// AnaInp 
  A_3111_UM011VA_COM : "SSA_SEW_MOVIDRIVE";	// FU_Siemens 
  A_3111_MXZ11_FU : "GCS7_E_Motor";	// Motor 
END_VAR
VAR_TEMP
  tmpEnable : BOOL ;	
  tmpFlag : BOOL ;	
  tmpCmdNext : BOOL ;	
  tmpLocal : BOOL ;	
  tmpAspOk : BOOL ;	
  tmpHWStop : BOOL ;	
  tmpInstanzDBNo : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =Read command from group control
//Parameter
      SET   ; 
      S     "GCS7_GrCtrlDB".ParMGR[267]; 
      R     "GCS7_GrCtrlDB".ParWGR[267]; 
      R     "GCS7_GrCtrlDB".ParGGR[267]; 
      R     "GCS7_GrCtrlDB".ParFeed[267]; 
      R     "GCS7_GrCtrlDB".ParShare[267]; 
      S     #Gr.Par.ParMgr; 
      R     #Gr.Par.ParWgr; 
      R     #Gr.Par.ParGgr; 
//Command
      U     "GCS7_GrCtrlDB".OutEnable[267]; 
      =     #Gr.InEnableGr; 
      U     "GCS7_GrCtrlDB".OutOccupied[267]; 
      =     #Gr.InOccupied; 
      U     "GCS7_GrCtrlDB".CmdFaultReset[267]; 
      R     "GCS7_GrCtrlDB".CmdFaultReset[267]; 
      S     #Gr.CmdFaultReset; 
      U     "GCS7_GrCtrlDB".CmdNext[267]; 
      R     "GCS7_GrCtrlDB".CmdNext[267]; 
      S     #Gr.CmdNext; 

      U     "GCS7_GrCtrlDB".CmdManualOff[267]; 
      UN    #Gr.InManual; 
      S     #Gr.CmdManualOff; 
      U     "GCS7_GrCtrlDB".CmdManualOff[267]; 
      R     "GCS7_GrCtrlDB".CmdManualOff[267]; 

//      U     "GCS7_GrCtrlDB".CmdManualOff[267]
//      O     "GCS7_GrCtrlDB".OutOccupied[267]
//      R     "GCS7_GrCtrlDB".CmdManualOff[267]
//      S     #Gr.CmdManualOff

      U     "GCS7_GrCtrlDB".CmdContinue[267]; 
      R     "GCS7_GrCtrlDB".CmdContinue[267]; 
      S     #Gr.CmdContinue; 
//Init Line Number
      U     "GCS7_GrCtrlDB".ParShare[267]; 
      SPBN  WE00; 
      L     0; 
      T     #Gr.InLine; 
WE00: NOP   0; 
NETWORK
TITLE =Call Methode Control
// 1. Handle Interface PCControl      
// 2. Prepare mode --> ModePassive,ModeActive,ModeIdling      
// 3. Reset global flags of the group
      CALL #Gr (
           FcCode                   := 1);
NETWORK
TITLE =Restart Idling Time

      L     #Gr.Par.ParIdling; 
      L     2; 
      >=I   ; 
      SPB   IdTm; 
      T     #Gr.Par.ParIdling; 
IdTm: NOP   0; 
      CLR   ; 
      S     #Gr.CmdRestartIdling; 
NETWORK
TITLE =Prepare global flags



NETWORK
TITLE =Set Pattern (Target position)



NETWORK
TITLE =Interlocking WayOk



NETWORK
TITLE =Interlocking Autostart

      UN    "G268M".Gr.OutAutostart; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE =Interlocking Enable

      U(    ; 
      U(    ; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
      U     "G268M".A_3115_BZS14.StTrue; // UNLOADING DOOR CLOSED
      O     ; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepUnloading; 
      U     "G268M".A_3115_BZS15.StTrue; // UNLOADING DOOR OPENED
      O     ; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepUnloading; 
      UN    "MAL1S032Par".Par.FlagFunction_1.FinishUnloading; 
      U     "MAL1S032Par".Par.FlagMachine.WEGMESSUNG_0; 
      O     ; 
      U     "G266M".TurningTo_Parkposition; 
      U     "MAL1S032Par".Par.FlagFunction_1.LoadEnd; 
      U     "MAL1S032".PCRead.Step.StActive; 
      )     ; 
      O     #Gr.InManual; 
      )     ; 
      U     #Gr.InEnableGr; 
      =     #Gr.OutEnableEl; 

NETWORK
TITLE =Interlocking Next

      U     #Gr.CmdNext; 
      U     #Gr.OutAutostart; 
      UN    #Gr.OutNewWay; 
      =     #tmpCmdNext; 

NETWORK
TITLE =Interlocking Local / HWStop

      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #tmpHWStop; 

      U     #Gr.ModePassive; 
      O     #tmpHWStop; 
      =     #tmpLocal; 

NETWORK
TITLE =Interlocking Start Warning

      NOP   0; 
//- Restart after fault --
      U     #Gr.CmdFaultReset; 
      U     #Gr.VarFault; 
      S     "SYS_StartWarningDB".Area[10].CmdResetStartWarning; 
//- Run Timer --
      AUF   "G267M"; 
      CALL "GCS7_StartWarning" , "SYS_StartWarningDB" (
           InModeHigher             := FALSE,
           InArea                   := 10,
           InEnable                 := #Gr.OutEnableEl,
           InLocal                  := #tmpLocal,
           InStarted                := #Gr.VarStarted,
           InCmdNext                := #tmpCmdNext,
           OutCmdNext               := #tmpCmdNext,
           OutStartwarning          := #Gr.VarStartwarning);
//- Store Startwarning done  --
//- Reset Autostart --
      U     "SYS_StartWarningDB".TmpArea.OutStartReleaseRunningL; 
      FN    #EdgeStartRelease; 
      U     #Gr.VarFault; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE ==A-3111-MXZ11_FU : FU UNLOADING / LOADING SREW (Netzschütz)
//Dieses Element dient nur fuer die Visualisierung!!
      NOP   0; 
//-InLocal--- 
      U     #tmpLocal; 
      =     #A_3111_MXZ11_FU.InLocal; 
//
//-Interlocking--- 
      U     "G267M".A_3111_UM011VA_COM.Input.StateWord_1.BETRIEBSBEREIT; 
      =     #A_3111_MXZ11_FU.InEnableFwd; 
// 
      SET   ; 
      S     #A_3111_MXZ11_FU.CmdNext; 
// 
      U     #Gr.OutSeqStop; 
      =     #A_3111_MXZ11_FU.InSeqStop; 
// 
//-Read inputs---
//      U     "A_3111_MXZ11_I1"
      U     "G267M".A_3111_UM011VA_COM.Input.StateWord_1.BETRIEBSBEREIT; 
      U     #A_3111_MXZ11_FU.OutpRunFwd; 
      =     #A_3111_MXZ11_FU.InpRunFwd; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_MXZ11_FU.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[91]; 
      =     #A_3111_MXZ11_FU.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3111_MXZ11_FU ;
// 
//-Write outputs---
//      U     #A_3111_MXZ11_FU.OutpRunFwd

// Netzschuetz "EIN"
      U     #Gr.OutEnableEl; 
      O     #A_3111_MXZ11.InManual; 
      O     #Gr.InManual; 
      =     "A_3111_MXZ11_O1"; 
NETWORK
TITLE ==A-3111-MXZ11 : UN-/LOADING SCREW KILN

      NOP   0; 
//-InLocal--- 
      U     #tmpLocal; 
      =     #A_3111_MXZ11.InLocal; 
//
//-Interlocking--- 
//-Load direction---
      U(    ; 
      U     "MAL1S032".ToASW.InfDataReady; 
      U(    ; 
      O     "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
      O     "MAL1S032Par".Par.FlagMachine.ImpulsNullstellung; 
      )     ; 
      UN    "G266M".A_3111_MXZ41MA.A_3111_BLH13.StFault; // Product sensor
      UN    "G266M".Gr.StFault; 
      )     ; 
      O(    ; 
      U     #Gr.InManual; 
      U     #Gr.InOutAutostartMan; 
      U     #USW14; // ILO-cmd run fwd
      )     ; 
      UN    #A_3111_UM011VA_PD2.OutFaulted; 
      U     #A_3111_MXZ11_FU.StStartedFwd; 
      U     #Gr.OutEnableEl; 
      =     #A_3111_MXZ11.InEnableFwd; 

//-Unload direction---
      O(    ; 
      U     "MAL1S032".ToASW.InfDataReady; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepUnloading; 
      U     DB368.DBX   99.2; // Discharge door open
      )     ; 
      O(    ; 
      U     DB368.DBX   99.2; // Discharge door open
      U     #Gr.InManual; 
      U     #Gr.InOutAutostartMan; 
      U     #USW15; // ILO-Mode man rev
      )     ; 
      UN    #A_3111_UM011VA_PD2.OutFaulted; 
      U     #A_3111_MXZ11_FU.StStartedFwd; 
      U     #Gr.OutEnableEl; 
      =     #A_3111_MXZ11.InEnableRev; 
// 
      U     #tmpCmdNext; 
      U(    ; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
      U     "G265M".A_3111_MXZ21_31MA.Ma.StAdjusted; 
      O     "MAL1S032Par".Par.FlagMachine.ImpulsNullstellung; 
      O     "MAL1S032Par".Par.FlagFunction_1.StepUnloading; 
      O     #Gr.InManual; 
      )     ; 
      S     #A_3111_MXZ11.CmdNext; 
// 
      U     #Gr.OutSeqStop; 
      =     #A_3111_MXZ11.InSeqStop; 
// 
//-Read inputs---
      U     #A_3111_MXZ11.OutpRunRev; 
      U     #A_3111_UM011VA_COM.OutRunningRev; 
      U     "A_3111_MXZ11_I1"; // Netzschuetz "EIN"
      =     #A_3111_MXZ11.InpRunRev; 
      U     #A_3111_MXZ11.OutpRunFwd; 
      U     #A_3111_UM011VA_COM.OutRunningFwd; 
      U     "A_3111_MXZ11_I1"; // Netzschuetz "EIN"
      =     #A_3111_MXZ11.InpRunFwd; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_MXZ11.InHWStop; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3111_MXZ11 ;
// 
//-Write outputs---
      U     #A_3111_MXZ11.OutpRunFwd; 
      R     #A_3111_MXZ11.InOutManRev; 
      U     #A_3111_MXZ11.OutpRunRev; 
      S     #A_3111_MXZ11.InOutManRev; 

NETWORK
TITLE ==A-3111-UM011VA_COM : FREQENCY INVERTOR UN/LOADING SCREW
//Von der Werkseinstellung weichen folgende Parameter ab:
//P100/101 Sollwertquelle/Steuerquelle => SBUS 1;
//P881 SBUS-Adresse <> 0; siehe Handbuch "Feldbusschnittstelle DFP21B"
//P872 Sollwertbeschreibung PA3 => Rampe
//=> P876 PA-Daten freigeben = EIN
//"Drive_IO_Address": Anfangsadresse des E/A-Bereichs => Hardwarekonfiguration
//
//Eingänge der FC:
//Freigabe des Antriebes:
//Freigabe via dig. Eingänge (Rechts/Halt & Freigabe/Stop) & 
//Freigabe via Steuerwort (Enable_rapid_stop & Enable)
//"Reset" = true: Fehler quittieren
//"SETPOINT_SPEED": Vorgabe der Sollgeschwindigkeit in rpm (Integer),
//"RAMP": Vorgabe der Rampe in ms (Integer)
//
//Ausgänge der FC:
//Ist die Datenübertragung/PROFIBUS gestört, so werden alle Ausgänge abgelöscht.
//=> "Kommuikation_OK" = false (selbstquittierend)
//"INVERTER_READY" = true: Es liegt kein Fehler vor & Netzspannung EIN
//"FAULT_OF_AXIS" = true: Ein Fehler steht an.
//"FAULT_NO" <> 0: Die Fehlercodes sind im Systemhandbuch dokumentiert und werden 
//in den MOVITOOLS (Status, Busmonitor)angezeigt.
//"ACTUAL_SPEED": Istgeschwindigkeit (INT) in rpm
//"OUTPUT_CURRENT": Scheinstrom (INT) in Prozent vom Gerätenennstrom
      NOP   0; 
      U     #A_3111_MXZ11.OutpRunFwd; 
      =     #A_3111_UM011VA_COM.InEnableFwd; 
      U     #A_3111_MXZ11.OutpRunRev; 
      =     #A_3111_UM011VA_COM.InEnableRev; 

      U     #Gr.CmdFaultReset; 
      S     #A_3111_UM011VA_COM.CmdFaultReset; 
//
// 
//-Read inputs---
// 
//-Set Parameters---
      L     600; // Einricht Drehzahl=3.00Hz Nmax=50Hz
      U     #A_3111_MXZ11.InManual; 
      SPB   TaSp; 
//-Fast by big product shift, slow by little produkt shift---
// Speed for 1. loading level
      U(    ; 
      L     "MAL1S032Par".RecipeHeader.Loading.LoadingSpeed_1; 
      L     "MAL1S032Par".Par.MachineValue.SollwertFahrantrieb; 
      ==I   ; 
      )     ; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
      L     10000; // 100.00% Nmax = 50.00Hz
      SPB   TaSp; 

// Speed for 2. loading level
      U(    ; 
      L     "MAL1S032Par".RecipeHeader.Loading.LoadingSpeed_2; 
      L     "MAL1S032Par".Par.MachineValue.SollwertFahrantrieb; 
      ==I   ; 
      )     ; 
      L     10000; // 100.00% Nmax = 50.00Hz
      SPB   TaSp; 

// Speed for 3. loading level
      U(    ; 
      L     "G266M".A_3111_MXZ41MA.Ma.OutCntRound; 
      L     L#2; 
      ==D   ; 
      )     ; 
      UN    "MAL1S032Par".Par.Switches1.StaOn03; // LiftingPositionRound_4
      U(    ; 
      L     87; // 87%
      L     "MAL1S032Par".Par.MachineValue.SollwertFahrantrieb; 
      ==I   ; 
      )     ; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
      L     10000; // 100.00% Nmax = 50.00Hz 
      SPB   TaSp; 


// Speed for 4. and more loading level
      U     "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
      U     "G266M".A_3111_MXZ41MA.A_3111_MXZ41.OutpRunFwd; 
      U(    ; 
      L     87; // 87%
      L     "MAL1S032Par".Par.MachineValue.SollwertFahrantrieb; 
      ==I   ; 
      )     ; 
      L     L#6000; // 60.00% Nmax = 30.00Hz 
      SPB   TaSp; 

      L     10000; // 100.00% Nmax = 50.00Hz
      U     "MAL1S032Par".Par.FlagFunction_1.StepUnloading; 
      SPB   TaSp; 
      L     10000; // 100.00% Nmax = 50.00Hz
TaSp: T     #A_3111_UM011VA_COM.OutValuePC; 
// 
//-Call object control---
      CALL #A_3111_UM011VA_COM ;
// 
//-Simu Inhousetest---
      U     "PLC_SystemDB".GlobalFlags.StaTest; 
      SPBN  NCo2; 
      L     #A_3111_UM011VA_COM.Output.SetpointValue01; 
      T     #A_3111_UM011VA_COM.Input.ActualValue01; 
      L     #A_3111_UM011VA_COM.OutValuePC; 
      T     #A_3111_UM011VA_COM.InValuePC; 


      L     1300; // Nennstrom
      T     #A_3111_UM011VA_COM.Input.ActualValue02; 

      U     #A_3111_MXZ11.OutpRunFwd; 
      O     #A_3111_MXZ11.OutpRunRev; 
      SPB   NCo2; 
      L     0; 
      T     #A_3111_UM011VA_COM.Input.ActualValue01; 
      T     #A_3111_UM011VA_COM.Input.ActualValue02; 
NCo2: NOP   0; 
//-Write outputs---
      U     "PLC_SystemDB".GlobalFlags.StaTest; 
      SPB   NCo1; 
      SPA   NCOL; 
NCo1: NOP   0; 
// Check if the inverter is working
      U     #A_3111_UM011VA_COM.Input.StateWord_1.STATE_WORD_BIT_15; 
      U     #A_3111_UM011VA_COM.Input.StateWord_1.BETRIEBSBEREIT; 
      UN    #A_3111_UM011VA_COM.Input.StateWord_1.STOERUNG_WARNUNG; 
      O     "PLC_SystemDB".GlobalFlags.StaTest; // JV190908
      U     #A_3111_MXZ11.OutpRunFwd; 
      =     #A_3111_UM011VA_COM.OutRunningFwd; 

      U     #A_3111_UM011VA_COM.Input.StateWord_1.STATE_WORD_BIT_15; 
      U     #A_3111_UM011VA_COM.Input.StateWord_1.BETRIEBSBEREIT; 
      UN    #A_3111_UM011VA_COM.Input.StateWord_1.STOERUNG_WARNUNG; 
      O     "PLC_SystemDB".GlobalFlags.StaTest; // JV190908
      U     #A_3111_MXZ11.OutpRunRev; 
      =     #A_3111_UM011VA_COM.OutRunningRev; 
NCOL: NOP   0; 

NETWORK
TITLE ==A-3111-UM011VA_PD1 : FREQUENCY FREQ. UN-/LOADING SCREW KILN

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      SET   ; 
      =     #A_3111_UM011VA_PD1.InLocal; 

//-Monitoring--- 
      U     #A_3111_MXZ11.StStartedFwd; 
      O     #A_3111_MXZ11.StStartedRev; 
      =     #A_3111_UM011VA_PD1.InMonLL; 
      =     #A_3111_UM011VA_PD1.InMonL; 
      CLR   ; 
      =     #A_3111_UM011VA_PD1.InMonMid; 
      CLR   ; 
      =     #A_3111_UM011VA_PD1.InMonH; 
      U     #A_3111_MXZ11.StStartedFwd; 
      O     #A_3111_MXZ11.StStartedRev; 
      =     #A_3111_UM011VA_PD1.InMonHH; 
// 
//-Read inputs---
      L     #A_3111_UM011VA_COM.InValuePC; // FU
      T     #A_3111_UM011VA_PD1.InpValue; 
      U     "PLC_SystemDB".HardwareStop.Germinating_1; 
      =     #A_3111_UM011VA_PD1.InHWStop; 
// 
//-Set Parameters---
// 4000H=100.00%=1500rpm=50.00Hz
//[2.0] - Erläuterung
      L     L#27648; // InpValue=Drehzahl direkt   
      T     "G267M".A_3111_UM011VA_PD1.Par.ParUnitsByMaxDigit; 
      L     L#10000; // OutValuePc=OutValueUnits
      T     "G267M".A_3111_UM011VA_PD1.Par.ParUnitsBy100Pc; 
      L     L#0; // 0.00%=0.00Hz                 
      T     "G267M".A_3111_UM011VA_PD1.Par.ParLimitLL; 
      L     L#600; // 6.00%=3.00Hz
      T     "G267M".A_3111_UM011VA_PD1.Par.ParLimitL; 
      L     L#10000; // 100.00%=50.00Hz
      T     "G267M".A_3111_UM011VA_PD1.Par.ParLimitH; 
      L     L#10100; // 101.00%=50.50Hz
      T     "G267M".A_3111_UM011VA_PD1.Par.ParLimitHH; 
// 
//-Call object control---
      CALL #A_3111_UM011VA_PD1 ;
// 
//-Write outputs---

NETWORK
TITLE ==A-3111-UM011VA_PD2 : CURRENT FREQ. UN-/LOADING SCREW KILN

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      SET   ; 
      =     #A_3111_UM011VA_PD2.InLocal; 
//
//-Monitoring--- 
      U     #A_3111_MXZ11.StStartedFwd; 
      O     #A_3111_MXZ11.StStartedRev; 
      =     #A_3111_UM011VA_PD2.InMonLL; 
      =     #A_3111_UM011VA_PD2.InMonL; 
      CLR   ; 
      =     #A_3111_UM011VA_PD2.InMonMid; 
      CLR   ; 
      =     #A_3111_UM011VA_PD2.InMonH; 
      CLR   ; 
      =     #A_3111_UM011VA_PD2.InMonHH; 
// 
//-Read inputs---
      L     #A_3111_UM011VA_COM.Input.ActualValue02; // FU
      T     #A_3111_UM011VA_PD2.InpValue; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_UM011VA_PD2.InHWStop; 
// 
//-Set Parameters---
// 1000Dez=100,0%=74,0A (In FU)
// 27648Dez = 1658.9A
// --> Erläuterung
      L     L#16589; // Stromwert bei 27648Dez
      T     "G267M".A_3111_UM011VA_PD2.Par.ParUnitsByMaxDigit; 
      L     L#10000; // OutValuePc=OutValueUnits
      T     "G267M".A_3111_UM011VA_PD2.Par.ParUnitsBy100Pc; 
      L     L#0; // 0.0A
      T     "G267M".A_3111_UM011VA_PD2.Par.ParLimitLL; 
      L     L#160; // 16.0A
      T     "G267M".A_3111_UM011VA_PD2.Par.ParLimitL; 
//    L     L#740                       // 74.0A=100.0% (In FU)
//    T     "G267M".A_3111_UM011VA_PD2.Par.ParLimitH
      L     L#962; // 96.2A=130.0% (In FU)
      T     "G267M".A_3111_UM011VA_PD2.Par.ParLimitHH; 
// 

      U     "MAL1S032".ToASW.InfDataReady; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
      L     L#814; // 81.4 Ampere
      SPB   pH; 
      L     L#840; // 84.0 Ampere
pH:   T     #A_3111_UM011VA_PD2.Par.ParLimitH; 
// 
//-Call object control---
      CALL #A_3111_UM011VA_PD2 ;
// 
//-Write outputs---
NETWORK
TITLE =Test Fault

      U     #A_3111_MXZ11_FU.StFault; 
      O     #A_3111_MXZ11.StFault; 
      O     #A_3111_UM011VA_PD1.OutFaulted; 
      O     #A_3111_UM011VA_PD2.OutFaulted; 
      =     #Gr.VarFault; 
NETWORK
TITLE =Test Warning

      U     #A_3111_UM011VA_PD1.OutWarning; 
      O     #A_3111_UM011VA_PD2.OutWarning; 
      =     #Gr.OutWarning; 
NETWORK
TITLE =Test Stopped

      U     #A_3111_MXZ11_FU.StStopped; 
      U     #A_3111_MXZ11.StStopped; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStopped; 
NETWORK
TITLE =Test Started

      U     #A_3111_MXZ11_FU.StStartedFwd; 
      U(    ; 
      O     #A_3111_MXZ11.StStartedFwd; 
      O     #A_3111_MXZ11.StStartedRev; 
      )     ; 
      UN    #Gr.VarStopped; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStarted; 
NETWORK
TITLE =Enable Previous Group

      U     #Gr.VarStarted; 
      =     #Gr.OutEnablePrevGr; 
NETWORK
TITLE =Call Methode State
//1. Make State of the group: This function use the signals:
//        Gr.VarStopped, Gr.VarStarted, Gr.VarFault
//2.Reset Commands to group: e.g.CmdFaultReset
      CALL #Gr (
           FcCode                   := 2);
NETWORK
TITLE =Init

      SET   ; 
      R     #CmdNewValue; 
      R     #CmdNewJob; 
NETWORK
TITLE =Write data to group control

      U     #Gr.StStopped; 
      =     "GCS7_GrCtrlDB".InStopped[267]; 
      U     #Gr.StStarted; 
      =     "GCS7_GrCtrlDB".InStarted[267]; 
      U     #Gr.StFault; 
      =     "GCS7_GrCtrlDB".InFault[267]; 
      U     #Gr.OutWarning; 
      =     "GCS7_GrCtrlDB".InWarning[267]; 
      U     #Gr.OutManual; 
      =     "GCS7_GrCtrlDB".InManual[267]; 
      U     #Gr.OutHWStop; 
      =     "GCS7_GrCtrlDB".InHWStop[267]; 
      U     #Gr.OutDPFault; 
      =     "GCS7_GrCtrlDB".InDPFault[267]; 
      U     #Gr.OutLoaded; 
      =     "GCS7_GrCtrlDB".InLoaded[267]; 
      U     #Gr.OutAutostart; 
      =     "GCS7_GrCtrlDB".InAutoStart[267]; 
      U     #Gr.VarStartwarning; 
      =     "GCS7_GrCtrlDB".InStartWarning[267]; 
NETWORK
TITLE =End of block

      BE    ; 

END_FUNCTION_BLOCK

