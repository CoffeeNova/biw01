FUNCTION_BLOCK "G265M_Code"
TITLE =G265M LIFTING DEVICE KILN 1
//COPYRIGHT:    (C) 2009  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      WinCoS_BIW01_09
//
//TITLE:        LIFTING DEVICE KILN 1
//                Machine group control
//                Maschinengruppensteuerung
// 
//--------------------------------------------------------------------------------
// 
//IDENT:        AUTHOR:             DEPT:                              DATE:
// 
//P01_FB365.1.0 K.Renz              CSB41                              10.Nov.2009
//             1st Release
// 
//--------------------------------------------------------------------------------
// 
//PURPOSE: 
// 
//MANUAL : 
// 
//RESTRICTIONS: 
// 
//--------------------------------------------------------------------------------
AUTHOR : KR
FAMILY : USW
VERSION : 9.9


VAR
  Gr : "GCS7_GrpCtrlSub";	//Standard interface to group
  InOutValue1 : DINT ;	//Data for user
  InOutValue2 : DINT ;	//Data for user
  OutWayConflict : BOOL ;	//Data for user
  OutSelectionOk : BOOL ;	//Data for user
  CmdNewValue : BOOL ;	//Data for user
  CmdNewJob : BOOL ;	//Data for user
  OutCodeOK : BOOL ;	//Data for user
  InEndOfJob : BOOL ;	//Data for user
  staWayOk : BOOL ;	//Data for user
  EdgeStartRelease : BOOL ;	//Data for user
  USW8 : BOOL ;	//Data for user
  USW9 : BOOL ;	//Data for user
  USW10 : BOOL ;	//Data for user
  USW11 : BOOL ;	//Data for user
  USW12 : BOOL ;	//Data for user
  USW13 : BOOL ;	//Data for user
  USW14 : BOOL ;	//Data for user
  USW15 : BOOL ;	//Data for user
  A_3111_MXZ21_31MA : "A_3111_MXZ21_31MA_Code";	// LiftingDevice 
END_VAR
VAR_TEMP
  tmpEnable : BOOL ;	
  tmpFlag : BOOL ;	
  tmpCmdNext : BOOL ;	
  tmpLocal : BOOL ;	
  tmpAspOk : BOOL ;	
  tmpHWStop : BOOL ;	
  tmpInstanzDBNo : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =Read command from group control
//Parameter
      SET   ; 
      S     "GCS7_GrCtrlDB".ParMGR[265]; 
      R     "GCS7_GrCtrlDB".ParWGR[265]; 
      R     "GCS7_GrCtrlDB".ParGGR[265]; 
      R     "GCS7_GrCtrlDB".ParFeed[265]; 
      R     "GCS7_GrCtrlDB".ParShare[265]; 
      S     #Gr.Par.ParMgr; 
      R     #Gr.Par.ParWgr; 
      R     #Gr.Par.ParGgr; 
//Command
      U     "GCS7_GrCtrlDB".OutEnable[265]; 
      =     #Gr.InEnableGr; 
      U     "GCS7_GrCtrlDB".OutOccupied[265]; 
      =     #Gr.InOccupied; 
      U     "GCS7_GrCtrlDB".CmdFaultReset[265]; 
      R     "GCS7_GrCtrlDB".CmdFaultReset[265]; 
      S     #Gr.CmdFaultReset; 
      U     "GCS7_GrCtrlDB".CmdNext[265]; 
      R     "GCS7_GrCtrlDB".CmdNext[265]; 
      S     #Gr.CmdNext; 

      U     "GCS7_GrCtrlDB".CmdManualOff[265]; 
      UN    #Gr.InManual; 
      S     #Gr.CmdManualOff; 
      U     "GCS7_GrCtrlDB".CmdManualOff[265]; 
      R     "GCS7_GrCtrlDB".CmdManualOff[265]; 

//      U     "GCS7_GrCtrlDB".CmdManualOff[265]
//      O     "GCS7_GrCtrlDB".OutOccupied[265]
//      R     "GCS7_GrCtrlDB".CmdManualOff[265]
//      S     #Gr.CmdManualOff

      U     "GCS7_GrCtrlDB".CmdContinue[265]; 
      R     "GCS7_GrCtrlDB".CmdContinue[265]; 
      S     #Gr.CmdContinue; 
//Init Line Number
      U     "GCS7_GrCtrlDB".ParShare[265]; 
      SPBN  WE00; 
      L     0; 
      T     #Gr.InLine; 
WE00: NOP   0; 
NETWORK
TITLE =Call Methode Control
// 1. Handle Interface PCControl      
// 2. Prepare mode --> ModePassive,ModeActive,ModeIdling      
// 3. Reset global flags of the group
      CALL #Gr (
           FcCode                   := 1);
NETWORK
TITLE =Restart Idling Time

      L     #Gr.Par.ParIdling; 
      L     2; 
      >=I   ; 
      SPB   IdTm; 
      T     #Gr.Par.ParIdling; 
IdTm: NOP   0; 
      CLR   ; 
      S     #Gr.CmdRestartIdling; 
NETWORK
TITLE =Prepare global flags



NETWORK
TITLE =Set Pattern (Target position)

      U     #Gr.InManual; 
      SPB   ILO6; 
      L     "MAL1S032Par".Par.MachineValue.SOLLWERT_HUBZUG1_HOEHE; 
      L     "MAL1S032Par".Par.MachineValue.SOLLWERT_HUBZUG2_HOEHE; 
      -I    ; 
      SPPZ  H1H; 
      L     "MAL1S032Par".Par.MachineValue.SOLLWERT_HUBZUG2_HOEHE; 
      SPA   IPT; 
H1H:  L     "MAL1S032Par".Par.MachineValue.SOLLWERT_HUBZUG1_HOEHE; 
IPT:  T     #A_3111_MXZ21_31MA.Ma.InPositionTarget; 
      SPA   En06; 
ILO6: NOP   0; 
      L     "MAL1S032Par".Par.ParIntProcess.TarPos_ILO_LiftDev; 
      T     #A_3111_MXZ21_31MA.Ma.InPositionTarget; 
En06: NOP   0; 
//-Schief beladen---
      L     0; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
//MW 2010.04.08
      O     ; 
      U     "MAL1S032Par".Par.FlagProcess.ProcessActive; 
      U     "MAL1S032Par".Par.FlagMachine.WEGMESSUNG_0_FLAG; 
      O     ; 
      U     "MAL1S032Par".Par.FlagFunction_1.FinishLoading; 
      U     "MAL1S032Par".Par.FlagMachine.WEGMESSUNG_0_FLAG; 
      SPBN  BLS0; 
      L     "MAL1S032Par".RecipeHeader.Loading.BankingLoadingScrew; 
BLS0: T     #A_3111_MXZ21_31MA.Ma.BankingLoadingScrew; 
NETWORK
TITLE =Interlocking WayOk



NETWORK
TITLE =Interlocking Autostart

      CLR   ; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE =Interlocking Enable

      U     #Gr.InEnableGr; 
      =     #Gr.OutEnableEl; 

NETWORK
TITLE =Interlocking Next

      U     #Gr.CmdNext; 
      U     #Gr.OutAutostart; 
      UN    #Gr.OutNewWay; 
      =     #tmpCmdNext; 

NETWORK
TITLE =Interlocking Local / HWStop

      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #tmpHWStop; 

      U     #Gr.ModePassive; 
      O     #tmpHWStop; 
      =     #tmpLocal; 

NETWORK
TITLE ==A-3111-MXZ21/31MA : LIFTING DRIVE 

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      =     #A_3111_MXZ21_31MA.Ma.InLocal; 
//
//-Interlocking---
      U     #Gr.OutEnableEl; 
//MW 2010.04.08      UN    "G266M".TurningTo_Parkposition
      =     #A_3111_MXZ21_31MA.Ma.InEnableUp; 
      U(    ; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
      U     "G267M".A_3111_MXZ11.StStartedFwd; //MW 2010.04.08
      )     ; 
      O(    ; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepUnloading; 
      U     "G267M".A_3111_MXZ11.StStartedRev; 
      O     ; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepUnloading; 
      U     "MAL1S032Par".Par.FlagFunction_1.FinishUnloading; 
      U     "G266M".A_3111_MXZ41MA.Ma.OutAdjusted; 
      )     ; 
      O(    ; 
      UN    "MAL1S032Par".Par.FlagFunction_1.StepLoading; 
      UN    "MAL1S032Par".Par.FlagFunction_1.StepUnloading; 
      )     ; 
      O     #Gr.InOutAutostartMan; // ILO-mode
      U     #Gr.OutEnableEl; 
      =     #A_3111_MXZ21_31MA.Ma.InEnableDown; 
// 
      U     #tmpCmdNext; 
      S     #A_3111_MXZ21_31MA.Ma.CmdNext; 
// 
      SET   ; 
      =     #A_3111_MXZ21_31MA.Ma.InInterlocked; 
// 
// Hubzüge nur bis min Position wenn senken, wenn die Ausräumklappe geschlossen ist
      U(    ; 
      U     #A_3111_MXZ21_31MA.Ma.OutHigherMinPos; 
      O     "G268M".A_3115_BZS15.InpTrue; // Discharge door open
      )     ; 
      =     #A_3111_MXZ21_31MA.Ma.InEnableLowerMinPos; // #Ma.InEnableLowerMinPos
// 
      UN    "PLC_DPInfo".Master[1].SlavesError[91]; 
      U     "SYS_infLog1"; // Zaehlerkarten-Spannungsueberwachung
//      SET                               // Keine Zaehlerkarte vorhanden !! 
      =     #A_3111_MXZ21_31MA.Ma.InPowerOk; 
// 
      U     #A_3111_MXZ21_31MA.Ma.InPowerOk; 
      L     S5T#15S; // Zeit zum Initialisierung
      SE    T    365; 
      U     T    365; 
      =     #A_3111_MXZ21_31MA.Ma.InPowerOkDelayDone; 
// 
//-Counter calibration by startup---
//      U     "ENG_RemarkFlag"
      CLR   ; 
      =     #A_3111_MXZ21_31MA.Ma.InCalibration; // #Ma.InCalibration
//      U     "ENG_RemarkFlag"
      CLR   ; 
      =     #A_3111_MXZ21_31MA.Ma.InCalibrationZero; // #Ma.InCalibrationZero
// 
//-Unloading mode---
      U     "MAL1S032".PCRead.Step.StEmptying; 
      U     "MAL1S032Par".Par.FlagFunction_1.StepUnloading; // Step unloading
      UN    "MAL1S032Par".Par.FlagFunction_1.FinishUnloading; 
      UN    "MAL1S032Par".Par.FlagFunction_0.ScrewAboveProductLayer; 
      UN    #Gr.InOutAutostartMan; 
//MW 2010.04.08      CLR   
      =     #A_3111_MXZ21_31MA.Ma.InUnloading; 
// 
//=Values and commands of elements===
//-=A-3111-BZS23---
//      CLR
//      S     #A_3111_MXZ21_31MA.A_3111_BZS23.CmdRunTimeOutTime
//-=A-3111-BZS33---
//      CLR
//      S     #A_3111_MXZ21_31MA.A_3111_BZS33.CmdRunTimeOutTime
//-=A-3111-QYS22---
//      CLR
//      S     #A_3111_MXZ21_31MA.A_3111_QYS22.CmdRunTimeOutTime
//-=A-3111-QYS32---
//      CLR
//      S     #A_3111_MXZ21_31MA.A_3111_QYS32.CmdRunTimeOutTime
//-=A-3111-MXZ21_SM---
//      CLR
//      S     #A_3111_MXZ21_31MA.A_3111_MXZ21_SM.CmdRunTimeOutTime
//-=A-3111-MXZ31_SM---
//      CLR
//      S     #A_3111_MXZ21_31MA.A_3111_MXZ31_SM.CmdRunTimeOutTime
//-=A-3111-MXZ21/31_BM---
//      CLR
//      S     #A_3111_MXZ21_31MA.A_3111_MXZ21_31_BM.CmdRunTimeOutTime
// 
//-Read inputs of elements---
//      U     "A_3111_MXZ21_I1"
      U     "A_3111_MXZ21_I2"; 
      =     #A_3111_MXZ21_31MA.A_3111_MXZ21.InpRunFwd; 
//      U     "A_3111_MXZ21_I2"
      U     "A_3111_MXZ21_I1"; 
      =     #A_3111_MXZ21_31MA.A_3111_MXZ21.InpRunRev; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_MXZ21_31MA.A_3111_MXZ21.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[91]; 
      =     #A_3111_MXZ21_31MA.A_3111_MXZ21.InDPFault; 
      U     "A_3111_MXZ31_I2"; 
//      U     "A_3111_MXZ31_I1"
      =     #A_3111_MXZ21_31MA.A_3111_MXZ31.InpRunFwd; 
      U     "A_3111_MXZ31_I1"; 
//      U     "A_3111_MXZ31_I2"
      =     #A_3111_MXZ21_31MA.A_3111_MXZ31.InpRunRev; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_MXZ21_31MA.A_3111_MXZ31.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[91]; 
      =     #A_3111_MXZ21_31MA.A_3111_MXZ31.InDPFault; 
      U     "A_3111_BZS23_I1"; 
      =     #A_3111_MXZ21_31MA.A_3111_BZS23.InpTrue; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_MXZ21_31MA.A_3111_BZS23.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[93]; 
      =     #A_3111_MXZ21_31MA.A_3111_BZS23.InDPFault; 
      U     "A_3111_BZS33_I1"; 
      =     #A_3111_MXZ21_31MA.A_3111_BZS33.InpTrue; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_MXZ21_31MA.A_3111_BZS33.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[93]; 
      =     #A_3111_MXZ21_31MA.A_3111_BZS33.InDPFault; 
      U     "A_3111_QYS22_I1"; 
      =     #A_3111_MXZ21_31MA.A_3111_QYS22.InpTrue; 
      U     "A_3111_QYS32_I1"; 
      =     #A_3111_MXZ21_31MA.A_3111_QYS32.InpTrue; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_MXZ21_31MA.A_3111_MXZ21_SM.InHWStop; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_MXZ21_31MA.A_3111_MXZ31_SM.InHWStop; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_1; 
      =     #A_3111_MXZ21_31MA.A_3111_MXZ21_31_BM.InHWStop; 
// 
//-Set Parameters---
      U     #Gr.InManual; 
      U     #Gr.InOutAutostartMan; 
      UN    #A_3111_MXZ21_31MA.Ma.InCalibration; // #Ma.InCalibration
      SPBN  Auto; 
      L     1; //MW 2010.04.08
      T     #A_3111_MXZ21_31MA.Ma.Par.ParTolPosition; 
Auto: NOP   0; 
// 
//-Call object control---
      CALL #A_3111_MXZ21_31MA ;
// 
//-Write outputs---
      U     #A_3111_MXZ21_31MA.A_3111_MXZ21.OutpRunFwd; 
      =     "A_3111_MXZ21_O2"; 
//      =     "A_3111_MXZ21_O1"
      U     #A_3111_MXZ21_31MA.A_3111_MXZ21.OutpRunRev; 
//      =     "A_3111_MXZ21_O2"
      =     "A_3111_MXZ21_O1"; 
      U     #A_3111_MXZ21_31MA.A_3111_MXZ31.OutpRunFwd; 
      =     "A_3111_MXZ31_O2"; 
//      =     "A_3111_MXZ31_O1"
      U     #A_3111_MXZ21_31MA.A_3111_MXZ31.OutpRunRev; 
      =     "A_3111_MXZ31_O1"; 
//      =     "A_3111_MXZ31_O2"
NETWORK
TITLE =Test Fault

      U     #A_3111_MXZ21_31MA.Ma.StFault; 
      =     #Gr.VarFault; 
NETWORK
TITLE =Test Warning

      CLR   ; 
      =     #Gr.OutWarning; 
NETWORK
TITLE =Test Stopped

      U     #Gr.ModeIdling; 
      U     #Gr.OutSeqStop; 
      O     #Gr.ModePassive; 
      U(    ; 
      O     #A_3111_MXZ21_31MA.Ma.StStopped; 
      O     #A_3111_MXZ21_31MA.Ma.StAdjusted; 
      )     ; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStopped; 
NETWORK
TITLE =Test Started

//      U     #A_3111_MXZ21_31MA.Ma.StAdjusted
      U     #Gr.InOccupied; 
      UN    #Gr.VarStopped; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStarted; 
NETWORK
TITLE =Enable Previous Group

      U     #Gr.VarStarted; 
      =     #Gr.OutEnablePrevGr; 
NETWORK
TITLE =Call Methode State
//1. Make State of the group: This function use the signals:
//        Gr.VarStopped, Gr.VarStarted, Gr.VarFault
//2.Reset Commands to group: e.g.CmdFaultReset
      CALL #Gr (
           FcCode                   := 2);
NETWORK
TITLE =Init

      SET   ; 
      R     #CmdNewValue; 
      R     #CmdNewJob; 
NETWORK
TITLE =Write data to group control

      U     #Gr.StStopped; 
      =     "GCS7_GrCtrlDB".InStopped[265]; 
      U     #Gr.StStarted; 
      =     "GCS7_GrCtrlDB".InStarted[265]; 
      U     #Gr.StFault; 
      =     "GCS7_GrCtrlDB".InFault[265]; 
      U     #Gr.OutWarning; 
      =     "GCS7_GrCtrlDB".InWarning[265]; 
      U     #Gr.OutManual; 
      =     "GCS7_GrCtrlDB".InManual[265]; 
      U     #Gr.OutHWStop; 
      =     "GCS7_GrCtrlDB".InHWStop[265]; 
      U     #Gr.OutDPFault; 
      =     "GCS7_GrCtrlDB".InDPFault[265]; 
      U     #Gr.OutLoaded; 
      =     "GCS7_GrCtrlDB".InLoaded[265]; 
      U     #Gr.OutAutostart; 
      =     "GCS7_GrCtrlDB".InAutoStart[265]; 
      U     #Gr.VarStartwarning; 
      =     "GCS7_GrCtrlDB".InStartWarning[265]; 
NETWORK
TITLE =End of block

      BE    ; 

END_FUNCTION_BLOCK

