FUNCTION_BLOCK "G171M_Code"
TITLE =G171M FRESH AIR LOUVER GERM 4
//COPYRIGHT:    (C) 2009  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      WinCoS_BIW01_09
//
//TITLE:        FRESH AIR LOUVER GERM 4
//                Machine group control
//                Maschinengruppensteuerung
// 
//--------------------------------------------------------------------------------
// 
//IDENT:        AUTHOR:             DEPT:                              DATE:
// 
//P01_FB271.1.0 K.Renz              CSB41                              13.Jan.2010
//             1st Release
// 
//--------------------------------------------------------------------------------
// 
//PURPOSE: 
// 
//MANUAL : 
// 
//RESTRICTIONS: 
// 
//--------------------------------------------------------------------------------
AUTHOR : KR
FAMILY : USW
VERSION : 9.9


VAR
  Gr : "GCS7_GrpCtrlSub";	//Standard interface to group
  InOutValue1 : DINT ;	//Data for user
  InOutValue2 : DINT ;	//Data for user
  OutWayConflict : BOOL ;	//Data for user
  OutSelectionOk : BOOL ;	//Data for user
  CmdNewValue : BOOL ;	//Data for user
  CmdNewJob : BOOL ;	//Data for user
  OutCodeOK : BOOL ;	//Data for user
  InEndOfJob : BOOL ;	//Data for user
  staWayOk : BOOL ;	//Data for user
  EdgeStartRelease : BOOL ;	//Data for user
  USW8 : BOOL ;	//Data for user
  USW9 : BOOL ;	//Data for user
  USW10 : BOOL ;	//Data for user
  USW11 : BOOL ;	//Data for user
  USW12 : BOOL ;	//Data for user
  USW13 : BOOL ;	//Data for user
  USW14 : BOOL ;	//Data for user
  USW15 : BOOL ;	//Data for user
  A_2451_BZS01 : "GCS7_E_DigInput";	// DigInpCont 
  A_2451_BZS02 : "GCS7_E_DigInput";	// DigInpCont 
  A_2451_MXZ01 : "GCS7_E_ValveControl4Pos";	// VLS4Ctrl 
  A_2452_BZS01 : "GCS7_E_DigInput";	// DigInpCont 
  A_2452_BZS02 : "GCS7_E_DigInput";	// DigInpCont 
  A_2452_MXZ01 : "GCS7_E_ValveControl4Pos";	// VLS4Ctrl 
END_VAR
VAR_TEMP
  tmpEnable : BOOL ;	
  tmpFlag : BOOL ;	
  tmpCmdNext : BOOL ;	
  tmpLocal : BOOL ;	
  tmpAspOk : BOOL ;	
  tmpHWStop : BOOL ;	
  tmpInstanzDBNo : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =Read command from group control
//Parameter
      SET   ; 
      S     "GCS7_GrCtrlDB".ParMGR[171]; 
      R     "GCS7_GrCtrlDB".ParWGR[171]; 
      R     "GCS7_GrCtrlDB".ParGGR[171]; 
      R     "GCS7_GrCtrlDB".ParFeed[171]; 
      R     "GCS7_GrCtrlDB".ParShare[171]; 
      S     #Gr.Par.ParMgr; 
      R     #Gr.Par.ParWgr; 
      R     #Gr.Par.ParGgr; 
//Command
      A     "GCS7_GrCtrlDB".OutEnable[171]; 
      =     #Gr.InEnableGr; 
      A     "GCS7_GrCtrlDB".OutOccupied[171]; 
      =     #Gr.InOccupied; 
      A     "GCS7_GrCtrlDB".CmdFaultReset[171]; 
      R     "GCS7_GrCtrlDB".CmdFaultReset[171]; 
      S     #Gr.CmdFaultReset; 
      A     "GCS7_GrCtrlDB".CmdNext[171]; 
      R     "GCS7_GrCtrlDB".CmdNext[171]; 
      S     #Gr.CmdNext; 

      A     "GCS7_GrCtrlDB".CmdManualOff[171]; 
      AN    #Gr.InManual; 
      S     #Gr.CmdManualOff; 
      A     "GCS7_GrCtrlDB".CmdManualOff[171]; 
      R     "GCS7_GrCtrlDB".CmdManualOff[171]; 

//      U     "GCS7_GrCtrlDB".CmdManualOff[171]
//      O     "GCS7_GrCtrlDB".OutOccupied[171]
//      R     "GCS7_GrCtrlDB".CmdManualOff[171]
//      S     #Gr.CmdManualOff

      A     "GCS7_GrCtrlDB".CmdContinue[171]; 
      R     "GCS7_GrCtrlDB".CmdContinue[171]; 
      S     #Gr.CmdContinue; 
//Init Line Number
      A     "GCS7_GrCtrlDB".ParShare[171]; 
      JCN   WE00; 
      L     0; 
      T     #Gr.InLine; 
WE00: NOP   0; 
NETWORK
TITLE =Call Methode Control
// 1. Handle Interface PCControl      
// 2. Prepare mode --> ModePassive,ModeActive,ModeIdling      
// 3. Reset global flags of the group
      CALL #Gr (
           FcCode                   := 1);
NETWORK
TITLE =Restart Idling Time

      L     #Gr.Par.ParIdling; 
      L     2; 
      >=I   ; 
      JC    IdTm; 
      T     #Gr.Par.ParIdling; 
IdTm: NOP   0; 
      CLR   ; 
      S     #Gr.CmdRestartIdling; 
NETWORK
TITLE =Prepare global flags



NETWORK
TITLE =Set Pattern (Target position)

      A     #Gr.InManual; 
      JC    ILO6; 
      L     0; // 0.0%
      O     "MAL1S020".PCRead.Step.StIdling; 
      O     "MAL1S020".PCRead.Step.StPassive; 
      JC    PE; 
      A     "MAL1S020Par".Par.FlagProcess.ProcessActive; 
      O(    ; 
      A     "MAL1S020Par".Par.FlagProcess.ProcessEnd; // germinating Process finished  
      AN    "MAL1S020".PCRead.Step.StEmptying; 
      )     ; 
      JCN   M003; 
      NOP   0; 
//BRT100310      L     "MAL1S020Par".StepAct.PosFreshAirFlapBasic
//      L     "MAL1S020Par".Par.ParIntMachine.TargetPosFreshAir    //BRT100310 
//      L     10                          // convert to 0.1%
//      *I    
PE:   NOP   0; 

      A     "G171M".A_2451_MXZ01.InManual; 
      JC    Man1; 
      L     "MAL1S020Par".Par.ParIntMachine.TargetPosFreshAir; //BRT100310 
      L     10; // convert to 0.1%
      *I    ; 
      T     #A_2451_MXZ01.InPositionMiddle; // setpoint element
//-> 26.02.2014 IS. Start regulating from current position when switch to automatic
Man1: L     #A_2451_MXZ01.InPositionMiddle; 
      L     10; 
      /D    ; 
      T     "MAL1S020Par".Par.ParIntMachine.TargetPosFreshAir; 
//<-

      A     "G171M".A_2452_MXZ01.InManual; 
      JC    Man2; 
      L     "MAL1S020Par".Par.ParIntProcess.Value29; // Target Position FreshAir 2
      L     10; // convert to 0.1%
      *I    ; 
      T     #A_2452_MXZ01.InPositionMiddle; // setpoint element
//-> 26.02.2014 IS. Start regulating from current position when switch to automatic
Man2: L     #A_2452_MXZ01.InPositionMiddle; 
      L     10; 
      /D    ; 
      T     "MAL1S020Par".Par.ParIntProcess.Value29; 
//<-

M003: NOP   0; 
      JU    En06; 
ILO6: NOP   0; 
      L     "MAL1S020Par".Par.ParIntProcess.TarPos_ILO_FreshAir; 
      L     10; // convert to 0.1%
      *I    ; 
      T     #A_2451_MXZ01.InPositionMiddle; // setpoint element
      T     #A_2452_MXZ01.InPositionMiddle; // setpoint element
En06: NOP   0; 

NETWORK
TITLE =Interlocking WayOk



NETWORK
TITLE =Interlocking Autostart

      CLR   ; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE =Interlocking Enable

      A     #Gr.InEnableGr; 
      =     #Gr.OutEnableEl; 

NETWORK
TITLE =Interlocking Next

      A     #Gr.CmdNext; 
      A     #Gr.OutAutostart; 
      AN    #Gr.OutNewWay; 
      =     #tmpCmdNext; 

NETWORK
TITLE =Interlocking Local / HWStop

      A     "PLC_SystemDB".HardwareStop.Germinating_4; 
//BRT100310 - HW-Stop nur bei NOT-AUS +C24 oder Customer, nicht bei NOT-AUS Maschine!
      A(    ; 
      O     "A_2499_SHE01_I1"; 
      O     "A_2499_SHE02_I1"; 
      O     "A_24_A097Z1_I1"; 
      )     ; 
      =     #tmpHWStop; 

      A     #Gr.ModePassive; 
      O     #tmpHWStop; 
      =     #tmpLocal; 

NETWORK
TITLE ==A-2451-BZS01 : FRESH AIR FLAP 1 100% CIRCULATION AIR

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2451_BZS01.InLocal; 
// 
//-Monitoring---
      CLR   ; 
      =     #A_2451_BZS01.InMonTrue; 
      CLR   ; 
      =     #A_2451_BZS01.InMonFalse; 
// 
//-PreAlarm---
      CLR   ; 
      =     #A_2451_BZS01.InPreAlarmTrue; 
      CLR   ; 
      =     #A_2451_BZS01.InPreAlarmFalse; 
// 
//-Additional commands---
//      CLR
//      S     #A_2451_BZS01.CmdRunTimeOutTime
// 
//-Read inputs---
      A     "A_2451_BZS01_I1"; 
      =     #A_2451_BZS01.InpTrue; 
      A     "PLC_DPInfo".Master[1].SlavesError[48]; 
      =     #A_2451_BZS01.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2451_BZS01 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2451-BZS02 : FRESH AIR FLAP 1 100% FRESH AIR

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2451_BZS02.InLocal; 
// 
//-Monitoring---
      CLR   ; 
      =     #A_2451_BZS02.InMonTrue; 
      CLR   ; 
      =     #A_2451_BZS02.InMonFalse; 
// 
//-PreAlarm---
      CLR   ; 
      =     #A_2451_BZS02.InPreAlarmTrue; 
      CLR   ; 
      =     #A_2451_BZS02.InPreAlarmFalse; 
// 
//-Additional commands---
//      CLR
//      S     #A_2451_BZS02.CmdRunTimeOutTime
// 
//-Read inputs---
      A     "A_2451_BZS02_I1"; 
      =     #A_2451_BZS02.InpTrue; 
      A     "PLC_DPInfo".Master[1].SlavesError[48]; 
      =     #A_2451_BZS02.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2451_BZS02 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2451-MXZ01 : FRESH AIR FLAP 1

      NOP   0; 
//-InLocal---
      A(    ; 
      A     #tmpLocal; 
      O     ; 
      A     #Gr.InManual; 
      AN    #Gr.InOutAutostartMan; 
      )     ; 
      A     #A_2451_MXZ01.StLN; 
      =     #A_2451_MXZ01.InLocal; 

//-Interlocking---
      A     #A_2451_MXZ01.Par.ParManual; 
      A     #A_2451_MXZ01.InManual; 
      JC    man1; 
      A(    ; 
      L     0; 
      L     #A_2451_MXZ01.InPositionMiddle; // setpoint element
      ==I   ; 
      )     ; 
      A(    ; 
      A     #Gr.InManual; 
      A     #Gr.InOutAutostartMan; 
      ON    #Gr.InManual; 
      )     ; 
      A     #Gr.OutEnableEl; 
      =     #A_2451_MXZ01.InLN; 

      A(    ; 
      L     #A_2451_MXZ01.InPositionMiddle; // setpoint element
      L     0; 
      >I    ; 
      )     ; 
      A(    ; 
      L     #A_2451_MXZ01.InPositionMiddle; // setpoint element
      L     1000; // 100,0%
      <I    ; 
      )     ; 
      A(    ; 
      A     #Gr.InManual; 
      A     #Gr.InOutAutostartMan; 
      ON    #Gr.InManual; 
      )     ; 
      A     #Gr.OutEnableEl; 
      S     #A_2451_MXZ01.CmdMiddle1; 

      CLR   ; 
      =     #A_2451_MXZ01.CmdMiddle2; 

      A(    ; 
      AN    #Gr.InOccupied; 
      AN    #Gr.InManual; 
      )     ; 
      O(    ; 
      L     #A_2451_MXZ01.InPositionMiddle; // setpoint element
      L     1000; // 100,0%
      >=I   ; 
      )     ; 
      A(    ; 
      A     #Gr.InManual; 
      A     #Gr.InOutAutostartMan; 
      ON    #Gr.InManual; 
      )     ; 
      A     #Gr.OutEnableEl; 
      S     #A_2451_MXZ01.CmdHN; 
man1: NOP   0; 
// 
//-Read inputs---
//====>BRT060310 - Regeln auf Zeit, ohne Auswertung Rückmeldung
      A     "A_2451_BZS01_I1"; 
      =     "G171M".A_2451_MXZ01.InpLN; 
      CLR   ; 
      =     "G171M".A_2451_MXZ01.InpMiddle1; 
      A     "A_2451_BZS02_I1"; 
      =     "G171M".A_2451_MXZ01.InpHN; 

//      U     #A_2451_BZS02.StTrue        // 100% Frischluft
//      SPBN  FL1
//      L     1000
//      SPA   INP1
//FL1:  NOP   0
//      U     #A_2451_BZS01.StTrue        // 100% Umluft
//      SPBN  RL1
//      L     0
//      SPA   INP1
//RL1:  NOP   0
//      L     "A_2451_BEE01_AI1"
//INP1: T     #A_2451_MXZ01.InpPosition
//<====BRT060310
//BRT110310      U     "PLC_SystemDB".HardwareStop.Germinating_4
      A     #tmpHWStop; //BRT110310
      =     #A_2451_MXZ01.InHWStop; 
      A     "PLC_DPInfo".Master[1].SlavesError[48]; 
      =     #A_2451_MXZ01.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2451_MXZ01 ;
// 
//-Write outputs---
      A     #A_2451_MXZ01.OutpLN; 
      =     "A_2451_MXZ01_O2"; 
      A     #A_2451_MXZ01.OutpHN; 
      =     "A_2451_MXZ01_O1"; 

NETWORK
TITLE ==A-2452-BZS01 : FRESH AIR FLAP 2 100% CIRCULATION AIR

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2452_BZS01.InLocal; 
// 
//-Monitoring---
      CLR   ; 
      =     #A_2452_BZS01.InMonTrue; 
      CLR   ; 
      =     #A_2452_BZS01.InMonFalse; 
// 
//-PreAlarm---
      CLR   ; 
      =     #A_2452_BZS01.InPreAlarmTrue; 
      CLR   ; 
      =     #A_2452_BZS01.InPreAlarmFalse; 
// 
//-Additional commands---
//      CLR
//      S     #A_2452_BZS01.CmdRunTimeOutTime
// 
//-Read inputs---
      A     "A_2452_BZS01_I1"; 
      =     #A_2452_BZS01.InpTrue; 
      A     "PLC_DPInfo".Master[1].SlavesError[48]; 
      =     #A_2452_BZS01.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2452_BZS01 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2452-BZS02 : FRESH AIR FLAP 2 100% FRESH AIR

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2452_BZS02.InLocal; 
// 
//-Monitoring---
      CLR   ; 
      =     #A_2452_BZS02.InMonTrue; 
      CLR   ; 
      =     #A_2452_BZS02.InMonFalse; 
// 
//-PreAlarm---
      CLR   ; 
      =     #A_2452_BZS02.InPreAlarmTrue; 
      CLR   ; 
      =     #A_2452_BZS02.InPreAlarmFalse; 
// 
//-Additional commands---
//      CLR
//      S     #A_2452_BZS02.CmdRunTimeOutTime
// 
//-Read inputs---
      A     "A_2452_BZS02_I1"; 
      =     #A_2452_BZS02.InpTrue; 
      A     "PLC_DPInfo".Master[1].SlavesError[48]; 
      =     #A_2452_BZS02.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2452_BZS02 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2452-MXZ01 : FRESH AIR FLAP 2

      NOP   0; 
//-InLocal---
      A(    ; 
      A     #tmpLocal; 
      O     ; 
      A     #Gr.InManual; 
      AN    #Gr.InOutAutostartMan; 
      )     ; 
      A     #A_2452_MXZ01.StLN; 
      =     #A_2452_MXZ01.InLocal; 

//-Interlocking---
      A     #A_2452_MXZ01.Par.ParManual; 
      A     #A_2452_MXZ01.InManual; 
      JC    man2; 
      A(    ; 
      L     0; 
      L     #A_2452_MXZ01.InPositionMiddle; // setpoint element
      ==I   ; 
      )     ; 
      A(    ; 
      A     #Gr.InManual; 
      A     #Gr.InOutAutostartMan; 
      ON    #Gr.InManual; 
      )     ; 
      A     #Gr.OutEnableEl; 
      =     #A_2452_MXZ01.InLN; 

      A(    ; 
      L     #A_2452_MXZ01.InPositionMiddle; // setpoint element
      L     0; 
      >I    ; 
      )     ; 
      A(    ; 
      L     #A_2452_MXZ01.InPositionMiddle; // setpoint element
      L     1000; // 100,0%
      <I    ; 
      )     ; 
      A(    ; 
      A     #Gr.InManual; 
      A     #Gr.InOutAutostartMan; 
      ON    #Gr.InManual; 
      )     ; 
      A     #Gr.OutEnableEl; 
      S     #A_2452_MXZ01.CmdMiddle1; 

      CLR   ; 
      =     #A_2452_MXZ01.CmdMiddle2; 

      A(    ; 
      AN    #Gr.InOccupied; 
      AN    #Gr.InManual; 
      )     ; 
      O(    ; 
      L     #A_2452_MXZ01.InPositionMiddle; // setpoint element
      L     1000; // 100,0%
      >=I   ; 
      )     ; 
      A(    ; 
      A     #Gr.InManual; 
      A     #Gr.InOutAutostartMan; 
      ON    #Gr.InManual; 
      )     ; 
      A     #Gr.OutEnableEl; 
      S     #A_2452_MXZ01.CmdHN; 
man2: NOP   0; 
// 
//-Read inputs---
//====>BRT060310 - Regeln auf Zeit, ohne Auswertung Rückmeldung
      A     "A_2452_BZS01_I1"; 
      =     "G171M".A_2452_MXZ01.InpLN; 
      CLR   ; 
      =     "G171M".A_2452_MXZ01.InpMiddle1; 
      A     "A_2452_BZS02_I1"; 
      =     "G171M".A_2452_MXZ01.InpHN; 

//      U     #A_2452_BZS02.StTrue        // 100% Frischluft
//      SPBN  FL2
//      L     1000
//      SPA   INP2
//FL2:  NOP   0
//      U     #A_2452_BZS01.StTrue        // 100% Umluft
//      SPBN  RL2
//      L     0
//      SPA   INP2
//RL2:  NOP   0
//      L     "A_2452_BEE01_AI1"
//INP2: T     #A_2452_MXZ01.InpPosition
//<====BRT100310
//BRT110310      U     "PLC_SystemDB".HardwareStop.Germinating_4
      A     #tmpHWStop; //BRT110310
      =     #A_2452_MXZ01.InHWStop; 
      A     "PLC_DPInfo".Master[1].SlavesError[48]; 
      =     #A_2452_MXZ01.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2452_MXZ01 ;
// 
//-Write outputs---
      A     #A_2452_MXZ01.OutpLN; 
      =     "A_2452_MXZ01_O2"; 
      A     #A_2452_MXZ01.OutpHN; 
      =     "A_2452_MXZ01_O1"; 

NETWORK
TITLE =Test Fault

      A     #A_2451_BZS01.StFault; 
      O     #A_2451_BZS02.StFault; 
      O     #A_2451_MXZ01.StFault; 
      O     #A_2452_BZS01.StFault; 
      O     #A_2452_BZS02.StFault; 
      O     #A_2452_MXZ01.StFault; 
      =     #Gr.VarFault; 
NETWORK
TITLE =Test Warning

      CLR   ; 
      =     #Gr.OutWarning; 
NETWORK
TITLE =Test Stopped

//BRT100310       U     #A_2451_MXZ01.StLN
//BRT100310       U     #A_2452_MXZ01.StLN
//BRT100310       UN    #Gr.VarFault
      AN    #Gr.VarStarted; 
      =     #Gr.VarStopped; 
NETWORK
TITLE =Test Started

      AN    #A_2451_MXZ01.StLN; 
      AN    #A_2451_MXZ01.StFault; 
      AN    #A_2451_MXZ01.StNoPosition; 
      AN    #A_2452_MXZ01.StLN; 
      AN    #A_2452_MXZ01.StFault; 
      AN    #A_2452_MXZ01.StNoPosition; 
//BRT100310      UN    #Gr.VarStopped
      AN    #Gr.VarFault; 
      =     #Gr.VarStarted; 
NETWORK
TITLE =Enable Previous Group

      A     #Gr.VarStarted; 
      =     #Gr.OutEnablePrevGr; 
NETWORK
TITLE =Call Methode State
//1. Make State of the group: This function use the signals:
//        Gr.VarStopped, Gr.VarStarted, Gr.VarFault
//2.Reset Commands to group: e.g.CmdFaultReset
      CALL #Gr (
           FcCode                   := 2);
NETWORK
TITLE =Init

      SET   ; 
      R     #CmdNewValue; 
      R     #CmdNewJob; 
NETWORK
TITLE =Write data to group control

      A     #Gr.StStopped; 
      =     "GCS7_GrCtrlDB".InStopped[171]; 
      A     #Gr.StStarted; 
      =     "GCS7_GrCtrlDB".InStarted[171]; 
      A     #Gr.StFault; 
      =     "GCS7_GrCtrlDB".InFault[171]; 
      A     #Gr.OutWarning; 
      =     "GCS7_GrCtrlDB".InWarning[171]; 
      A     #Gr.OutManual; 
      =     "GCS7_GrCtrlDB".InManual[171]; 
      A     #Gr.OutHWStop; 
      =     "GCS7_GrCtrlDB".InHWStop[171]; 
      A     #Gr.OutDPFault; 
      =     "GCS7_GrCtrlDB".InDPFault[171]; 
      A     #Gr.OutLoaded; 
      =     "GCS7_GrCtrlDB".InLoaded[171]; 
      A     #Gr.OutAutostart; 
      =     "GCS7_GrCtrlDB".InAutoStart[171]; 
      A     #Gr.VarStartwarning; 
      =     "GCS7_GrCtrlDB".InStartWarning[171]; 
NETWORK
TITLE =End of block

      BE    ; 

END_FUNCTION_BLOCK

