FUNCTION_BLOCK "G296M_Code"
TITLE =G296M AIR SHUT SLIDE KILN 2
//COPYRIGHT:    (C) 2009  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      WinCoS_BIW01_09
//
//TITLE:        AIR SHUT SLIDE KILN 2
//                Machine group control
//                Maschinengruppensteuerung
// 
//--------------------------------------------------------------------------------
// 
//IDENT:        AUTHOR:             DEPT:                              DATE:
// 
//P01_FB396.1.0 K.Renz              CSB41                              25.Mai.2010
//             1st Release
// 
//--------------------------------------------------------------------------------
// 
//PURPOSE: 
// 
//MANUAL : 
// 
//RESTRICTIONS: 
// 
//--------------------------------------------------------------------------------
AUTHOR : KR
FAMILY : USW
VERSION : 9.9


VAR
  Gr : "GCS7_GrpCtrlSub";	//Standard interface to group
  InOutValue1 : DINT ;	//Data for user
  InOutValue2 : DINT ;	//Data for user
  OutWayConflict : BOOL ;	//Data for user
  OutSelectionOk : BOOL ;	//Data for user
  CmdNewValue : BOOL ;	//Data for user
  CmdNewJob : BOOL ;	//Data for user
  OutCodeOK : BOOL ;	//Data for user
  InEndOfJob : BOOL ;	//Data for user
  staWayOk : BOOL ;	//Data for user
  EdgeStartRelease : BOOL ;	//Data for user
  USW8 : BOOL ;	//Data for user
  USW9 : BOOL ;	//Data for user
  USW10 : BOOL ;	//Data for user
  USW11 : BOOL ;	//Data for user
  USW12 : BOOL ;	//Data for user
  USW13 : BOOL ;	//Data for user
  USW14 : BOOL ;	//Data for user
  USW15 : BOOL ;	//Data for user
  A_3201_BZS04 : "GCS7_E_DigInput";	// DigInpCont 
  A_3201_BZS05 : "GCS7_E_DigInput";	// DigInpCont 
  A_3201_MXZ01 : "GCS7_E_Motor";	// Motor 
END_VAR
VAR_TEMP
  tmpEnable : BOOL ;	
  tmpFlag : BOOL ;	
  tmpCmdNext : BOOL ;	
  tmpLocal : BOOL ;	
  tmpAspOk : BOOL ;	
  tmpHWStop : BOOL ;	
  tmpInstanzDBNo : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =Read command from group control
//Parameter
      SET   ; 
      S     "GCS7_GrCtrlDB".ParMGR[296]; 
      R     "GCS7_GrCtrlDB".ParWGR[296]; 
      R     "GCS7_GrCtrlDB".ParGGR[296]; 
      R     "GCS7_GrCtrlDB".ParFeed[296]; 
      R     "GCS7_GrCtrlDB".ParShare[296]; 
      S     #Gr.Par.ParMgr; 
      R     #Gr.Par.ParWgr; 
      R     #Gr.Par.ParGgr; 
//Command
      U     "GCS7_GrCtrlDB".OutEnable[296]; 
      =     #Gr.InEnableGr; 
      U     "GCS7_GrCtrlDB".OutOccupied[296]; 
      =     #Gr.InOccupied; 
      U     "GCS7_GrCtrlDB".CmdFaultReset[296]; 
      R     "GCS7_GrCtrlDB".CmdFaultReset[296]; 
      S     #Gr.CmdFaultReset; 
      U     "GCS7_GrCtrlDB".CmdNext[296]; 
      R     "GCS7_GrCtrlDB".CmdNext[296]; 
      S     #Gr.CmdNext; 

      U     "GCS7_GrCtrlDB".CmdManualOff[296]; 
      UN    #Gr.InManual; 
      S     #Gr.CmdManualOff; 
      U     "GCS7_GrCtrlDB".CmdManualOff[296]; 
      R     "GCS7_GrCtrlDB".CmdManualOff[296]; 

//      U     "GCS7_GrCtrlDB".CmdManualOff[296]
//      O     "GCS7_GrCtrlDB".OutOccupied[296]
//      R     "GCS7_GrCtrlDB".CmdManualOff[296]
//      S     #Gr.CmdManualOff

      U     "GCS7_GrCtrlDB".CmdContinue[296]; 
      R     "GCS7_GrCtrlDB".CmdContinue[296]; 
      S     #Gr.CmdContinue; 
//Init Line Number
      U     "GCS7_GrCtrlDB".ParShare[296]; 
      SPBN  WE00; 
      L     0; 
      T     #Gr.InLine; 
WE00: NOP   0; 
NETWORK
TITLE =Call Methode Control
// 1. Handle Interface PCControl      
// 2. Prepare mode --> ModePassive,ModeActive,ModeIdling      
// 3. Reset global flags of the group
      CALL #Gr (
           FcCode                   := 1);
NETWORK
TITLE =Restart Idling Time

      L     #Gr.Par.ParIdling; 
      L     2; 
      >=I   ; 
      SPB   IdTm; 
      T     #Gr.Par.ParIdling; 
IdTm: NOP   0; 
      CLR   ; 
      S     #Gr.CmdRestartIdling; 
NETWORK
TITLE =Prepare global flags



NETWORK
TITLE =Set Pattern (Target position)



NETWORK
TITLE =Interlocking WayOk



NETWORK
TITLE =Interlocking Autostart

      CLR   ; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE =Interlocking Enable

      U     #Gr.InEnableGr; 
      =     #Gr.OutEnableEl; 

NETWORK
TITLE =Interlocking Next

      U     #Gr.CmdNext; 
      U     #Gr.OutAutostart; 
      UN    #Gr.OutNewWay; 
      =     #tmpCmdNext; 

NETWORK
TITLE =Interlocking Local / HWStop

      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #tmpHWStop; 

      U     #Gr.ModePassive; 
      O     #tmpHWStop; 
      =     #tmpLocal; 

NETWORK
TITLE ==A-3201-BZS04 : OUTLET FLAP CLOSED

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      =     #A_3201_BZS04.InLocal; 
// 
//-Monitoring---
      CLR   ; 
      =     #A_3201_BZS04.InMonTrue; 
      CLR   ; 
      =     #A_3201_BZS04.InMonFalse; 
// 
//-PreAlarm---
      CLR   ; 
      =     #A_3201_BZS04.InPreAlarmTrue; 
      CLR   ; 
      =     #A_3201_BZS04.InPreAlarmFalse; 
// 
//-Additional commands---
//      CLR
//      S     #A_3201_BZS04.CmdRunTimeOutTime
// 
//-Read inputs---
      U     "A_3201_BZS04_I1"; 
      =     #A_3201_BZS04.InpTrue; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #A_3201_BZS04.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[101]; 
      =     #A_3201_BZS04.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3201_BZS04 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-3201-BZS05 : OUTLET FLAP OPENED

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      =     #A_3201_BZS05.InLocal; 
// 
//-Monitoring---
      CLR   ; 
      =     #A_3201_BZS05.InMonTrue; 
      CLR   ; 
      =     #A_3201_BZS05.InMonFalse; 
// 
//-PreAlarm---
      CLR   ; 
      =     #A_3201_BZS05.InPreAlarmTrue; 
      CLR   ; 
      =     #A_3201_BZS05.InPreAlarmFalse; 
// 
//-Additional commands---
//      CLR
//      S     #A_3201_BZS05.CmdRunTimeOutTime
// 
//-Read inputs---
      U     "A_3201_BZS05_I1"; 
      =     #A_3201_BZS05.InpTrue; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #A_3201_BZS05.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[101]; 
      =     #A_3201_BZS05.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3201_BZS05 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-3201-MXZ01 : OUTLET FLAP DRIVE

      NOP   0; 
//-InLocal--- 
      U     #tmpLocal; 
      U(    ; 
      O     #A_3201_BZS04.StTrue; // Geschlossen
//      O     #A_3201_BZS05.StTrue        // Offen
      )     ; 
      =     #A_3201_MXZ01.InLocal; 
//
//-Interlocking manual--- 
      U(    ; 
      U     #A_3201_MXZ01.OutpRunRev; 
      U     #A_3201_BZS05.StTrue; 
      O     ; 
      U     #A_3201_MXZ01.OutpRunFwd; 
      U     #A_3201_BZS04.StTrue; 
      )     ; 
      U     #A_3201_MXZ01.InManual; 
      =     #A_3201_MXZ01.CmdOff; 
      R     #A_3201_MXZ01.InManual; 
//      S     #A_3201_MXZ01.CmdFaultReset

//-Interlocking--- 
      UN    "MAL1S033Par".Par.FlagFunction_1.StepLoading; // Step loading
      UN    #A_3201_BZS04.StCtoTrue; // Geschlossen
      UN    #A_3201_BZS04.StTrue; // Geschlossen
      ON    #Gr.OutEnableEl; 
      =     #A_3201_MXZ01.InEnableFwd; // Schieber Schliessen  

      U     "MAL1S033Par".Par.FlagFunction_1.StepLoading; // Step loading
      UN    #A_3201_BZS05.StCtoTrue; // Offen
      UN    #A_3201_BZS05.StTrue; // Offen
      U     #Gr.OutEnableEl; 
      =     #A_3201_MXZ01.InEnableRev; // Schieber Öffnen
// 
      U     #tmpCmdNext; 
      O(    ; 
      UN    #Gr.InOccupied; 
      UN    #A_3201_BZS04.StTrue; // Geschlossen
      )     ; 
      S     #A_3201_MXZ01.CmdNext; 
// 
      U     #Gr.OutSeqStop; 
      U     #A_3201_BZS04.StTrue; // Geschlossen
      =     #A_3201_MXZ01.InSeqStop; 
// 
//-Read inputs---
      U     "A_3201_MXZ01_I1"; 
//      O     #A_3201_BZS04.StCtoTrue     // Geschlossen
      =     #A_3201_MXZ01.InpRunFwd; 
      U     "A_3201_MXZ01_I2"; 
//      O     #A_3201_BZS05.StCtoTrue     // Offen
      =     #A_3201_MXZ01.InpRunRev; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #A_3201_MXZ01.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[101]; 
      =     #A_3201_MXZ01.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3201_MXZ01 ;
// 
//-Write outputs---
      U     #A_3201_MXZ01.OutpRunFwd; 
      UN    #A_3201_BZS04.StTrue; // Geschlossen
      =     "A_3201_MXZ01_O1"; 
      U     #A_3201_MXZ01.OutpRunRev; 
      UN    #A_3201_BZS05.StTrue; // Offen
      =     "A_3201_MXZ01_O2"; 
NETWORK
TITLE =Test Fault

      U     #A_3201_BZS04.StFault; 
      O     #A_3201_BZS05.StFault; 
      O     #A_3201_MXZ01.StFault; 
      =     #Gr.VarFault; 
NETWORK
TITLE =Test Warning

      CLR   ; 
      =     #Gr.OutWarning; 
NETWORK
TITLE =Test Stopped

//      U     #A_3201_MXZ01.StStopped
//      UN    #Gr.VarFault
      UN    #Gr.VarStarted; 
      =     #Gr.VarStopped; 
NETWORK
TITLE =Test Started

//      U(    
//      O     #A_3201_MXZ01.StStartedFwd
//      O     #A_3201_MXZ01.StStartedRev
//      )     
//      UN    #Gr.VarStopped
      U     #A_3201_BZS05.StTrue; // Offen
      O     #A_3201_BZS04.StTrue; // Geschlossen
      UN    #Gr.VarFault; 
      =     #Gr.VarStarted; 
NETWORK
TITLE =Enable Previous Group

      U     #Gr.VarStarted; 
      =     #Gr.OutEnablePrevGr; 
NETWORK
TITLE =Call Methode State
//1. Make State of the group: This function use the signals:
//        Gr.VarStopped, Gr.VarStarted, Gr.VarFault
//2.Reset Commands to group: e.g.CmdFaultReset
      CALL #Gr (
           FcCode                   := 2);
NETWORK
TITLE =Init

      SET   ; 
      R     #CmdNewValue; 
      R     #CmdNewJob; 
NETWORK
TITLE =Write data to group control

      U     #Gr.StStopped; 
      =     "GCS7_GrCtrlDB".InStopped[296]; 
      U     #Gr.StStarted; 
      =     "GCS7_GrCtrlDB".InStarted[296]; 
      U     #Gr.StFault; 
      =     "GCS7_GrCtrlDB".InFault[296]; 
      U     #Gr.OutWarning; 
      =     "GCS7_GrCtrlDB".InWarning[296]; 
      U     #Gr.OutManual; 
      =     "GCS7_GrCtrlDB".InManual[296]; 
      U     #Gr.OutHWStop; 
      =     "GCS7_GrCtrlDB".InHWStop[296]; 
      U     #Gr.OutDPFault; 
      =     "GCS7_GrCtrlDB".InDPFault[296]; 
      U     #Gr.OutLoaded; 
      =     "GCS7_GrCtrlDB".InLoaded[296]; 
      U     #Gr.OutAutostart; 
      =     "GCS7_GrCtrlDB".InAutoStart[296]; 
      U     #Gr.VarStartwarning; 
      =     "GCS7_GrCtrlDB".InStartWarning[296]; 
NETWORK
TITLE =End of block

      BE    ; 

END_FUNCTION_BLOCK

