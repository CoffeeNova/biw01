FUNCTION_BLOCK "G294M_Code"
TITLE =G294M DISCHARGE DOOR KILN 2
//COPYRIGHT:    (C) 2009  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      WinCoS_BIW01_09
//
//TITLE:        DISCHARGE DOOR KILN 2
//                Machine group control
//                Maschinengruppensteuerung
// 
//--------------------------------------------------------------------------------
// 
//IDENT:        AUTHOR:             DEPT:                              DATE:
// 
//P01_FB394.1.0 K.Renz              CSB41                              06.Nov.2009
//             1st Release
// 
//--------------------------------------------------------------------------------
// 
//PURPOSE: 
// 
//MANUAL : 
// 
//RESTRICTIONS: 
// 
//--------------------------------------------------------------------------------
AUTHOR : KR
FAMILY : USW
VERSION : 9.9


VAR
  Gr : "GCS7_GrpCtrlSub";	//Standard interface to group
  InOutValue1 : DINT ;	//Data for user
  InOutValue2 : DINT ;	//Data for user
  OutWayConflict : BOOL ;	//Data for user
  OutSelectionOk : BOOL ;	//Data for user
  CmdNewValue : BOOL ;	//Data for user
  CmdNewJob : BOOL ;	//Data for user
  OutCodeOK : BOOL ;	//Data for user
  InEndOfJob : BOOL ;	//Data for user
  staWayOk : BOOL ;	//Data for user
  EdgeStartRelease : BOOL ;	//Data for user
  USW8 : BOOL ;	//Data for user
  USW9 : BOOL ;	//Data for user
  USW10 : BOOL ;	//Data for user
  USW11 : BOOL ;	//Data for user
  USW12 : BOOL ;	//Data for user
  USW13 : BOOL ;	//Data for user
  USW14 : BOOL ;	//Data for user
  USW15 : BOOL ;	//Data for user
  A_3215_MXZ11 : "GCS7_E_Motor";	// Motor 
  spare94 : ARRAY  [94 .. 127 ] OF BYTE ;	
  A_3215_BZS14 : "GCS7_E_DigInput";	// DigInpCont 
  A_3215_BZS15 : "GCS7_E_DigInput";	// DigInpCont 
  A_3215_BZS16 : "GCS7_E_DigInput";	// DigInpCont 
END_VAR
VAR_TEMP
  tmpEnable : BOOL ;	
  tmpFlag : BOOL ;	
  tmpCmdNext : BOOL ;	
  tmpLocal : BOOL ;	
  tmpAspOk : BOOL ;	
  tmpHWStop : BOOL ;	
  tmpInstanzDBNo : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =Read command from group control
//Parameter
      SET   ; 
      S     "GCS7_GrCtrlDB".ParMGR[294]; 
      R     "GCS7_GrCtrlDB".ParWGR[294]; 
      R     "GCS7_GrCtrlDB".ParGGR[294]; 
      R     "GCS7_GrCtrlDB".ParFeed[294]; 
      R     "GCS7_GrCtrlDB".ParShare[294]; 
      S     #Gr.Par.ParMgr; 
      R     #Gr.Par.ParWgr; 
      R     #Gr.Par.ParGgr; 
//Command
      U     "GCS7_GrCtrlDB".OutEnable[294]; 
      =     #Gr.InEnableGr; 
      U     "GCS7_GrCtrlDB".OutOccupied[294]; 
      =     #Gr.InOccupied; 
      U     "GCS7_GrCtrlDB".CmdFaultReset[294]; 
      R     "GCS7_GrCtrlDB".CmdFaultReset[294]; 
      S     #Gr.CmdFaultReset; 
      U     "GCS7_GrCtrlDB".CmdNext[294]; 
      R     "GCS7_GrCtrlDB".CmdNext[294]; 
      S     #Gr.CmdNext; 

      U     "GCS7_GrCtrlDB".CmdManualOff[294]; 
      UN    #Gr.InManual; 
      S     #Gr.CmdManualOff; 
      U     "GCS7_GrCtrlDB".CmdManualOff[294]; 
      R     "GCS7_GrCtrlDB".CmdManualOff[294]; 

//      U     "GCS7_GrCtrlDB".CmdManualOff[294]
//      O     "GCS7_GrCtrlDB".OutOccupied[294]
//      R     "GCS7_GrCtrlDB".CmdManualOff[294]
//      S     #Gr.CmdManualOff

      U     "GCS7_GrCtrlDB".CmdContinue[294]; 
      R     "GCS7_GrCtrlDB".CmdContinue[294]; 
      S     #Gr.CmdContinue; 
//Init Line Number
      U     "GCS7_GrCtrlDB".ParShare[294]; 
      SPBN  WE00; 
      L     0; 
      T     #Gr.InLine; 
WE00: NOP   0; 
NETWORK
TITLE =Call Methode Control
// 1. Handle Interface PCControl      
// 2. Prepare mode --> ModePassive,ModeActive,ModeIdling      
// 3. Reset global flags of the group
      CALL #Gr (
           FcCode                   := 1);
NETWORK
TITLE =Restart Idling Time

      L     #Gr.Par.ParIdling; 
      L     2; 
      >=I   ; 
      SPB   IdTm; 
      T     #Gr.Par.ParIdling; 
IdTm: NOP   0; 
      CLR   ; 
      S     #Gr.CmdRestartIdling; 
NETWORK
TITLE =Prepare global flags



NETWORK
TITLE =Set Pattern (Target position)



NETWORK
TITLE =Interlocking WayOk



NETWORK
TITLE =Interlocking Autostart

      CLR   ; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE =Interlocking Enable

      U     #Gr.InEnableGr; 
      =     #Gr.OutEnableEl; 

NETWORK
TITLE =Interlocking Next

      U     #Gr.CmdNext; 
      U     #Gr.OutAutostart; 
      UN    #Gr.OutNewWay; 
      =     #tmpCmdNext; 

NETWORK
TITLE =Interlocking Local / HWStop

      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #tmpHWStop; 

      U     #Gr.ModePassive; 
      O     #tmpHWStop; 
      =     #tmpLocal; 

NETWORK
TITLE =Interlocking Start Warning

      NOP   0; 
//- Restart after fault --
      U     #Gr.CmdFaultReset; 
      U     #Gr.VarFault; 
      S     "SYS_StartWarningDB".Area[11].CmdResetStartWarning; 
//- Run Timer --
      AUF   "G294M"; 
      CALL "GCS7_StartWarning" , "SYS_StartWarningDB" (
           InModeHigher             := FALSE,
           InArea                   := 11,
           InEnable                 := #Gr.OutEnableEl,
           InLocal                  := #tmpLocal,
           InStarted                := #Gr.VarStarted,
           InCmdNext                := #tmpCmdNext,
           OutCmdNext               := #tmpCmdNext,
           OutStartwarning          := #Gr.VarStartwarning);
//- Store Startwarning done  --
//- Reset Autostart --
      U     "SYS_StartWarningDB".TmpArea.OutStartReleaseRunningL; 
      FN    #EdgeStartRelease; 
      U     #Gr.VarFault; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE ==A-3215-MXZ11 : DISCHARGE FLAP 

      NOP   0; 
//-InLocal--- 
      U     #tmpLocal; 
      =     #A_3215_MXZ11.InLocal; 
//
//-Interlocking--- 
      U     "MAL1S033".ToASW.InfDataReady; 
      U     "MAL1S033Par".Par.FlagFunction_1.StepUnloading; 
//    U     "MAL1S033Par".Par.FlagFunction_1.UnloadingStarted
      U     "G280M".A_3921_MXZ01MA.Ma.StStartedFwd; 
      U     #A_3215_BZS16.StTrue; // Cleaning Flap
      O(    ; 
      U     #Gr.InManual; 
      U     #USW14; // ManOpen
      U     #Gr.InOutAutostartMan; 
      )     ; 
      UN    #A_3215_BZS15.InpTrue; // Unloading door open
      UN    #A_3215_BZS15.StFault; 
      U     "G280M".A_3922_MXZ01_BZS04_05.StHN; 
      U     #Gr.OutEnableEl; 
      =     #A_3215_MXZ11.InEnableFwd; 

      U(    ; 
      U     "MAL1S033Par".Par.FlagProcess.ProcessActive; 
      UN    "MAL1S033Par".Par.FlagFunction_1.StepUnloading; 
      O     "MAL1S033Par".Par.FlagFunction_1.StepLoading; 
      U     "MAL1S033".ToASW.InfDataReady; 
      UN(   ; 
      UN    "MAL1S033Par".Par.FlagProcess.ProcessActive; 
      UN    "MAL1S033Par".Par.FlagFunction_1.StepLoading; 
      U     #Gr.InManual; 
      U     #USW14; // ManOpen
      U     #Gr.InOutAutostartMan; 
      )     ; 
      )     ; 
      O(    ; 
      U     #Gr.InManual; 
      U     #USW15; // ManClose
      U     #Gr.InOutAutostartMan; 
      )     ; 
//-Hubzüge über min Position zum Schliessen der Ausräumklappe---
      U     "G291M".A_3211_MXZ21_31MA.Ma.OutHigherMinPos; 
      UN    #A_3215_BZS14.InpTrue; // Unloading door close
      UN    #A_3215_BZS14.StFault; 
      U     #Gr.OutEnableEl; 
      =     #A_3215_MXZ11.InEnableRev; 
// 
      U     "G305M".Gr.OutEnablePrevGr; 
      U     #tmpCmdNext; 
      S     #A_3215_MXZ11.CmdNext; 
// 
      U     #Gr.OutSeqStop; 
      =     #A_3215_MXZ11.InSeqStop; 
// 
//-Read inputs---
      U     #A_3215_MXZ11.OutpRunRev; 
      =     #A_3215_MXZ11.InpRunRev; 
      U     #A_3215_MXZ11.OutpRunFwd; 
      =     #A_3215_MXZ11.InpRunFwd; 
      U     #A_3215_MXZ11.OutpRunFwd; 
      =     #A_3215_MXZ11.InpRunFwd; 
      U     #A_3215_MXZ11.OutpRunRev; 
      =     #A_3215_MXZ11.InpRunRev; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #A_3215_MXZ11.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[101]; 
      =     #A_3215_MXZ11.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3215_MXZ11 ;
// 
//-Write outputs---
      U     #A_3215_MXZ11.OutpRunFwd; 
      =     "A_3215_MXZ11_O2"; 
      U     #A_3215_MXZ11.OutpRunRev; 
      =     "A_3215_MXZ11_O1"; 

//- SW-Interlocking anstatt HW-Interlocking ---
      U     #A_3215_MXZ11.OutpRunFwd; 
      U     #A_3215_BZS15.InpTrue; 
      O     ; 
      U     #A_3215_MXZ11.OutpRunRev; 
      U     #A_3215_BZS14.InpTrue; 
      R     #A_3215_MXZ11.InManual; 

NETWORK
TITLE ==A-3215-BZS14 : DISCHARGE FLAP CLOSED

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      =     #A_3215_BZS14.InLocal; 
// 
//-Monitoring---
      U     #A_3215_MXZ11.OutpRunRev; // closing  
      =     #A_3215_BZS14.InMonTrue; 
      U     #A_3215_BZS15.InpTrue; 
      =     #A_3215_BZS14.InMonFalse; 
// 
//-PreAlarm---
      CLR   ; 
      =     #A_3215_BZS14.InPreAlarmTrue; 
      CLR   ; 
      =     #A_3215_BZS14.InPreAlarmFalse; 
// 
//-Additional commands---
      U     #A_3215_MXZ11.StStarting; 
      S     #A_3215_BZS14.CmdRunTimeoutTime; 
// 
//-Read inputs---
      UN    "A_3215_BZS14_I1"; // Öffner-Kontakt!!
      =     #A_3215_BZS14.InpTrue; 
      U     "PLC_DPInfo".Master[1].SlavesError[101]; 
      =     #A_3215_BZS14.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3215_BZS14 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-3215-BZS15 : DISCHARGE FLAP OPENED

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      =     #A_3215_BZS15.InLocal; 
// 
//-Monitoring---
      U     #A_3215_MXZ11.OutpRunFwd; // opening
      =     #A_3215_BZS15.InMonTrue; 
      U     #A_3215_BZS14.InpTrue; 
      =     #A_3215_BZS15.InMonFalse; 
// 
//-PreAlarm---
      CLR   ; 
      =     #A_3215_BZS15.InPreAlarmTrue; 
      CLR   ; 
      =     #A_3215_BZS15.InPreAlarmFalse; 
// 
//-Additional commands---
      U     #A_3215_MXZ11.StStarting; 
      S     #A_3215_BZS15.CmdRunTimeoutTime; 
// 
//-Read inputs---
      UN    "A_3215_BZS15_I1"; // Öffner-Kontakt!!
      =     #A_3215_BZS15.InpTrue; 
      U     "PLC_DPInfo".Master[1].SlavesError[103]; 
      =     #A_3215_BZS15.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3215_BZS15 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-3215-BZS16 : CLEANING FLAP CLOSED

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      =     #A_3215_BZS16.InLocal; 
// 
//-Monitoring---
      U     "MAL1S033Par".Par.FlagFunction_1.StepUnloading; 
      =     #A_3215_BZS16.InMonTrue; 
      CLR   ; 
      =     #A_3215_BZS16.InMonFalse; 
// 
//-PreAlarm---
      CLR   ; 
      =     #A_3215_BZS16.InPreAlarmTrue; 
      CLR   ; 
      =     #A_3215_BZS16.InPreAlarmFalse; 
// 
//-Additional commands---
//      CLR
//      S     #A_3215_BZS16.CmdRunTimeOutTime
// 
//-Read inputs---
      U     "A_3215_BZS16_I1"; 
      =     #A_3215_BZS16.InpTrue; 
      U     "PLC_SystemDB".GlobalFlags.Kilning_2; 
      =     #A_3215_BZS16.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[102]; 
      =     #A_3215_BZS16.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_3215_BZS16 ;
// 
//-Write outputs---
NETWORK
TITLE =Test Fault

      U     #A_3215_MXZ11.StFault; 
      O     #A_3215_BZS14.StFault; 
      O     #A_3215_BZS15.StFault; 
      O     #A_3215_BZS16.StFault; 
      =     #Gr.VarFault; 
NETWORK
TITLE =Test Warning

      CLR   ; 
      =     #Gr.OutWarning; 
NETWORK
TITLE =Test Stopped

      U     #Gr.ModeIdling; 
      U     #Gr.OutSeqStop; 
      O     #Gr.ModePassive; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStopped; 
NETWORK
TITLE =Test Started

//      U     #A_3215_MXZ11.StStartedFwd
      U(    ; 
      UN    "MAL1S033Par".Par.FlagFunction_1.StepUnloading; 
      U     #A_3215_BZS14.StTrue; // UNLOADING DOOR CLOSED
      )     ; 
      O(    ; 
      U     "MAL1S033Par".Par.FlagFunction_1.StepUnloading; 
      U     #A_3215_BZS15.StTrue; // UNLOADING DOOR OPENED
      )     ; 
      UN    #Gr.VarStopped; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStarted; 
NETWORK
TITLE =Enable Previous Group

      U     #Gr.VarStarted; 
      =     #Gr.OutEnablePrevGr; 
NETWORK
TITLE =Call Methode State
//1. Make State of the group: This function use the signals:
//        Gr.VarStopped, Gr.VarStarted, Gr.VarFault
//2.Reset Commands to group: e.g.CmdFaultReset
      CALL #Gr (
           FcCode                   := 2);
NETWORK
TITLE =Init

      SET   ; 
      R     #CmdNewValue; 
      R     #CmdNewJob; 
NETWORK
TITLE =Write data to group control

      U     #Gr.StStopped; 
      =     "GCS7_GrCtrlDB".InStopped[294]; 
      U     #Gr.StStarted; 
      =     "GCS7_GrCtrlDB".InStarted[294]; 
      U     #Gr.StFault; 
      =     "GCS7_GrCtrlDB".InFault[294]; 
      U     #Gr.OutWarning; 
      =     "GCS7_GrCtrlDB".InWarning[294]; 
      U     #Gr.OutManual; 
      =     "GCS7_GrCtrlDB".InManual[294]; 
      U     #Gr.OutHWStop; 
      =     "GCS7_GrCtrlDB".InHWStop[294]; 
      U     #Gr.OutDPFault; 
      =     "GCS7_GrCtrlDB".InDPFault[294]; 
      U     #Gr.OutLoaded; 
      =     "GCS7_GrCtrlDB".InLoaded[294]; 
      U     #Gr.OutAutostart; 
      =     "GCS7_GrCtrlDB".InAutoStart[294]; 
      U     #Gr.VarStartwarning; 
      =     "GCS7_GrCtrlDB".InStartWarning[294]; 
NETWORK
TITLE =End of block

      BE    ; 

END_FUNCTION_BLOCK

