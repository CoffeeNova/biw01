FUNCTION_BLOCK "G079M_Code"
TITLE =G079M CO2 FAN AND FESH AIR FLAP STEEPING 22
//COPYRIGHT:    (C) 2009  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      WinCoS_BIW01_09
//
//TITLE:        CO2 FAN AND FESH AIR FLAP STEEPING 22
//                Machine group control
//                Maschinengruppensteuerung
// 
//--------------------------------------------------------------------------------
// 
//IDENT:        AUTHOR:             DEPT:                              DATE:
// 
//P01_FB179.1.0 K.Renz              CSB41                              29.Sep.2009
//             1st Release
// 
//--------------------------------------------------------------------------------
// 
//PURPOSE: 
// 
//MANUAL : 
// 
//RESTRICTIONS: 
// 
//--------------------------------------------------------------------------------
AUTHOR : KR
FAMILY : USW
VERSION : 9.9


VAR
  Gr : "GCS7_GrpCtrlSub";	//Standard interface to group
  InOutValue1 : DINT ;	//Data for user
  InOutValue2 : DINT ;	//Data for user
  OutWayConflict : BOOL ;	//Data for user
  OutSelectionOk : BOOL ;	//Data for user
  CmdNewValue : BOOL ;	//Data for user
  CmdNewJob : BOOL ;	//Data for user
  OutCodeOK : BOOL ;	//Data for user
  InEndOfJob : BOOL ;	//Data for user
  staWayOk : BOOL ;	//Data for user
  EdgeStartRelease : BOOL ;	//Data for user
  USW8 : BOOL ;	//Data for user
  USW9 : BOOL ;	//Data for user
  USW10 : BOOL ;	//Data for user
  USW11 : BOOL ;	//Data for user
  USW12 : BOOL ;	//Data for user
  USW13 : BOOL ;	//Data for user
  USW14 : BOOL ;	//Data for user
  USW15 : BOOL ;	//Data for user
  A_1132_MXZ11 : "GCS7_E_Motor";	// Motor 
  A_1182_MXZ_BZS01_02 : "GCS7_E_ValveControl";	// VLS 
END_VAR
VAR_TEMP
  tmpEnable : BOOL ;	
  tmpFlag : BOOL ;	
  tmpCmdNext : BOOL ;	
  tmpLocal : BOOL ;	
  tmpAspOk : BOOL ;	
  tmpHWStop : BOOL ;	
  tmpInstanzDBNo : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =Read command from group control
//Parameter
      SET   ; 
      S     "GCS7_GrCtrlDB".ParMGR[79]; 
      R     "GCS7_GrCtrlDB".ParWGR[79]; 
      R     "GCS7_GrCtrlDB".ParGGR[79]; 
      R     "GCS7_GrCtrlDB".ParFeed[79]; 
      R     "GCS7_GrCtrlDB".ParShare[79]; 
      S     #Gr.Par.ParMgr; 
      R     #Gr.Par.ParWgr; 
      R     #Gr.Par.ParGgr; 
//Command
      U     "GCS7_GrCtrlDB".OutEnable[79]; 
      =     #Gr.InEnableGr; 
      U     "GCS7_GrCtrlDB".OutOccupied[79]; 
      =     #Gr.InOccupied; 
      U     "GCS7_GrCtrlDB".CmdFaultReset[79]; 
      R     "GCS7_GrCtrlDB".CmdFaultReset[79]; 
      S     #Gr.CmdFaultReset; 
      U     "GCS7_GrCtrlDB".CmdNext[79]; 
      R     "GCS7_GrCtrlDB".CmdNext[79]; 
      S     #Gr.CmdNext; 
      U     "GCS7_GrCtrlDB".CmdManualOff[79]; 
      UN    #Gr.InManual; 
      S     #Gr.CmdManualOff; 
      U     "GCS7_GrCtrlDB".CmdManualOff[79]; 
      R     "GCS7_GrCtrlDB".CmdManualOff[79]; 

//      U     "GCS7_GrCtrlDB".CmdManualOff[79]
//      O     "GCS7_GrCtrlDB".OutOccupied[79]
//      R     "GCS7_GrCtrlDB".CmdManualOff[79]
//      S     #Gr.CmdManualOff

      U     "GCS7_GrCtrlDB".CmdContinue[79]; 
      R     "GCS7_GrCtrlDB".CmdContinue[79]; 
      S     #Gr.CmdContinue; 
//Init Line Number
      U     "GCS7_GrCtrlDB".ParShare[79]; 
      SPBN  WE00; 
      L     0; 
      T     #Gr.InLine; 
WE00: NOP   0; 
NETWORK
TITLE =Call Methode Control
// 1. Handle Interface PCControl      
// 2. Prepare mode --> ModePassive,ModeActive,ModeIdling      
// 3. Reset global flags of the group
      CALL #Gr (
           FcCode                   := 1);
NETWORK
TITLE =Restart Idling Time

      L     #Gr.Par.ParIdling; 
      L     2; 
      >=I   ; 
      SPB   IdTm; 
      T     #Gr.Par.ParIdling; 
IdTm: NOP   0; 
      CLR   ; 
      S     #Gr.CmdRestartIdling; 
NETWORK
TITLE =Prepare global flags



NETWORK
TITLE =Set Pattern (Target position)



NETWORK
TITLE =Interlocking WayOk



NETWORK
TITLE =Interlocking Autostart

      CLR   ; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE =Interlocking Enable

      U     #Gr.InEnableGr; 
      U     #Gr.InOccupied; 
      =     #Gr.OutEnableEl; 

NETWORK
TITLE =Interlocking Next

      U     #Gr.CmdNext; 
      U     #Gr.OutAutostart; 
      UN    #Gr.OutNewWay; 
      U(    ; 
      O     "G076M".Gr.OutEnablePrevGr; // fan steep1 is started
      O     "GCS7_GrCtrlDB".InFault[76]; 
      )     ; 
      =     #tmpCmdNext; 

NETWORK
TITLE =Interlocking Local / HWStop

      U     "PLC_SystemDB".HardwareStop.Steeping; 
      =     #tmpHWStop; 

      U     #Gr.ModePassive; 
      O     #tmpHWStop; 
      =     #tmpLocal; 

NETWORK
TITLE =Interlocking Start Warning
//      NOP   0
////- Restart after fault --
//      U     #Gr.CmdFaultReset
//      U     #Gr.VarFault
//      S     "SYS_StartWarningDB".Area[1].CmdResetStartWarning
////- Run Timer --
//      AUF   "G079M"
//      CALL  "GCS7_StartWarning" , "SYS_StartWarningDB"
//       InModeHigher   :=FALSE
//       InArea         :=1
//       InEnable       :=#Gr.OutEnableEl
//       InLocal        :=#tmpLocal
//       InStarted      :=#Gr.VarStarted
//       InCmdNext      :=#tmpCmdNext
//       OutCmdNext     :=#tmpCmdNext
//       OutStartwarning:=#Gr.VarStartwarning
////- Store Startwarning done  --
////- Reset Autostart --
//      U     "SYS_StartWarningDB".TmpArea.OutStartReleaseRunningL
//      FN    #EdgeStartRelease
//      U     #Gr.VarFault
//      R     #Gr.OutAutostart
//


NETWORK
TITLE ==A-1132-MXZ11 : CO2 FAN ST 22

      NOP   0; 
//-InLocal--- 
      U     #tmpLocal; 
      =     #A_1132_MXZ11.InLocal; 
//
//-Interlocking--- 
      U     "MAL1S007Par".Par.FlagProcess1.CO2SuckStart; // CO2 Suck Run
      U     "MAL1S007Par".Par.FlagProcess1.CO2SuckActive; // CO2 Suck Active
      UN    "MAL1S007Par".Par.FlagProcess1.CO2SuckPause; // CO2 Suck Pause
      U     #A_1182_MXZ_BZS01_02.StHN; 
      U     #Gr.OutEnableEl; 
      =     #A_1132_MXZ11.InEnableFwd; 
// 
      U     #tmpCmdNext; 
      S     #A_1132_MXZ11.CmdNext; 
// 
      U     #Gr.OutSeqStop; 
      =     #A_1132_MXZ11.InSeqStop; 
// 
//-Read inputs---
      U     "A_1132_MXZ11_I1"; 
      =     #A_1132_MXZ11.InpRunFwd; 
      U     "PLC_SystemDB".HardwareStop.Steeping; 
      =     #A_1132_MXZ11.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[5]; 
      =     #A_1132_MXZ11.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_1132_MXZ11 ;
// 
//-Write outputs---
      U     #A_1132_MXZ11.OutpRunFwd; 
      =     "A_1132_MXZ11_O1"; 
NETWORK
TITLE ==A-1182-MXZ/BZS01/02 : CO2 VALVE ST 22

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      =     #A_1182_MXZ_BZS01_02.InLocal; 
//
//-Interlocking---
      U     "MAL1S007Par".Par.FlagProcess1.CO2SuckStart; // CO2 Suck Run
      U     #Gr.OutEnableEl; 
//      U     #A_1132_MXZ11.StStartedFwd
      =     #A_1182_MXZ_BZS01_02.InEnableHN; 
      NOT   ; 
      =     #A_1182_MXZ_BZS01_02.InEnableLN; 
//
      SET   ; 
      S     #A_1182_MXZ_BZS01_02.CmdNext; 
//
      U     #Gr.OutSeqStop; 
      =     #A_1182_MXZ_BZS01_02.InSeqStop; 
// 
//-Read inputs---
      U     "A_1182_BZS01_I1"; 
      =     #A_1182_MXZ_BZS01_02.InpLN; 
      U     "A_1182_BZS02_I1"; 
      =     #A_1182_MXZ_BZS01_02.InpHN; 
// 1.1
      CLR   ; 
      =     #A_1182_MXZ_BZS01_02.InpFaultDev; 
      U     "PLC_SystemDB".HardwareStop.Steeping; 
      =     #A_1182_MXZ_BZS01_02.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[8]; 
      O     "PLC_DPInfo".Master[1].SlavesError[9]; 
      =     #A_1182_MXZ_BZS01_02.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_1182_MXZ_BZS01_02 ;
// 
//-Write outputs---
// 1.1
      U     #A_1182_MXZ_BZS01_02.OutpLN; 
      =     "A_1182_MXZ01_O1"; 
      U     #A_1182_MXZ_BZS01_02.OutpHN; 
      =     "A_1182_MXZ01_O2"; 
NETWORK
TITLE =Test Fault

      U     #A_1132_MXZ11.StFault; 
      O     #A_1182_MXZ_BZS01_02.StFault; 
      =     #Gr.VarFault; 
NETWORK
TITLE =Test Warning

      CLR   ; 
      =     #Gr.OutWarning; 
NETWORK
TITLE =Test Stopped

      U     #A_1182_MXZ_BZS01_02.StLN; 
      U     #A_1132_MXZ11.StStopped; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStopped; 
NETWORK
TITLE =Test Started

      U     #A_1132_MXZ11.StStartedFwd; 
      U     #A_1182_MXZ_BZS01_02.StHN; 
      UN    #Gr.VarStopped; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStarted; 
NETWORK
TITLE =Enable Previous Group

      U     #Gr.VarStarted; 
      =     #Gr.OutEnablePrevGr; 
NETWORK
TITLE =Call Methode State
//1. Make State of the group: This function use the signals:
//        Gr.VarStopped, Gr.VarStarted, Gr.VarFault
//2.Reset Commands to group: e.g.CmdFaultReset
      CALL #Gr (
           FcCode                   := 2);
NETWORK
TITLE =Init

      SET   ; 
      R     #CmdNewValue; 
      R     #CmdNewJob; 
NETWORK
TITLE =Write data to group control

      U     #Gr.StStopped; 
      =     "GCS7_GrCtrlDB".InStopped[79]; 
      U     #Gr.StStarted; 
      =     "GCS7_GrCtrlDB".InStarted[79]; 
      U     #Gr.StFault; 
      =     "GCS7_GrCtrlDB".InFault[79]; 
      U     #Gr.OutWarning; 
      =     "GCS7_GrCtrlDB".InWarning[79]; 
      U     #Gr.OutManual; 
      =     "GCS7_GrCtrlDB".InManual[79]; 
      U     #Gr.OutHWStop; 
      =     "GCS7_GrCtrlDB".InHWStop[79]; 
      U     #Gr.OutDPFault; 
      =     "GCS7_GrCtrlDB".InDPFault[79]; 
      U     #Gr.OutLoaded; 
      =     "GCS7_GrCtrlDB".InLoaded[79]; 
      U     #Gr.OutAutostart; 
      =     "GCS7_GrCtrlDB".InAutoStart[79]; 
      U     #Gr.VarStartwarning; 
      =     "GCS7_GrCtrlDB".InStartWarning[79]; 
NETWORK
TITLE =End of block

      BE    ; 

END_FUNCTION_BLOCK

