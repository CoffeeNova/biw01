FUNCTION_BLOCK "G109M_Code"
TITLE =G109M HP CLEANING GERM 1
//COPYRIGHT:    (C) 2009  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      WinCoS_BIW01_09
//
//TITLE:        HP CLEANING GERM 1
//                Machine group control
//                Maschinengruppensteuerung
// 
//--------------------------------------------------------------------------------
// 
//IDENT:        AUTHOR:             DEPT:                              DATE:
// 
//P01_FB209.1.0 K.Renz              CSB41                              29.Sep.2009
//             1st Release
// 
//--------------------------------------------------------------------------------
// 
//PURPOSE: 
// 
//MANUAL : 
// 
//RESTRICTIONS: 
// 
//--------------------------------------------------------------------------------
AUTHOR : KR
FAMILY : USW
VERSION : 9.9


VAR
  Gr : "GCS7_GrpCtrlSub";	//Standard interface to group
  InOutValue1 : DINT ;	//Data for user
  InOutValue2 : DINT ;	//Data for user
  OutWayConflict : BOOL ;	//Data for user
  OutSelectionOk : BOOL ;	//Data for user
  CmdNewValue : BOOL ;	//Data for user
  CmdNewJob : BOOL ;	//Data for user
  OutCodeOK : BOOL ;	//Data for user
  InEndOfJob : BOOL ;	//Data for user
  staWayOk : BOOL ;	//Data for user
  EdgeStartRelease : BOOL ;	//Data for user
  USW8 : BOOL ;	//Data for user
  USW9 : BOOL ;	//Data for user
  USW10 : BOOL ;	//Data for user
  USW11 : BOOL ;	//Data for user
  USW12 : BOOL ;	//Data for user
  USW13 : BOOL ;	//Data for user
  USW14 : BOOL ;	//Data for user
  USW15 : BOOL ;	//Data for user
  A_2113_QXV_BZS11_12 : "GCS7_E_ValveControl";	// VLS 
  A_2115_QYS18 : "GCS7_E_DigInput";	// DigInpCont 
  A_2115_QYS19 : "GCS7_E_DigInput";	// DigInpCont 
  A_2115MXZ91 : "GCS7_E_Motor";	// Motor 
END_VAR
VAR_TEMP
  tmpEnable : BOOL ;	
  tmpFlag : BOOL ;	
  tmpCmdNext : BOOL ;	
  tmpLocal : BOOL ;	
  tmpAspOk : BOOL ;	
  tmpHWStop : BOOL ;	
  tmpInstanzDBNo : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =Read command from group control
//Parameter
      SET   ; 
      S     "GCS7_GrCtrlDB".ParMGR[109]; 
      R     "GCS7_GrCtrlDB".ParWGR[109]; 
      R     "GCS7_GrCtrlDB".ParGGR[109]; 
      R     "GCS7_GrCtrlDB".ParFeed[109]; 
      R     "GCS7_GrCtrlDB".ParShare[109]; 
      S     #Gr.Par.ParMgr; 
      R     #Gr.Par.ParWgr; 
      R     #Gr.Par.ParGgr; 
//Command
      U     "GCS7_GrCtrlDB".OutEnable[109]; 
      =     #Gr.InEnableGr; 
      U     "GCS7_GrCtrlDB".OutOccupied[109]; 
      =     #Gr.InOccupied; 
      U     "GCS7_GrCtrlDB".CmdFaultReset[109]; 
      R     "GCS7_GrCtrlDB".CmdFaultReset[109]; 
      S     #Gr.CmdFaultReset; 
      U     "GCS7_GrCtrlDB".CmdNext[109]; 
      R     "GCS7_GrCtrlDB".CmdNext[109]; 
      S     #Gr.CmdNext; 

      U     "GCS7_GrCtrlDB".CmdManualOff[109]; 
      UN    #Gr.InManual; 
      S     #Gr.CmdManualOff; 
      U     "GCS7_GrCtrlDB".CmdManualOff[109]; 
      R     "GCS7_GrCtrlDB".CmdManualOff[109]; 

//      U     "GCS7_GrCtrlDB".CmdManualOff[109]
//      O     "GCS7_GrCtrlDB".OutOccupied[109]
//      R     "GCS7_GrCtrlDB".CmdManualOff[109]
//      S     #Gr.CmdManualOff

      U     "GCS7_GrCtrlDB".CmdContinue[109]; 
      R     "GCS7_GrCtrlDB".CmdContinue[109]; 
      S     #Gr.CmdContinue; 
//Init Line Number
      U     "GCS7_GrCtrlDB".ParShare[109]; 
      SPBN  WE00; 
      L     0; 
      T     #Gr.InLine; 
WE00: NOP   0; 
NETWORK
TITLE =Call Methode Control
// 1. Handle Interface PCControl      
// 2. Prepare mode --> ModePassive,ModeActive,ModeIdling      
// 3. Reset global flags of the group
      CALL #Gr (
           FcCode                   := 1);
NETWORK
TITLE =Restart Idling Time

      L     #Gr.Par.ParIdling; 
      L     2; 
      >=I   ; 
      SPB   IdTm; 
      T     #Gr.Par.ParIdling; 
IdTm: NOP   0; 
      CLR   ; 
      S     #Gr.CmdRestartIdling; 
NETWORK
TITLE =Prepare global flags



NETWORK
TITLE =Set Pattern (Target position)



NETWORK
TITLE =Interlocking WayOk



NETWORK
TITLE =Interlocking Autostart

      CLR   ; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE =Interlocking Enable

      U     #Gr.InEnableGr; 
      =     #Gr.OutEnableEl; 

NETWORK
TITLE =Interlocking Next

      U     #Gr.CmdNext; 
      U     #Gr.OutAutostart; 
      UN    #Gr.OutNewWay; 
      =     #tmpCmdNext; 

NETWORK
TITLE =Interlocking Local / HWStop

      U     "PLC_SystemDB".HardwareStop.Germinating_1; 
      =     #tmpHWStop; 

      U     #Gr.ModePassive; 
      O     #tmpHWStop; 
      =     #tmpLocal; 

NETWORK
TITLE =Interlocking Start Warning

      NOP   0; 
//- Restart after fault --
      U     #Gr.CmdFaultReset; 
      U     #Gr.VarFault; 
      S     "SYS_StartWarningDB".Area[2].CmdResetStartWarning; 
//- Run Timer --
      AUF   "G109M"; 
      CALL "GCS7_StartWarning" , "SYS_StartWarningDB" (
           InModeHigher             := FALSE,
           InArea                   := 2,
           InEnable                 := #Gr.OutEnableEl,
           InLocal                  := #tmpLocal,
           InStarted                := #Gr.VarStarted,
           InCmdNext                := #tmpCmdNext,
           OutCmdNext               := #tmpCmdNext,
           OutStartwarning          := #Gr.VarStartwarning);
//- Store Startwarning done  --
//- Reset Autostart --
      U     "SYS_StartWarningDB".TmpArea.OutStartReleaseRunningL; 
      FN    #EdgeStartRelease; 
      U     #Gr.VarFault; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE ==A-2113-QXV/BZS11/12 : HP WATER

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      =     #A_2113_QXV_BZS11_12.InLocal; 
//
//-Interlocking---
      U     #Gr.OutEnableEl; 
      U     #A_2115_QYS18.StTrue; // cleaning flap is closed
      =     #A_2113_QXV_BZS11_12.InEnableHN; 
      NOT   ; 
      =     #A_2113_QXV_BZS11_12.InEnableLN; 
//
      SET   ; 
      S     #A_2113_QXV_BZS11_12.CmdNext; 
//
      U     #Gr.OutSeqStop; 
      =     #A_2113_QXV_BZS11_12.InSeqStop; 
// 
//-Read inputs---
      UN    #A_2113_QXV_BZS11_12.OutpHN; 
      =     #A_2113_QXV_BZS11_12.InpLN; 
      U     #A_2113_QXV_BZS11_12.OutpHN; 
      =     #A_2113_QXV_BZS11_12.InpHN; 
      U     "PLC_SystemDB".HardwareStop.Steeping; 
      =     #A_2113_QXV_BZS11_12.InHWStop; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2113_QXV_BZS11_12 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2115-QYS18 : CLEANING FLAP 1

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      UN    "G101M".A_2115_MXZ15MA.Ma.InEnableFwd; 
      UN    "G101M".A_2115_MXZ15MA.A_2115_MXZ15.OutpRunFwd; 
      UN    "G101M".A_2115_MXZ15MA.A_2115_MXZ15.InManual; 
      =     #A_2115_QYS18.InLocal; 
// 
//-Monitoring---
      U     "G101M".A_2115_MXZ15MA.Ma.InEnableFwd; 
      O     "G101M".A_2115_MXZ15MA.A_2115_MXZ15.OutpRunFwd; 
      O     "G101M".A_2115_MXZ15MA.A_2115_MXZ15.InManual; 
      =     #A_2115_QYS18.InMonTrue; 
      CLR   ; 
      =     #A_2115_QYS18.InMonFalse; 
// 
//-PreAlarm---
      U     "G101M".A_2115_MXZ15MA.Ma.InEnableFwd; 
      O     "G101M".A_2115_MXZ15MA.A_2115_MXZ15.OutpRunFwd; 
      O     "G101M".A_2115_MXZ15MA.A_2115_MXZ15.InManual; 
      =     #A_2115_QYS18.InPreAlarmTrue; 
      CLR   ; 
      =     #A_2115_QYS18.InPreAlarmFalse; 
// 
//-Additional commands---
//      CLR
//      S     #A_2115_QYS18.CmdRunTimeOutTime
// 
//-Read inputs---
      UN    "A_2115_QYS18_I1"; // inverted
      =     #A_2115_QYS18.InpTrue; 
      U     "PLC_SystemDB".HardwareStop.Germinating_1; 
      =     #A_2115_QYS18.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[15]; 
      =     #A_2115_QYS18.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2115_QYS18 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2115-QYS19 : CLEANING FLAP 2

      NOP   0; 
//-InLocal---
      U     #tmpLocal; 
      UN    "G101M".A_2115_MXZ15MA.Ma.InEnableFwd; 
      UN    "G101M".A_2115_MXZ15MA.A_2115_MXZ15.OutpRunFwd; 
      UN    "G101M".A_2115_MXZ15MA.A_2115_MXZ15.InManual; 
      =     #A_2115_QYS19.InLocal; 
// 
//-Monitoring---
      U     "G101M".A_2115_MXZ15MA.Ma.InEnableFwd; 
      O     "G101M".A_2115_MXZ15MA.A_2115_MXZ15.OutpRunFwd; 
      O     "G101M".A_2115_MXZ15MA.A_2115_MXZ15.InManual; 
      =     #A_2115_QYS19.InMonTrue; 
      CLR   ; 
      =     #A_2115_QYS19.InMonFalse; 
// 
//-PreAlarm---
      U     "G101M".A_2115_MXZ15MA.Ma.InEnableFwd; 
      O     "G101M".A_2115_MXZ15MA.A_2115_MXZ15.OutpRunFwd; 
      O     "G101M".A_2115_MXZ15MA.A_2115_MXZ15.InManual; 
      =     #A_2115_QYS19.InPreAlarmTrue; 
      CLR   ; 
      =     #A_2115_QYS19.InPreAlarmFalse; 
// 
//-Additional commands---
//      CLR
//      S     #A_2115_QYS19.CmdRunTimeOutTime
// 
//-Read inputs---
      UN    "A_2115_QYS19_I1"; // inverted
      =     #A_2115_QYS19.InpTrue; 
      U     "PLC_SystemDB".HardwareStop.Germinating_1; 
      =     #A_2115_QYS19.InHWStop; 
      U     "PLC_DPInfo".Master[1].SlavesError[15]; 
      =     #A_2115_QYS19.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2115_QYS19 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2115MXZ91 : CLLEANING DEVICE

      NOP   0; 
//-InLocal--- 
      U     #tmpLocal; 
      =     #A_2115MXZ91.InLocal; 
//
//-Interlocking--- 
      U(    ; 
      U     #A_2113_QXV_BZS11_12.InEnableLN; 
      U     #A_2113_QXV_BZS11_12.StLN; 
      O     ; 
      U     #A_2113_QXV_BZS11_12.InEnableHN; 
      U     #A_2113_QXV_BZS11_12.StHN; 
      )     ; 
      U     #Gr.OutEnableEl; 
      =     #A_2115MXZ91.InEnableFwd; 
// 
      U     #tmpCmdNext; 
      S     #A_2115MXZ91.CmdNext; 
// 
      U     #Gr.OutSeqStop; 
      =     #A_2115MXZ91.InSeqStop; 
// 
//-Read inputs---
      U     #A_2115MXZ91.OutpRunFwd; 
      =     #A_2115MXZ91.InpRunFwd; 
      U     "PLC_SystemDB".HardwareStop.Germinating_1; 
      =     #A_2115MXZ91.InHWStop; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2115MXZ91 ;
// 
//-Write outputs---
NETWORK
TITLE =Test Fault

      U     #A_2113_QXV_BZS11_12.StFault; 
      O     #A_2115_QYS18.StFault; 
      O     #A_2115_QYS19.StFault; 
      O     #A_2115MXZ91.StFault; 
      =     #Gr.VarFault; 
NETWORK
TITLE =Test Warning

      CLR   ; 
      =     #Gr.OutWarning; 
NETWORK
TITLE =Test Stopped

      U     #A_2115MXZ91.StStopped; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStopped; 
NETWORK
TITLE =Test Started

      U     #A_2113_QXV_BZS11_12.StHN; 
      U     #A_2115MXZ91.StStartedFwd; 
      UN    #Gr.VarStopped; 
      UN    #Gr.VarFault; 
      =     #Gr.VarStarted; 
NETWORK
TITLE =Enable Previous Group

      U     #Gr.VarStarted; 
      =     #Gr.OutEnablePrevGr; 
NETWORK
TITLE =Call Methode State
//1. Make State of the group: This function use the signals:
//        Gr.VarStopped, Gr.VarStarted, Gr.VarFault
//2.Reset Commands to group: e.g.CmdFaultReset
      CALL #Gr (
           FcCode                   := 2);
NETWORK
TITLE =Init

      SET   ; 
      R     #CmdNewValue; 
      R     #CmdNewJob; 
NETWORK
TITLE =Write data to group control

      U     #Gr.StStopped; 
      =     "GCS7_GrCtrlDB".InStopped[109]; 
      U     #Gr.StStarted; 
      =     "GCS7_GrCtrlDB".InStarted[109]; 
      U     #Gr.StFault; 
      =     "GCS7_GrCtrlDB".InFault[109]; 
      U     #Gr.OutWarning; 
      =     "GCS7_GrCtrlDB".InWarning[109]; 
      U     #Gr.OutManual; 
      =     "GCS7_GrCtrlDB".InManual[109]; 
      U     #Gr.OutHWStop; 
      =     "GCS7_GrCtrlDB".InHWStop[109]; 
      U     #Gr.OutDPFault; 
      =     "GCS7_GrCtrlDB".InDPFault[109]; 
      U     #Gr.OutLoaded; 
      =     "GCS7_GrCtrlDB".InLoaded[109]; 
      U     #Gr.OutAutostart; 
      =     "GCS7_GrCtrlDB".InAutoStart[109]; 
      U     #Gr.VarStartwarning; 
      =     "GCS7_GrCtrlDB".InStartWarning[109]; 
NETWORK
TITLE =End of block

      BE    ; 

END_FUNCTION_BLOCK

