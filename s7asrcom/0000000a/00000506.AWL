FUNCTION_BLOCK "G250M_Code"
TITLE =G250M FAN AND VALVE SRAY AIR GERM 8
//COPYRIGHT:    (C) 2009  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      WinCoS_BIW01_09
//
//TITLE:        FAN AND VALVE SRAY AIR GERM 8
//                Machine group control
//                Maschinengruppensteuerung
// 
//--------------------------------------------------------------------------------
// 
//IDENT:        AUTHOR:             DEPT:                              DATE:
// 
//P01_FB350.1.0 K.Renz              CSB41                              13.Jan.2010
//             1st Release
// 
//--------------------------------------------------------------------------------
// 
//PURPOSE: 
// 
//MANUAL : 
// 
//RESTRICTIONS: 
// 
//--------------------------------------------------------------------------------
AUTHOR : KR
FAMILY : USW
VERSION : 9.9


VAR
  Gr : "GCS7_GrpCtrlSub";	//Standard interface to group
  InOutValue1 : DINT ;	//Data for user
  InOutValue2 : DINT ;	//Data for user
  OutWayConflict : BOOL ;	//Data for user
  OutSelectionOk : BOOL ;	//Data for user
  CmdNewValue : BOOL ;	//Data for user
  CmdNewJob : BOOL ;	//Data for user
  OutCodeOK : BOOL ;	//Data for user
  InEndOfJob : BOOL ;	//Data for user
  staWayOk : BOOL ;	//Data for user
  EdgeStartRelease : BOOL ;	//Data for user
  AirMinOpened_1 : BOOL ;	//Data for user
  AirMinOpened_2 : BOOL ;	//Data for user
  USW10 : BOOL ;	//Data for user
  USW11 : BOOL ;	//Data for user
  USW12 : BOOL ;	//Data for user
  USW13 : BOOL ;	//Data for user
  USW14 : BOOL ;	//Data for user
  USW15 : BOOL ;	//Data for user
  A_2831_QXV12 : "GCS7_E_ValveControl";	// VLS 
  A_2831_QXV22 : "GCS7_E_ValveControl";	// VLS 
  A_2860_MXZ01 : "GCS7_E_Motor";	// Motor 
  A_2860_MXZ02 : "GCS7_E_Motor";	// Motor 
  A_2860_TSC01_COM : "SSA_ABB";	// FU_ABB 
  A_2860_TSC01_PD1 : "GCS7_E_AnalogInput";	// AnaInp 
  A_2860_TSC01_PD2 : "GCS7_E_AnalogInput";	// AnaInp 
  A_2860_TSC01_PD3 : "GCS7_E_AnalogInput";	// AnaInp 
  A_2860_TSC01_PD4 : "GCS7_E_AnalogInput";	// AnaInp 
  A_2860_TSC02_COM : "SSA_ABB";	// FU_ABB 
  A_2860_TSC02_PD1 : "GCS7_E_AnalogInput";	// AnaInp 
  A_2860_TSC02_PD2 : "GCS7_E_AnalogInput";	// AnaInp 
  A_2860_TSC02_PD3 : "GCS7_E_AnalogInput";	// AnaInp 
  A_2860_TSC02_PD4 : "GCS7_E_AnalogInput";	// AnaInp 
END_VAR
VAR_TEMP
  tmpEnable : BOOL ;	
  tmpFlag : BOOL ;	
  tmpCmdNext : BOOL ;	
  tmpLocal : BOOL ;	
  tmpAspOk : BOOL ;	
  tmpHWStop : BOOL ;	
  tmpInstanzDBNo : INT ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =Read command from group control
//Parameter
      SET   ; 
      S     "GCS7_GrCtrlDB".ParMGR[250]; 
      R     "GCS7_GrCtrlDB".ParWGR[250]; 
      R     "GCS7_GrCtrlDB".ParGGR[250]; 
      R     "GCS7_GrCtrlDB".ParFeed[250]; 
      R     "GCS7_GrCtrlDB".ParShare[250]; 
      S     #Gr.Par.ParMgr; 
      R     #Gr.Par.ParWgr; 
      R     #Gr.Par.ParGgr; 
//Command
      A     "GCS7_GrCtrlDB".OutEnable[250]; 
      =     #Gr.InEnableGr; 
      A     "GCS7_GrCtrlDB".OutOccupied[250]; 
      =     #Gr.InOccupied; 
      A     "GCS7_GrCtrlDB".CmdFaultReset[250]; 
      R     "GCS7_GrCtrlDB".CmdFaultReset[250]; 
      S     #Gr.CmdFaultReset; 
      A     "GCS7_GrCtrlDB".CmdNext[250]; 
      R     "GCS7_GrCtrlDB".CmdNext[250]; 
      S     #Gr.CmdNext; 

      A     "GCS7_GrCtrlDB".CmdManualOff[250]; 
      AN    #Gr.InManual; 
      S     #Gr.CmdManualOff; 
      A     "GCS7_GrCtrlDB".CmdManualOff[250]; 
      R     "GCS7_GrCtrlDB".CmdManualOff[250]; 

//      U     "GCS7_GrCtrlDB".CmdManualOff[250]
//      O     "GCS7_GrCtrlDB".OutOccupied[250]
//      R     "GCS7_GrCtrlDB".CmdManualOff[250]
//      S     #Gr.CmdManualOff

      A     "GCS7_GrCtrlDB".CmdContinue[250]; 
      R     "GCS7_GrCtrlDB".CmdContinue[250]; 
      S     #Gr.CmdContinue; 
//Init Line Number
      A     "GCS7_GrCtrlDB".ParShare[250]; 
      JCN   WE00; 
      L     0; 
      T     #Gr.InLine; 
WE00: NOP   0; 
NETWORK
TITLE =Call Methode Control
// 1. Handle Interface PCControl      
// 2. Prepare mode --> ModePassive,ModeActive,ModeIdling      
// 3. Reset global flags of the group
      CALL #Gr (
           FcCode                   := 1);
NETWORK
TITLE =Restart Idling Time

      L     #Gr.Par.ParIdling; 
      L     2; 
      >=I   ; 
      JC    IdTm; 
      T     #Gr.Par.ParIdling; 
IdTm: NOP   0; 
      CLR   ; 
      S     #Gr.CmdRestartIdling; 
NETWORK
TITLE =Prepare global flags



NETWORK
TITLE =Set Pattern (Target position)
////-Target speed fan---
//// If both louvers are closed, the fan speed must reduce  
//      L     "G251M".A_2851_MXZ01.OutPosition    //Fresh Air Flap
//      L     "G252M".A_2850_MXZ01.OutPosition    //Exhaust Air Flap
//      +I    
//      L     200                         //20.0%
//      <I    
//      L     50                          //50% fan speed
//      SPB   En06
//      U     #Gr.InManual
//      SPB   ILO6
//      L     "MAL1S024Par".Par.ParIntMachine.TargetSpeedGermFan
//      SPA   En06
//ILO6: NOP   0
//      L     "MAL1S024Par".Par.ParIntProcess.Speed_ILO_Fan
//En06: NOP   0
//      L     100
//      *I    
//      T     #A_2860_TSC01_COM.OutValuePC    // xxx.xx%  
//
//// If both louvers are closed, the fan speed must reduce  
//      L     "G251M".A_2852_MXZ01.OutPosition    //Fresh Air Flap
//      L     "G252M".A_2850_MXZ01.OutPosition    //Exhaust Air Flap
//      +I    
//      L     200                         //20.0%
//      <I    
//      L     50                          //50% fan speed
//      SPB   En07
//      U     #Gr.InManual
//      SPB   ILO7
//      L     "MAL1S024Par".Par.ParIntMachine.TargetSpeedGermFan
//      SPA   En07
//ILO7: NOP   0
//      L     "MAL1S024Par".Par.ParIntProcess.Speed_ILO_Fan
//En07: NOP   0
//      L     100
//      *I    
//      T     #A_2860_TSC02_COM.OutValuePC    // xxx.xx%  
//BRT100310 - bei 0% Firschluft hat es 100% Umluft, da mechanisch gekoppelt!
//-Target speed fan---
// If both louvers are closed, the fan speed must reduce  
//      L     "G241M".A_2851_MXZ01.OutPosition    //Fresh Air Flap
//      L     "G242M".A_2850_MXZ01.OutPosition    //Exhaust Air Flap
//      +I    
//      L     200                         //20.0%
//      <I    
//      L     50                          //50% fan speed
//      SPB   En06
//17.02.18 donky
// If  summ position of external and internal louvers are smaller than 40%, the fan speed must reduce to minimum (20%)
      L     "G251M".A_2851_MXZ01.OutPosition; 
      L     "G251M".A_2851_MXZ02.OutPosition; 
      >I    ; 
      JCN   A1; 
      TAK   ; 
A1:   T     MW   277; 
      L     "G251M".A_2851_MXZ03.OutPosition; 
      L     0; 
      >I    ; 
      JCN   A2; 
      TAK   ; 
A2:   L     MW   277; 
      +I    ; 
      L     400; 
      <I    ; 
      L     20; 
      JC    En06; 

      A     #Gr.InManual; 
      JC    ILO6; 
      L     "MAL1S024Par".Par.ParIntMachine.TargetSpeedGermFan; 
      AN    "MAL1S024Par".Par.FlagProcess.ProcessEnd; 
      JC    En06; 
      L     20; //BRT170310 - nach Prozess Ende bis Start Entladen mit 20%
      JU    En06; 
ILO6: NOP   0; 
      L     "MAL1S024Par".Par.ParIntProcess.Speed_ILO_Fan; 
En06: NOP   0; 
      L     100; 
      *I    ; 
      T     #A_2860_TSC01_COM.OutValuePC; // xxx.xx%  

//BRT - bei 0% Firschluft hat es 100% Umluft, da mechanisch gekoppelt!
// If both louvers are closed, the fan speed must reduce  
//      L     "G241M".A_2852_MXZ01.OutPosition    //Fresh Air Flap
//      L     "G242M".A_2850_MXZ01.OutPosition    //Exhaust Air Flap
//      +I    
//      L     200                         //20.0%
//      <I    
//      L     50                          //50% fan speed
//      SPB   En07
      A     #Gr.InManual; 
      JC    ILO7; 
//      L     "MAL1S024Par".Par.ParIntMachine.TargetSpeedGermFan
      L     "MAL1S024Par".Par.ParIntProcess.Value26; // Target Speed GermFan 2
      AN    "MAL1S024Par".Par.FlagProcess.ProcessEnd; 
      JC    En07; 
      L     25; //BRT170310 - nach Prozess Ende bis Start Entladen mit 25%
      JU    En07; 
ILO7: NOP   0; 
      L     "MAL1S024Par".Par.ParIntProcess.Speed_ILO_Fan; 
En07: NOP   0; 
      L     100; 
      *I    ; 
      NEGI  ; 
      T     #A_2860_TSC02_COM.OutValuePC; // xxx.xx%  

NETWORK
TITLE =Interlocking WayOk

      L     "G251M".A_2851_MXZ01.OutPosition; //Fresh Air Flap
      L     "G252M".A_2850_MXZ01.OutPosition; //Exhaust Air Flap
      +I    ; 
      L     200; // 20%
      >=I   ; // opened more than min
      =     "G250M".AirMinOpened_1; 

      L     "G251M".A_2851_MXZ02.OutPosition; //Fresh Air Flap
      L     "G252M".A_2850_MXZ01.OutPosition; //Exhaust Air Flap
      +I    ; 
      L     200; // 20%
      >=I   ; // opened more than min
      =     "G250M".AirMinOpened_2; 

NETWORK
TITLE =Interlocking Autostart

      CLR   ; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE =Interlocking Enable
//BRT100310
      AN    "MAL1S024".PCRead.Status.StaImmediateStopASW; 
      A     #Gr.InEnableGr; 
      =     #Gr.OutEnableEl; 

NETWORK
TITLE =Interlocking Next

      A     #Gr.CmdNext; 
      A     #Gr.OutAutostart; 
      AN    #Gr.OutNewWay; 
      =     #tmpCmdNext; 

NETWORK
TITLE =Interlocking Local / HWStop

      A     "PLC_SystemDB".GlobalFlags.Germinating_8; 
//BRT100310 - HW-Stop nur bei NOT-AUS +C28 oder Customer, nicht bei NOT-AUS Maschine!
      A(    ; 
      O     "A_2899_SHE01_I1"; 
      O     "A_2899_SHE02_I1"; 
      O     "A_28_A097Z1_I1"; 
      O     "A_28_A106Z1_I1"; 
      )     ; 
      =     #tmpHWStop; 

      A     #Gr.ModePassive; 
      O     #tmpHWStop; 
      =     #tmpLocal; 

NETWORK
TITLE =Interlocking Start Warning

      NOP   0; 
//- Restart after fault --
      A     #Gr.CmdFaultReset; 
      A     #Gr.VarFault; 
      S     "SYS_StartWarningDB".Area[9].CmdResetStartWarning; 
//- Run Timer --
      OPN   "G250M"; 
      CALL "GCS7_StartWarning" , "SYS_StartWarningDB" (
           InModeHigher             := FALSE,
           InArea                   := 9,
           InEnable                 := #Gr.OutEnableEl,
           InLocal                  := #tmpLocal,
           InStarted                := #Gr.VarStarted,
           InCmdNext                := #tmpCmdNext,
           OutCmdNext               := #tmpCmdNext,
           OutStartwarning          := #Gr.VarStartwarning);
//- Store Startwarning done  --
//- Reset Autostart --
      A     "SYS_StartWarningDB".TmpArea.OutStartReleaseRunningL; 
      FN    #EdgeStartRelease; 
      A     #Gr.VarFault; 
      R     #Gr.OutAutostart; 
NETWORK
TITLE ==A-2860-MXZ01 : GERM FAN 1

      NOP   0; 
//-InLocal--- 
      A     #tmpLocal; 
      =     #A_2860_MXZ01.InLocal; 

//-Interlocking--- 
      A     #Gr.OutEnableEl; 
// and air-louvers are min opened
      A     "G250M".AirMinOpened_1; 
      O(    ; 
      A     #Gr.InManual; 
      A     #Gr.InOutAutostartMan; 
      ON    #Gr.InManual; 
      )     ; 
      =     #A_2860_MXZ01.InEnableFwd; 
// 
      A     #tmpCmdNext; 
      S     #A_2860_MXZ01.CmdNext; 
// 
      A     #Gr.OutSeqStop; 
      =     #A_2860_MXZ01.InSeqStop; 
// 
//-Read inputs---
      A     #A_2860_TSC01_COM.OutRunningFwd; 
      O     "PLC_SystemDB".GlobalFlags.StaTest; 
      A     "A_2860_MXZ01_I1"; 
      A     #A_2860_MXZ01.OutpRunFwd; 
      =     #A_2860_MXZ01.InpRunFwd; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_MXZ01.InHWStop; 
      A     "PLC_DPInfo".Master[1].SlavesError[88]; 
      =     #A_2860_MXZ01.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2860_MXZ01 ;
// 
//-Write outputs---
      A     #A_2860_MXZ01.OutpRunFwd; 

// Netzschuetz "EIN"
      AN    #A_2860_MXZ01.InSeqStop; 
      O     "G250M".A_2860_MXZ01.InManual; 
      O     #Gr.InManual; 
      =     "A_2860_MXZ01_O1"; 

NETWORK
TITLE ==A-2831-QXV12 : VALVE HUMIDIFICATION 1

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2831_QXV12.InLocal; 

//-Interlocking---
      A(    ; 
//      L     "MAL1S024Par".StepAct.AirMoisten
//====>BRT100310 - kein Kommentar!
//      L     DB914.DBB 1540
      L     "MAL1S024Par".StepAct.AirMoisten; 
      L     1; 
      ==I   ; 
      )     ; 
//      U     #A_2860_TSC01_PD1.OutL
//<====BRT100310
//      U     "MAL1S024Par".Par.FlagProcess.AirHumidificationValve1    // air humifidication valve 1 on
      A     #A_2860_MXZ01.StStartedFwd; 
      =     #A_2831_QXV12.InEnableHN; 
      NOT   ; 
      =     #A_2831_QXV12.InEnableLN; 

      SET   ; 
      S     #A_2831_QXV12.CmdNext; 

      A     #Gr.OutSeqStop; 
      =     #A_2831_QXV12.InSeqStop; 
// 
//-Read inputs---
      AN    #A_2831_QXV12.OutpHN; 
      =     #A_2831_QXV12.InpLN; 
      A     #A_2831_QXV12.OutpHN; 
      =     #A_2831_QXV12.InpHN; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2831_QXV12.InHWStop; 
      A     "PLC_DPInfo".Master[1].SlavesError[83]; 
      =     #A_2831_QXV12.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2831_QXV12 ;
// 
//-Write outputs---
      A     #A_2831_QXV12.OutpHN; 
      =     "A_2831_QXV12_O1"; 

NETWORK
TITLE ==A-2860-TSC01_COM : FREQUENCY. INVERTER GERM FAN 1

      NOP   0; 
      A     #A_2860_MXZ01.OutpRunFwd; 
      =     #A_2860_TSC01_COM.InEnableFwd; 
//      U     #A_2860_MXZ01.OutpRunRev
      CLR   ; 
      =     #A_2860_TSC01_COM.InEnableRev; 

      A     #Gr.CmdFaultReset; 
      S     #A_2860_TSC01_COM.CmdFaultReset; 

// 
//-Read inputs---
// 
//-Set Parameters---
// 
      A     "PLC_SystemDB".GlobalFlags.StaTest; 
      JC    NCo1; 
//-Call object control---
      CALL #A_2860_TSC01_COM ;
// 
//-Write outputs---
NCo1: NOP   0; 
// Simu
      A     "PLC_SystemDB".GlobalFlags.StaTest; 
      A     #A_2860_MXZ01.StStartedFwd; 
      JCN   ACT1; 
      L     #A_2860_TSC01_COM.OutValuePC; // xxx.xx%
      L     6; 
      /I    ; 
      L     10; 
      *I    ; 
      T     #A_2860_TSC01_COM.Output.SetpointValue01; 

      L     190; // 190A
      T     #A_2860_TSC01_COM.Output.SetpointValue02; 
ACT1: NOP   0; 

NETWORK
TITLE ==A-2860-TSC01_PD1 : FREQUENCY FREQ. GERM FAN 1

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2860_TSC01_PD1.InLocal; 

//-Monitoring--- 
      CLR   ; 
      =     #A_2860_TSC01_PD1.InMonLL; 
      CLR   ; 
      =     #A_2860_TSC01_PD1.InMonL; 
      CLR   ; 
      =     #A_2860_TSC01_PD1.InMonMid; 
      CLR   ; 
      =     #A_2860_TSC01_PD1.InMonH; 
      CLR   ; 
      =     #A_2860_TSC01_PD1.InMonHH; 
// 
// Simu
      A     #A_2860_MXZ01.OutpRunFwd; 
      A     "PLC_SystemDB".GlobalFlags.StaTest; 
      JCN   M160; 
      L     #A_2860_TSC01_COM.OutValuePC; // xxx.xx%  
      T     #A_2860_TSC01_COM.InValuePC; // Umgerechnet in FU-FB
M160: NOP   0; 
//-Read inputs---
      L     #A_2860_TSC01_COM.InValuePC; // Umgerechnet in FU-FB
      T     #A_2860_TSC01_PD1.InpValue; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_TSC01_PD1.InHWStop; 
// 
//-Set Parameters---
// --> Erläuterung
      L     L#27648; // InpValue=Drehzahl direkt   
      T     "G250M".A_2860_TSC01_PD1.Par.ParUnitsByMaxDigit; 
      L     L#10000; // OutValuePc=OutValueUnits
      T     "G250M".A_2860_TSC01_PD1.Par.ParUnitsBy100Pc; 
      L     L#4000; // 40.00% ist untere Grenze
      T     "G250M".A_2860_TSC01_PD1.Par.ParLimitLL; 
      L     L#4500; // 45.00%
      T     "G250M".A_2860_TSC01_PD1.Par.ParLimitL; 
      L     L#10000; // 100.00%
      T     "G250M".A_2860_TSC01_PD1.Par.ParLimitH; 
      L     L#10100; // 101.00%
      T     "G250M".A_2860_TSC01_PD1.Par.ParLimitHH; 
// 
//-Call object control---
      CALL #A_2860_TSC01_PD1 ;
// 
//-Write outputs---

NETWORK
TITLE ==A-2860-TSC01_PD2 : CURRENT FREQ. GERM FAN 1

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2860_TSC01_PD2.InLocal; 

//-Monitoring--- 
      CLR   ; 
      =     #A_2860_TSC01_PD2.InMonLL; 
      CLR   ; 
      =     #A_2860_TSC01_PD2.InMonL; 
      CLR   ; 
      =     #A_2860_TSC01_PD2.InMonMid; 
      CLR   ; 
      =     #A_2860_TSC01_PD2.InMonH; 
      CLR   ; 
      =     #A_2860_TSC01_PD2.InMonHH; 
// 
// Simu
      A     #A_2860_MXZ01.OutpRunFwd; 
      A     "PLC_SystemDB".GlobalFlags.StaTest; 
      JCN   M170; 
      L     900; 
      T     #A_2860_TSC01_COM.Input.ActualValue02; 
M170: NOP   0; 
//-Read inputs---
      L     #A_2860_TSC01_COM.Input.ActualValue02; 
      L     10; 
      *I    ; 
      T     #A_2860_TSC01_PD2.InpValue; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_TSC01_PD2.InHWStop; 
// 
//-Set Parameters---
//--> Erläuterung
      L     L#2666; // Stromwert bei 27648Dez
      T     "G250M".A_2860_TSC01_PD2.Par.ParUnitsByMaxDigit; 
      L     L#10000; // OutValuePc=OutValueUnits
      T     "G250M".A_2860_TSC01_PD2.Par.ParUnitsBy100Pc; 
      L     L#0; // 0.0A
      T     "G250M".A_2860_TSC01_PD2.Par.ParLimitLL; 
      L     L#400; // 40.00A
      T     "G250M".A_2860_TSC01_PD2.Par.ParLimitL; 
      L     L#1580; // 158.0A=100.0%
      T     "G250M".A_2860_TSC01_PD2.Par.ParLimitH; 
      L     L#2000; // 200.00A=130.0%
      T     "G250M".A_2860_TSC01_PD2.Par.ParLimitHH; 
// 
//-Call object control---
      CALL #A_2860_TSC01_PD2 ;
// 
//-Write outputs---

NETWORK
TITLE ==A-2860-TSC01_PD3 : TORQUE FREQ. INVERTER GERM FAN 1

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      SET   ; 
      =     #A_2860_TSC01_PD3.InLocal; 

//-Monitoring--- 
      CLR   ; 
      =     #A_2860_TSC01_PD3.InMonLL; 
      CLR   ; 
      =     #A_2860_TSC01_PD3.InMonL; 
      CLR   ; 
      =     #A_2860_TSC01_PD3.InMonMid; 
      CLR   ; 
      =     #A_2860_TSC01_PD3.InMonH; 
      CLR   ; 
      =     #A_2860_TSC01_PD3.InMonHH; 
// 
//-Read inputs---
//MW  L     #A_2860_TSC01_COM.Output.SetpointValue01
//MW      ITD   
//MW      DTR   
//MW      ABS   
//MW      RND   
      L     #A_2860_TSC01_COM.Input.ActualValue03; 
      T     #A_2860_TSC01_PD3.InpValue; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_TSC01_PD3.InHWStop; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2860_TSC01_PD3 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2860-TSC01_PD4 : PERFORMENCE FREQ. INVERTER GERM FAN 1

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      SET   ; 
      =     #A_2860_TSC01_PD4.InLocal; 

//-Monitoring--- 
      CLR   ; 
      =     #A_2860_TSC01_PD4.InMonLL; 
      CLR   ; 
      =     #A_2860_TSC01_PD4.InMonL; 
      CLR   ; 
      =     #A_2860_TSC01_PD4.InMonMid; 
      CLR   ; 
      =     #A_2860_TSC01_PD4.InMonH; 
      CLR   ; 
      =     #A_2860_TSC01_PD4.InMonHH; 
// 
//-Read inputs---
//MW  L     #A_2860_TSC01_COM.Output.SetpointValue01
//MW      ITD   
//MW      DTR   
//MW      ABS   
//MW      RND   
      L     #A_2860_TSC01_COM.Input.ActualValue04; 
      T     #A_2860_TSC01_PD4.InpValue; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_TSC01_PD4.InHWStop; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2860_TSC01_PD4 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2860-MXZ02 : GERM FAN 2

      NOP   0; 
//-InLocal--- 
      A     #tmpLocal; 
      =     #A_2860_MXZ02.InLocal; 

//-Interlocking--- 
      A     #Gr.OutEnableEl; 
// and air-louvers are min opened
      A     "G250M".AirMinOpened_2; 
      O(    ; 
      A     #Gr.InManual; 
      A     #Gr.InOutAutostartMan; 
      ON    #Gr.InManual; 
      )     ; 
      =     #A_2860_MXZ02.InEnableFwd; 
// 
      A     #tmpCmdNext; 
      S     #A_2860_MXZ02.CmdNext; 
// 
      A     #Gr.OutSeqStop; 
      =     #A_2860_MXZ02.InSeqStop; 
// 
//-Read inputs---
      A     #A_2860_TSC02_COM.OutRunningFwd; 
      O     "PLC_SystemDB".GlobalFlags.StaTest; 
      A     "A_2860_MXZ02_I1"; 
      A     #A_2860_MXZ02.OutpRunFwd; 
      =     #A_2860_MXZ02.InpRunFwd; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_MXZ02.InHWStop; 
      A     "PLC_DPInfo".Master[1].SlavesError[88]; 
      =     #A_2860_MXZ02.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2860_MXZ02 ;
// 
//-Write outputs---
//      U     #A_2860_MXZ02.OutpRunFwd

      AN    #A_2860_MXZ02.InSeqStop; 
      O     "G250M".A_2860_MXZ02.InManual; 
      O     #Gr.InManual; 
      =     "A_2860_MXZ02_O1"; 

NETWORK
TITLE ==A-2831-QXV22 : VALVE HUMIDIFICATION 2

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2831_QXV22.InLocal; 

//-Interlocking---
      A(    ; 
//      L     "MAL1S024Par".StepAct.AirMoisten
//====>BRT100310 - kein Kommentar!
//      L     DB914.DBB 1540
      L     "MAL1S024Par".StepAct.AirMoisten; 
      L     1; 
      ==I   ; 
      )     ; 
//      U     #A_2860_TSC02_PD1.OutL
//<====BRT070310
//      U     "MAL1S024Par".Par.FlagProcess.AirHumidificationValve1    // air humifidication valve 1 on
      A     #A_2860_MXZ02.StStartedFwd; 
      =     #A_2831_QXV22.InEnableHN; 
      NOT   ; 
      =     #A_2831_QXV22.InEnableLN; 

      SET   ; 
      S     #A_2831_QXV22.CmdNext; 

      A     #Gr.OutSeqStop; 
      =     #A_2831_QXV22.InSeqStop; 
// 
//-Read inputs---
      AN    #A_2831_QXV22.OutpHN; 
      =     #A_2831_QXV22.InpLN; 
      A     #A_2831_QXV22.OutpHN; 
      =     #A_2831_QXV22.InpHN; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2831_QXV22.InHWStop; 
      A     "PLC_DPInfo".Master[1].SlavesError[83]; 
      =     #A_2831_QXV22.InDPFault; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2831_QXV22 ;
// 
//-Write outputs---
      A     #A_2831_QXV22.OutpHN; 
      =     "A_2831_QXV22_O1"; 

NETWORK
TITLE ==A-2860-TSC02_COM : FREQUENCY. INVERTER GERM FAN 2

      NOP   0; 
      A     #A_2860_MXZ02.OutpRunFwd; 
      =     #A_2860_TSC02_COM.InEnableFwd; 
//      U     #A_2860_MXZ02.OutpRunRev
      CLR   ; 
      =     #A_2860_TSC02_COM.InEnableRev; 

      A     #Gr.CmdFaultReset; 
      S     #A_2860_TSC02_COM.CmdFaultReset; 

// 
//-Read inputs---
// 
//-Set Parameters---
      A     "PLC_SystemDB".GlobalFlags.StaTest; 
      JC    NCo2; 
//-Call object control---
      CALL #A_2860_TSC02_COM ;
// 
//-Write outputs---
NCo2: NOP   0; 
// Simu
      A     "PLC_SystemDB".GlobalFlags.StaTest; 
      A     #A_2860_MXZ02.StStartedFwd; 
      JCN   ACT2; 
      L     #A_2860_TSC02_COM.OutValuePC; // xxx.xx%
      L     6; 
      /I    ; 
      L     10; 
      *I    ; 
      T     #A_2860_TSC02_COM.Output.SetpointValue01; 

      L     190; // 190A
      T     #A_2860_TSC02_COM.Output.SetpointValue02; 
ACT2: NOP   0; 

NETWORK
TITLE ==A-2860-TSC02_PD1 : FREQUENCY FREQ. GERM FAN 2

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2860_TSC02_PD1.InLocal; 

//-Monitoring--- 
      CLR   ; 
      =     #A_2860_TSC02_PD1.InMonLL; 
      CLR   ; 
      =     #A_2860_TSC02_PD1.InMonL; 
      CLR   ; 
      =     #A_2860_TSC02_PD1.InMonMid; 
      CLR   ; 
      =     #A_2860_TSC02_PD1.InMonH; 
      CLR   ; 
      =     #A_2860_TSC02_PD1.InMonHH; 
// 
// Simu
      A     #A_2860_MXZ02.OutpRunFwd; 
      A     "PLC_SystemDB".GlobalFlags.StaTest; 
      JCN   M230; 
      L     #A_2860_TSC02_COM.OutValuePC; // xxx.xx%  
      T     #A_2860_TSC02_COM.InValuePC; // Umgerechnet in FU-FB
M230: NOP   0; 
//-Read inputs---
      L     #A_2860_TSC02_COM.InValuePC; 
      T     #A_2860_TSC02_PD1.InpValue; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_TSC02_PD1.InHWStop; 
// 
//-Set Parameters---
// --> Erläuterung
      L     L#27648; // InpValue=Drehzahl direkt   
      T     "G250M".A_2860_TSC02_PD1.Par.ParUnitsByMaxDigit; 
      L     L#10000; // OutValuePc=OutValueUnits
      T     "G250M".A_2860_TSC02_PD1.Par.ParUnitsBy100Pc; 
      L     L#4000; // 40.00% ist untere Grenze
      T     "G250M".A_2860_TSC02_PD1.Par.ParLimitLL; 
      L     L#4500; // 45.00%
      T     "G250M".A_2860_TSC02_PD1.Par.ParLimitL; 
      L     L#10000; // 100.00%
      T     "G250M".A_2860_TSC02_PD1.Par.ParLimitH; 
      L     L#10100; // 101.00%
      T     "G250M".A_2860_TSC02_PD1.Par.ParLimitHH; 
// 
//-Call object control---
      CALL #A_2860_TSC02_PD1 ;
// 
//-Write outputs---

NETWORK
TITLE ==A-2860-TSC02_PD2 : CURRENT FREQ. GERM FAN 2

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      =     #A_2860_TSC02_PD2.InLocal; 

//-Monitoring--- 
      CLR   ; 
      =     #A_2860_TSC02_PD2.InMonLL; 
      CLR   ; 
      =     #A_2860_TSC02_PD2.InMonL; 
      CLR   ; 
      =     #A_2860_TSC02_PD2.InMonMid; 
      CLR   ; 
      =     #A_2860_TSC02_PD2.InMonH; 
      CLR   ; 
      =     #A_2860_TSC02_PD2.InMonHH; 
// 
// Simu
      A     #A_2860_MXZ02.OutpRunFwd; 
      A     "PLC_SystemDB".GlobalFlags.StaTest; 
      JCN   M240; 
      L     900; 
      T     #A_2860_TSC02_COM.Input.ActualValue02; 
M240: NOP   0; 
//-Read inputs---
      L     #A_2860_TSC02_COM.Input.ActualValue02; 
      L     10; 
      *I    ; 
      T     #A_2860_TSC02_PD2.InpValue; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_TSC02_PD2.InHWStop; 
// 
//-Set Parameters---
//--> Erläuterung
      L     L#2666; // Stromwert bei 27648Dez
      T     "G250M".A_2860_TSC02_PD2.Par.ParUnitsByMaxDigit; 
      L     L#10000; // OutValuePc=OutValueUnits
      T     "G250M".A_2860_TSC02_PD2.Par.ParUnitsBy100Pc; 
      L     L#0; // 0.0A
      T     "G250M".A_2860_TSC02_PD2.Par.ParLimitLL; 
      L     L#400; // 40.00A
      T     "G250M".A_2860_TSC02_PD2.Par.ParLimitL; 
      L     L#1580; // 158.0A=100.0%
      T     "G250M".A_2860_TSC02_PD2.Par.ParLimitH; 
      L     L#2000; // 200.00A=130.0%
      T     "G250M".A_2860_TSC02_PD2.Par.ParLimitHH; 
// 
//-Call object control---
      CALL #A_2860_TSC02_PD2 ;
// 
//-Write outputs---

NETWORK
TITLE ==A-2860-TSC02_PD3 : TORQUE FREQ. INVERTER GERM FAN 2

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      SET   ; 
      =     #A_2860_TSC02_PD3.InLocal; 

//-Monitoring--- 
      CLR   ; 
      =     #A_2860_TSC02_PD3.InMonLL; 
      CLR   ; 
      =     #A_2860_TSC02_PD3.InMonL; 
      CLR   ; 
      =     #A_2860_TSC02_PD3.InMonMid; 
      CLR   ; 
      =     #A_2860_TSC02_PD3.InMonH; 
      CLR   ; 
      =     #A_2860_TSC02_PD3.InMonHH; 
// 
//-Read inputs---
//MW  L     #A_2860_TSC01_COM.Output.SetpointValue01
//MW      ITD   
//MW      DTR   
//MW      ABS   
//MW      RND   
      L     #A_2860_TSC02_COM.Input.ActualValue03; 
      T     #A_2860_TSC02_PD3.InpValue; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_TSC02_PD3.InHWStop; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2860_TSC02_PD3 ;
// 
//-Write outputs---
NETWORK
TITLE ==A-2860-TSC02_PD4 : PERFORMENCE FREQ. INVERTER GERM FAN 2

      NOP   0; 
//-InLocal---
      A     #tmpLocal; 
      SET   ; 
      =     #A_2860_TSC02_PD4.InLocal; 

//-Monitoring--- 
      CLR   ; 
      =     #A_2860_TSC02_PD4.InMonLL; 
      CLR   ; 
      =     #A_2860_TSC02_PD4.InMonL; 
      CLR   ; 
      =     #A_2860_TSC02_PD4.InMonMid; 
      CLR   ; 
      =     #A_2860_TSC02_PD4.InMonH; 
      CLR   ; 
      =     #A_2860_TSC02_PD4.InMonHH; 
// 
//-Read inputs---
//MW  L     #A_2860_TSC01_COM.Output.SetpointValue01
//MW      ITD   
//MW      DTR   
//MW      ABS   
//MW      RND   
      L     #A_2860_TSC02_COM.Input.ActualValue04; 
      T     #A_2860_TSC02_PD4.InpValue; 
//BRT100310      U     "PLC_SystemDB".HardwareStop.Germinating_8
      A     #tmpHWStop; //BRT100310
      =     #A_2860_TSC02_PD4.InHWStop; 
// 
//-Set Parameters---
// 
//-Call object control---
      CALL #A_2860_TSC02_PD4 ;
// 
//-Write outputs---
NETWORK
TITLE =Test Fault

      A     #A_2860_MXZ01.StFault; 
      O     #A_2831_QXV12.StFault; 
      O     #A_2860_TSC01_PD1.OutFaulted; 
      O     #A_2860_TSC01_PD2.OutFaulted; 
      O     #A_2860_TSC01_PD3.OutFaulted; 
      O     #A_2860_TSC01_PD4.OutFaulted; 
      O     #A_2860_MXZ02.StFault; 
      O     #A_2831_QXV22.StFault; 
      O     #A_2860_TSC02_PD1.OutFaulted; 
      O     #A_2860_TSC02_PD2.OutFaulted; 
      O     #A_2860_TSC02_PD3.OutFaulted; 
      O     #A_2860_TSC02_PD4.OutFaulted; 
      =     #Gr.VarFault; 
NETWORK
TITLE =Test Warning

      A     #A_2860_TSC01_PD1.OutWarning; 
      O     #A_2860_TSC01_PD2.OutWarning; 
      O     #A_2860_TSC01_PD3.OutWarning; 
      O     #A_2860_TSC01_PD4.OutWarning; 
      O     #A_2860_TSC02_PD1.OutWarning; 
      O     #A_2860_TSC02_PD2.OutWarning; 
      O     #A_2860_TSC02_PD3.OutWarning; 
      O     #A_2860_TSC02_PD4.OutWarning; 
      =     #Gr.OutWarning; 
NETWORK
TITLE =Test Stopped
//BRT100310      U     #A_2860_MXZ01.StStopped
//BRT100310      U     #A_2860_MXZ02.StStopped
//BRT100310      UN    #Gr.VarFault
      AN    #Gr.VarStarted; 
      =     #Gr.VarStopped; 
NETWORK
TITLE =Test Started
//BRT100310      U     #A_2831_QXV12.StHN
//BRT100310      U     #A_2831_QXV22.StHN
//BRT100310      ON    DB914.DBX 1540.0            // air humifidication valve 1 
//+ 2 on
      A     #A_2860_MXZ01.StStartedFwd; 
      A     #A_2860_MXZ02.StStartedFwd; 
//BRT100310      UN    #Gr.VarStopped
      AN    #Gr.VarFault; 
      =     #Gr.VarStarted; 

NETWORK
TITLE =Enable Previous Group

      A     #Gr.VarStarted; 
      =     #Gr.OutEnablePrevGr; 
NETWORK
TITLE =Call Methode State
//1. Make State of the group: This function use the signals:
//        Gr.VarStopped, Gr.VarStarted, Gr.VarFault
//2.Reset Commands to group: e.g.CmdFaultReset
      CALL #Gr (
           FcCode                   := 2);
NETWORK
TITLE =Init

      SET   ; 
      R     #CmdNewValue; 
      R     #CmdNewJob; 
NETWORK
TITLE =Write data to group control

      A     #Gr.StStopped; 
      =     "GCS7_GrCtrlDB".InStopped[250]; 
      A     #Gr.StStarted; 
      =     "GCS7_GrCtrlDB".InStarted[250]; 
      A     #Gr.StFault; 
      =     "GCS7_GrCtrlDB".InFault[250]; 
      A     #Gr.OutWarning; 
      =     "GCS7_GrCtrlDB".InWarning[250]; 
      A     #Gr.OutManual; 
      =     "GCS7_GrCtrlDB".InManual[250]; 
      A     #Gr.OutHWStop; 
      =     "GCS7_GrCtrlDB".InHWStop[250]; 
      A     #Gr.OutDPFault; 
      =     "GCS7_GrCtrlDB".InDPFault[250]; 
      A     #Gr.OutLoaded; 
      =     "GCS7_GrCtrlDB".InLoaded[250]; 
      A     #Gr.OutAutostart; 
      =     "GCS7_GrCtrlDB".InAutoStart[250]; 
      A     #Gr.VarStartwarning; 
      =     "GCS7_GrCtrlDB".InStartWarning[250]; 
NETWORK
TITLE =End of block

      BE    ; 

END_FUNCTION_BLOCK

