FUNCTION_BLOCK "GCS7_M_Motor2Mon"
TITLE =Motor with 2 monitoring elements 
//  
//COPYRIGHT:    (C) 1997  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      GCS7
//
//TITLE:        FB 51 MACHINE-CONTROL:Motor with 2 Digital Input
//               
//-------------------------------------------------------------------------
//
//IDENT:        AUTHOR:             DEPT:                       DATE:
//
//FB51.4.1      A. Egli             CT3                         14-Nov-2003
//              Interlocking check Ma.Par.ParWithMon2
//FB51.4.0      A. Egli             CT3                         17-Jun-2002
//              New Release with one block 
//FB51.3.4      A. Egli             CT3                         18-Oct-2001
//              Conditon for Prealarm with ParPS
//FB51.3.3      A. Egli             CT3                         26-Sep-2001
//              ParPS  GCPROVB: Revision 33
//FB51.3.2      A. Egli             CT3                         29-Jun-2001
//              ParManual 
//FB51.3.1      S. Kalberer         CT3                         10-Jan-2000
//              Set Mon1.InPreAlarmTrue instead of InPreAlarmFalse
//FB51.3.0      A. Egli             CT3                         11-Oct-1999
//              Local data reduced 
//FB51.2C       A. Egli             CT3                         03-Aug-1999
//              Test ParWithMon1 before call element
//FB51.2B       K. Tischer          MU664                       06-Mrz-1999
//              CC FC2 not UC FC2
//FB51.2A       A. Egli             CT3                         04-Mai-1998
//              StFault = StCode 32
//FB51.1A       A.Egli              AEE61                       5-Jul-1997
//              1st Release in S7
//
//-------------------------------------------------------------------------
//
//PURPOSE:      MANUAL : see [LL.M2IxxMAN]
//
//RESTRICTIONS: GCPROVB: Revision 33 for >=FB51.3.3
//              S7
//              ACCU2: Pointer to dynamic datas of the machine
//              ACCU1: Pointer to parameter datas of the machine
//
//-------------------------------------------------------------------------
AUTHOR : EA
FAMILY : GCS7LL
NAME : M_2DI
VERSION : 4.1


VAR
  Ma : "GCS7_M_FB51_Data";	
  Mon1 : "GCS7_E_DigInput";	//Element
  Mon2 : "GCS7_E_DigInput";	//Element
  Motor : "GCS7_E_Motor";	//Element
END_VAR
VAR_TEMP
  tmpFaultStop : BOOL ;	
  tmpFaultSum : BOOL ;	
  tmpLocal : BOOL ;	
  tmpLenDyn : INT ;	
  tmpEnable : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =
//
//
      L     10; 
      T     #tmpLenDyn; 

NETWORK
TITLE =PC Control
//=== Test Object number =====================
      L     "SYS_DirectCtrlDB".PC.ObjectNo; //PCControl.ObjectNo
      L     #Ma.Par.ParObjectNo; //Input ObjectNo
      <>I   ; 
      SPB   aEnd; 
//=== Test OType      ========================
      L     2051; //Input object type
      L     "SYS_DirectCtrlDB".PC.ObjectType; //PCControl.ObjectType
      <>I   ; 
      SPB   aEnd; 
//=== Test Handshake =========================
      U     "SYS_DirectCtrlDB".PC.Hsk.FExamine; // Handshake.FExamine
      SPB   aExa; 
      U     "SYS_DirectCtrlDB".PC.Hsk.FDefineCmd; // Handshake.FDefineCmd
      O     "SYS_DirectCtrlDB".PC.Hsk.FDefinePara; // Handshake.FDefinePara
      SPB   aCmd; 
aIni: L     2; // FcInit
      UC    "SYS_DirectCtrl"; 
      SPA   aEnd; 
// Write new commands from PC to Interface of group 
aCmd: U     "SYS_DirectCtrlDB".PC.Cmd1[8]; //Cmd1[8]=CmdOff
      S     #Ma.CmdOff; 
      U     "SYS_DirectCtrlDB".PC.Cmd1[9]; //Cmd1[9]=CmdOn
      S     #Ma.CmdOn; 
      U     "SYS_DirectCtrlDB".PC.Cmd1[10]; //Cmd1[10]=CmdNext
      S     #Ma.CmdNext; 
      U     "SYS_DirectCtrlDB".PC.Cmd1[11]; //Cmd1[11]=CmdFaultReset
      S     #Ma.CmdFaultReset; 
      U     "SYS_DirectCtrlDB".PC.Cmd1[13]; //Cmd1[13]=CmdManualOn
      S     #Ma.InManual; 
      U     "SYS_DirectCtrlDB".PC.Cmd2[13]; //Cmd2[13]=CmdManualOff
      R     #Ma.InManual; 
      U     "SYS_DirectCtrlDB".PC.Cmd1[6]; //Cmd1[14]=SlowOn
      S     #Motor.InOutManSlow; 
      U     "SYS_DirectCtrlDB".PC.Cmd2[6]; //Cmd2[14]=SlowOff
      R     #Motor.InOutManSlow; 
      U     "SYS_DirectCtrlDB".PC.Cmd1[7]; //Cmd1[15]=RevOn
      S     #Motor.InOutManRev; 
      U     "SYS_DirectCtrlDB".PC.Cmd2[7]; //Cmd2[15]=RevOff
      R     #Motor.InOutManRev; 
      UN    "SYS_DirectCtrlDB".PC.Hsk.FDefineCmd; // Handshake.FDefineCmd
      SPB   aIni; 
      SPA   aIni; 
aExa: TAR2  ; // Address Dynamic datas
      T     "SYS_DirectCtrlDB".GC.InDynAddr; // -->PCControl.GC.InDynAddr
      L     #tmpLenDyn; // Length Dynamic data
      T     "SYS_DirectCtrlDB".GC.InDynLen; // -->PCControl.GC.InDynLen
      L     0; 
      T     "SYS_DirectCtrlDB".GC.InParaAddr; // -->PCControl.GC.InParaAddr
      T     "SYS_DirectCtrlDB".GC.InParaLen; // -->PCControl.GC.InParaLen
      L     1; // Code FcExamine
      UC    "SYS_DirectCtrl"; 
      L     10; //Revision
      T     "SYS_DirectCtrlDB".GC.Revision; 
aEnd: NOP   0; 

NETWORK
TITLE =Interlocking
//===Global Group Orders===
      U     "GCS7_Gr_OutManualOn"; //Gr.OutManualOn
      S     #Ma.InManual; 
      U     "GCS7_Gr_OutManualOff"; //Gr.OutManualOff
      R     #Ma.InManual; 
      U     #Ma.InManual; 
      S     "GCS7_Gr_InManual"; //Gr.InManual
      U     #Ma.InLocal; 
      UN    #Ma.InManual; 
      =     #tmpLocal; 

//--- Fault----
      U     #Mon2.StFault; 
      U     #Ma.Par.ParMon2StopByFault; 
      O(    ; 
      U     #Mon1.StFault; 
      U     #Ma.Par.ParMon1StopByFault; 
      )     ; 
      =     #tmpFaultStop; 
      U     #Mon1.StFault; 
      O     #Mon2.StFault; 
      O     #Motor.StFault; 
      =     #tmpFaultSum; 
//--- Motor----
      U     #Mon1.StTrue; 
      O     #Mon1.StCtoFalse; 
      ON    #Ma.Par.ParMon1IsLS; 
      ON    #Ma.Par.ParMon1StopByFault; 
      U(    ; 
      O     #Mon2.StTrue; 
      O     #Mon2.StCtoFalse; 
      ON    #Ma.Par.ParMon2IsLS; 
      ON    #Ma.Par.ParMon2StopByFault; 
      ON    #Ma.Par.ParWithMon2; //[4.1]
      )     ; 
      =     #tmpEnable; //[4.0]
      U(    ; //3.2
      UN    #Motor.Par.ParManual; //3.2
      UN    #Motor.InOutManRev; //3.2
      ON    #Ma.InManual; //3.2
      U     #Ma.InEnableFwd; //3.2
      O     ; //3.2
      U     #Ma.InManual; //3.2
      U     #Motor.Par.ParManual; //3.2
      UN    #Motor.InOutManRev; //3.2
      )     ; //3.3
      UN    #tmpFaultStop; 
      U     #tmpEnable; //[6.0]
      UN    #Ma.CmdOff; 
      =     #Motor.InEnableFwd; 

      U(    ; //3.2
      UN    #Motor.Par.ParManual; //3.2
      U     #Motor.InOutManRev; //3.2
      ON    #Ma.InManual; //3.2
      U     #Ma.InEnableRev; //3.2
      O     ; //3.2
      U     #Ma.InManual; //3.2
      U     #Motor.Par.ParManual; //3.2
      U     #Motor.InOutManRev; //3.2
      )     ; //3.2
      UN    #tmpFaultStop; 
      UN    #Ma.CmdOff; 
      U     #tmpEnable; //[6.0]
      =     #Motor.InEnableRev; 

      U     #Ma.CmdNext; 
      UN    #Ma.InManual; 
      O     ; 
      U     #Ma.InManual; 
      U     #Ma.CmdOn; 
      S     #Motor.CmdNext; 
      U     #Ma.InSeqStop; 
      UN    #Ma.InManual; //3.2
      =     #Motor.InSeqStop; 

//--- Local mode --
      U     #tmpLocal; 
      =     #Motor.InLocal; 
      U     #Motor.StStopped; 
      U     #tmpLocal; 
      =     #Mon1.InLocal; 
      U     #Motor.StStopped; 
      O     #Ma.Par.ParMon2IsHL; 
      U     #tmpLocal; 
      O     #Ma.Par.ParMon2NoFault; 
      =     #Mon2.InLocal; 
//--- Monitoring
      O     #Motor.StStartedFwd; //[3.3]
      O     #Motor.StStartedRev; //[3.3]
      UN    #Mon1.InMonTrue; //[3.3]
      U     #Ma.Par.ParMon1IsPS; //[3.3]
      S     #Mon1.CmdRunTimeoutTime; //[3.3]

      O     #Motor.StStartedFwd; 
      O     #Motor.StStartedRev; 
      O     #Ma.Par.ParMon1IsLS; 
      =     #Mon1.InMonTrue; 

      U     #Motor.StStartedFwd; 
      O     #Motor.StStartedRev; 
      UN    #Ma.Par.ParMon2IsHL; 
      O     #Ma.Par.ParMon2IsLS; 
      =     #Mon2.InMonTrue; 

      U     #Ma.Par.ParMon2IsHL; 
      =     #Mon2.InMonFalse; 

      U     #Motor.StStartedFwd; // [3.4]
      UN    #Mon1.OutTimeoutTime; // [3.4]
      ON    #Ma.Par.ParMon1IsPS; // [3.4]
      U     #Ma.Par.ParMon1PreAlarm; 
      UN    #Mon1.CmdRunTimeoutTime; // [3.4]
      =     #Mon1.InPreAlarmTrue; //3.1: not InPreAlarmFalse

      UN    #Mon1.StTrue; 
      U     #Mon1.InPreAlarmTrue; // [3.4]
      =     #Ma.OutWarning; 
//--- Fault Reset -------------
      U     #Ma.CmdFaultReset; 
      S     #Motor.CmdFaultReset; 
      S     #Mon1.CmdFaultReset; 
      S     #Mon2.CmdFaultReset; 
NETWORK
TITLE =Set Mon1.OutDelayTime =0

      UN    #Motor.StStopped; //[3.4  --->
      UN    #Motor.StFault; 
      ON    #Mon1.StCtoFalse; 
      ON    #Ma.Par.ParMon1IsPS; 
      SPB   Par0; 
//Set Parameter
      L     0; 
      T     #Mon1.Par.OutDelayTime; 
Par0: NOP   0; // <-- 3.4]

NETWORK
TITLE =Call Digital Input: Monitor B1

      UN    #Ma.Par.ParWithMon1; 
      SPB   B2; 
      CALL #Mon1 ;

NETWORK
TITLE =Call Digital Input: Monitor B2

B2:   UN    #Ma.Par.ParWithMon2; 
      SPB   Moto; 
      CALL #Mon2 ;

NETWORK
TITLE =Call Motor

Moto: CALL #Motor ;

NETWORK
TITLE =Jump to actual state

      U     #Ma.StStopped; 
      SPB   S0; // ===> State
      U     #Ma.StStartedFwd; 
      SPB   S2; // ===> State
      U     #Ma.StStarting; 
      SPB   S1; // ===> State
      U     #Ma.StStopping; 
      SPB   S3; // ===> State
      U     #Ma.StStartedRev; 
      SPB   S5; // ===> State
      U     #Ma.StFault; 
      SPB   S4; // ===> State

NETWORK
TITLE =Stopped

xxS0: S     #Ma.StStopped; 
S0:   U     #tmpFaultSum; 
      R     #Ma.StStopped; 
      SPB   xxS4; //--> StFault
      UN    #Motor.StStopped; 
      R     #Ma.StStopped; 
      SPB   xxS1; //--> StStarting
      SPA   OUT; 

NETWORK
TITLE =Starting

xxS1: S     #Ma.StStarting; 
S1:   U     #tmpFaultSum; 
      R     #Ma.StStarting; 
      SPB   xxS4; //--> StFault
// Condition for StStopping
      U     #Motor.StStopped; 
      O     #Motor.StStopping; 
      R     #Ma.StStarting; 
      SPB   xxS3; //--> StStopping
      UN    #Mon1.StTrue; 
      U     #Ma.Par.ParWithMon1; 
      O     ; 
      UN    #Mon2.StTrue; 
      U     #Ma.Par.ParWithMon2; 
      UN    #Ma.Par.ParMon2IsHL; 
      SPB   S2En; 
// Condition for StStartedFwd
      U     #Motor.StStartedFwd; 
      R     #Ma.StStarting; 
      SPB   xxS2; //--> StStartedFwd
// Condition for StStartedFwd
      U     #Motor.StStartedRev; 
      R     #Ma.StStarting; 
      SPB   xxS5; //--> StStartedRev
S2En: SPA   OUT; 

NETWORK
TITLE =StartedFwd

xxS2: S     #Ma.StStartedFwd; 
S2:   U     #tmpFaultSum; 
      R     #Ma.StStartedFwd; 
      SPB   xxS4; //--> StFault
// Condition for StStopping
      UN    #Motor.StStartedFwd; 
      R     #Ma.StStartedFwd; 
      SPB   xxS3; //--> StStopping
      SPA   OUT; 
NETWORK
TITLE =StartedRev
//any Transition 
xxS5: S     #Ma.StStartedRev; 
//=== Logic
// Condition for StFault
S5:   U     #tmpFaultSum; 
      R     #Ma.StStartedRev; 
      SPB   xxS4; //--> StFault
// Condition for StStopping
      UN    #Motor.StStartedRev; 
      R     #Ma.StStartedRev; 
      SPB   xxS3; //--> StStopping
      SPA   OUT; 

NETWORK
TITLE =Stopping

xxS3: S     #Ma.StStopping; 
S3:   U     #tmpFaultSum; 
      R     #Ma.StStopping; 
      SPB   xxS4; //--> StFault
// Condition for StStopped
      U     #Motor.StStopped; 
      R     #Ma.StStopping; 
      SPB   xxS0; //--> StStopped
// Condition for StStarting
      UN    #Motor.StStopping; 
      R     #Ma.StStopping; 
      SPB   xxS1; //--> StStarting
      SPA   OUT; 

NETWORK
TITLE =Fault

xxS4: S     #Ma.StFault; 
S4:   UN    #tmpFaultSum; 
      R     #Ma.StFault; 
      SPB   xxS3; //--> StStopping
      SPA   OUT; 

NETWORK
TITLE =Global End

OUT:  CLR   ; //---Clear all commands
      =     #Ma.CmdOn; 
      =     #Ma.CmdOff; 
      =     #Ma.CmdNext; 
      =     #Ma.CmdFaultReset; 
NETWORK
TITLE =State Code
// Same as motor
      L     32; // StCode=Fault
      U     #Ma.StFault; 
      SPB   Code; 
      L     2; // StCode=Starting
      U     #Ma.StStarting; 
      SPB   Code; 
      L     8; // StCode=Warning
      U     #Ma.StStartedFwd; 
      O     #Ma.StStartedRev; 
      U     #Ma.OutWarning; 
      SPB   Code; 
      L     #Motor.StCode; // StCode=Motor
Code: L     #Ma.StCode; // Old state code
      TAK   ; 
      T     #Ma.StCode; // Save state code
      <>I   ; 
      L     3; 
      CC    "SYS_CBitCtrl"; //Set change bit

END_FUNCTION_BLOCK

