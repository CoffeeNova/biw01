FUNCTION FC 73 : VOID
TITLE =LOG: Message Converter for LOG Entry Format 10
//COPYRIGHT:     (C) 2003 BY  BUEHLER LTD. 
//                            CH-9240 UZWIL  SWITZERLAND
//
//PRODUCT:      GCS7 
//
//TITLE:        Message Converter for LOG Entry Format 10
//
//--------------------------------------------------------------------------
//
//IDENT:        AUTOR:              DEPT:                       DATE:
//
//FC73.1.0      A. Egli             CT3                         08.12.2003
//              1st Release
//
//--------------------------------------------------------------------------
//
//PURPOSE:       DATA-CONVERT FOR PRINTER PER LOG WITH PC
//
//RESTRICTIONS:  S7
//
//--------------------------------------------------------------------------
AUTHOR : EA
FAMILY : GCS7LL
NAME : MECONV
VERSION : 1.0


VAR_INPUT
  ObjectNo : INT ;	//Is used for communication with PC
  MsgType : INT ;	//Is used for message translation
  ClassMsgTxt : WORD ;	//Contains MessageClass and MessageTextNumber
  OType : INT ;	//Object Type
  MsgFormat : INT ;	
  OldValue : DINT ;	//Old Value to Report
  NewValue : DINT ;	//New Value to Report
END_VAR
VAR_TEMP
  tmpHeader : WORD ;	
  tmpPara2 : WORD ;	
  tmpObjectNo : WORD ;	
  tmpLineNo : WORD ;	
  tmpMsgType : WORD ;	
  tmpGr : INT ;	
  tmpIndex : INT ;	
  tmpPara5 : WORD ;	
  tmpPara6 : WORD ;	
  tmpPara7 : WORD ;	
  tmpPara8 : WORD ;	
END_VAR
BEGIN
NETWORK
TITLE = MESSAGE

//Standard GCS90
      L     #ObjectNo; // Change type INT --> WORD
      T     #tmpObjectNo; 
      L     #ClassMsgTxt; // Load: HighByte= Class ¦ Low Byte= TextNo (8 Bit)
      SRW   8; // Delete TextNo
      SLW   5; 
      L     #MsgFormat; 
      +I    ; // add MessageFormatNo
      T     #tmpHeader; //-> Header=Format := Class ¦ MsgFormatNo
      L     #ClassMsgTxt; // Load: HighByte= Class ¦ Low Byte= TextNo (8 Bit)
      L     2#11111111; // Delete Class
      UW    ; 
      L     #MsgType; 
      SLW   8; 
      OW    ; 
      T     #tmpPara2; //-->Para2 := MsgType ¦ TextNo

      L     #OldValue; 
      T     #tmpPara6; 
      SRD   16; 
      T     #tmpPara5; 
      L     #NewValue; 
      T     #tmpPara8; 
      SRD   16; 
      T     #tmpPara7; 
      L     MB    14; 
      T     #tmpLineNo; 
      L     #MsgType; 
      T     #tmpMsgType; 
      U     M     11.2; 
      SPB   EQ00; 
      CALL FC    46 (//---Call Message Entry---
           DBNumber                 := 5,// Queue DB Number                
           Header                   := #tmpHeader,// Format
           Para1                    := #tmpObjectNo,// ObjectNumber
           Para2                    := #tmpPara2,// MsgType ¦ TextNo
           Para3                    := #tmpLineNo,// Line
           Para4                    := #tmpMsgType,// MsgType S7
           Para5                    := #tmpPara5,// Old Value 
           Para6                    := #tmpPara6,// Old Value
           Para7                    := #tmpPara7,// New Value 
           Para8                    := #tmpPara8,// New Value 
           Para9                    := W#16#0,// not used 
           Para10                   := W#16#0,// not used 
           Para11                   := W#16#0);// not used
EQ00: NOP   0; 
NETWORK
TITLE =End of Block



END_FUNCTION

