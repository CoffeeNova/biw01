FUNCTION_BLOCK "GCS7_E_DFCA_Com"
TITLE =
//
//COPYRIGHT:    (C) 2000  BUHLER AG, CH-9240 UZWIL, SWITZERLAND
//
//PRODUCT:      GCS7
//
//TITLE:        DFCA: Control
//
//-------------------------------------------------------------------------
//
//IDENT:        AUTHOR:             DEPT:                       DATE:
//
//FB73.2.3     A.Egli              CT3                         06-Jan-2003
//               PumpXRequested,CmdCoolerRestdischarge
//FB73.2.2     A.Egli              CT3                         23-Mai-2002
//               PC Direct Control
//FB73.2.1     A.Egli              CT3                         16-Mar-2002
//               Set HeavyStartup flag
//FB73.2.0     A.Egli              CT3                         05-Feb-2002
//               With Alarm messages
//FB73.1.1     A.Egli              CT3                         10-Sep-2001
//               Send Stop also in state 0
//FB73.1.0     A.Egli              CT3                         30-Jul-2001
//               Integration in Machine
//FB73.1C      B.Thoma             FU53                        09-Jan-2001
//               Dosierunterbruch keine Flanke generieren !
//FB73.1B      B.Thoma             FU53                        10-Nov-2000
//               Fehler in Telex B102 und B103 nur wenn AKN=0 (see V1.B:)
//FB73.1A      A. Egli             CT3                         24-Mai-2000
//              1st Release in S7 (Original = FB80)
//
//-------------------------------------------------------------------------
//
//PURPOSE:      MANUAL : see [MxxMAN]
//
//RESTRICTIONS: 
//
//-------------------------------------------------------------------------
AUTHOR : EA
FAMILY : GCS7LL
NAME : DFCA_COM
VERSION : 2.3


VAR_INPUT
  IOByteNr : INT ;	//1.Byte der zugehörigen Ein- und Ausgänge
END_VAR
VAR
  Spare : INT ;	
  In : STRUCT 	
   CmdSendB101 : BOOL ;	//Command to send telegram B101
   CmdSendB102 : BOOL ;	//Command to send telegram B102
   CmdSendB103 : BOOL ;	//Command to send telegram B103
   Simulation : BOOL ;	//Simulation is on
   spare04 : BOOL ;	
   spare05 : BOOL ;	
   spare06 : BOOL ;	
   spare07 : BOOL ;	
   spare10 : BOOL ;	
   spare11 : BOOL ;	
   spare12 : BOOL ;	
   spare13 : BOOL ;	
   spare14 : BOOL ;	
   spare15 : BOOL ;	
   spare16 : BOOL ;	
   spare17 : BOOL ;	
   spare20 : INT ;	
  END_STRUCT ;	
  Out : STRUCT 	
   ErrorSendB101 : BOOL ;	//DFCA rejects B101
   ErrorSendB102 : BOOL ;	//DFCA rejects B102
   ErrorSendB103 : BOOL ;	//DFCA rejects B103
   DPFault : BOOL ;	//SFC14 or SFC15 returns with error code
   spare04 : BOOL ;	
   spare05 : BOOL ;	
   spare06 : BOOL ;	
   FlagStartPress : BOOL ;	
   FlagStopPress : BOOL ;	
   FlagResetTotal : BOOL ;	
   FlagResetHorn : BOOL ;	
   FlagFaultResetSingle : BOOL ;	
   FaultResetAll : BOOL ;	
   FlagHeavyStartup : BOOL ;	
   spare16 : BOOL ;	
   spare17 : BOOL ;	
   spare20 : INT ;	
  END_STRUCT ;	
  InCommand : STRUCT 	//Command are sent to DFCA
   StartPress : BOOL ;	
   StopPress : BOOL ;	
   DischargerRunning : BOOL ;	//de:Abtransport läuft
   CoolerOnMan : BOOL ;	//de:Austrag Gegenstromkuehler manuell ein
   HeaterOnMan : BOOL ;	//de:Pressenheizung und -heissluft manuell ein
   OverwriteLLHopper : BOOL ;	//de:Ueberbrueckung Vordepot Leerstand
   Suspend : BOOL ;	//de:Dosierunterbruch
   CmdResetTotal : BOOL ;	//de:Reset Mehlmenge
   CmdResetHorn : BOOL ;	//de:Reset Horn
   CmdFaultResetSingle : BOOL ;	//de:Reset einzelne Alarme
   CmdFaultResetAll : BOOL ;	//de:Reset alle Alarme
   CmdCoolerRestdischarge : BOOL ;	//de:Gegenstromkühler Restentleerung (CoolerOnMan must be =1)
   Bit4 : BOOL ;	
   Bit5 : BOOL ;	
   Bit6 : BOOL ;	
   Bit7 : BOOL ;	
   AlarmNummer : BYTE ;	
   Byte5 : BYTE ;	
  END_STRUCT ;	
  OutA101Ac : STRUCT 	//A101: Actuals value are read from DFCA 
   PressCurrent : INT ;	//de: Pressenstrom
   DosingSpeed : INT ;	//de: Dosierdrehzahl
   ConditionerTemperature : INT ;	//de: Konditioniertemperatur
   Liquid1 : INT ;	//de: Fluessigkeit 1
   Liquid2 : INT ;	//de: Fluessigkeit 2
   Liquid3 : INT ;	//de: Fluessigkeit 3
   WorkingGap : INT ;	//de: Arbeitsspalt
   State : INT ;	//de: Betriebszustand
   Mode : INT ;	//de: Betriebsmode
   spare20 : INT ;	
   spare22 : INT ;	
   FlouerWt : DINT ;	//de: Mehlmenge
   Flowrate : DINT ;	//de: Dosierleistung
  END_STRUCT ;	
  OutA102Ac : STRUCT 	//A102: Actual value are read from DFCA
   FormName : ARRAY  [1 .. 10 ] OF //de: Formname
   CHAR ;	
   RecipeIdent : ARRAY  [1 .. 16 ] OF //de: Rezeptident
   CHAR ;	
   Reserve28 : INT ;	
   Reserve30 : INT ;	
  END_STRUCT ;	
  OutA103Ac : STRUCT 	//A103: Actual values are read from DFCA : Recipe name
   RecipeName : ARRAY  [1 .. 30 ] OF //de: Rezeptname
   CHAR ;	
  END_STRUCT ;	
  OutA104Ac : STRUCT 	//A103: Actual values are read from DFCA : Status for visualisation
   MotorPress : BYTE ;	//de: MotorPresse
   DoorPress : BYTE ;	//de: TuerePresse
   HLInletPresss : BYTE ;	//de: EinlaufsondePresse
   HLOutletPress : BYTE ;	//de: AuslaufsondePresse
   FatPumpPress : BYTE ;	//de: FettpumpePresse
   OverflowFlap : BYTE ;	//de: Ueberlaufklappe
   MotorMixer1 : BYTE ;	//de: MotorMischer1
   MotorMixer2 : BYTE ;	//de: MotorMischer2
   MotorMixer3 : BYTE ;	//de: MotorMischer3
   DoorMixer1 : BYTE ;	//de: TuereMischer1
   DoorMixer2 : BYTE ;	//de: TuereMischer2
   DoorMixer3 : BYTE ;	//de: TuereMischer3
   MotorDosing : BYTE ;	//de: MotorDosierung
   DoorDosing : BYTE ;	//de: TuereDosierung
   Heater : BYTE ;	//de: Heizung
   HotAirFan : BYTE ;	//de: Heissluft
   LSProductFlowCooler : BYTE ;	//de: ProduktmelderKuehler
   HLCooler : BYTE ;	//de: VollstandKuehler
   VibroCooler : BYTE ;	//de: VibromotorKuehler
   HydraulicPump : BYTE ;	//de: Hydraulikpumpe
   FireProtectionFlap : BYTE ;	//de: BrandschutzSchieber
   LLInletHopper : BYTE ;	//de: LeerstandVordepot
   LLBin : BYTE ;	//de: LeerstandSilo
   MiidleLevlBin : BYTE ;	//de: MittelstandSilo
   HornOn : BOOL ;	//de: HornEin
   SumAlarm : BOOL ;	//de: SammelAlarm
   Pump1Requested : BOOL ;	//de: Pumpe Flüssigkeit 1 einschalten
   Pump2Requested : BOOL ;	//de: Pumpe Flüssigkeit 2 einschalten
   Pump3Requested : BOOL ;	//de: Pumpe Flüssigkeit 3 einschalten
   spare245 : BOOL ;	
   Messages : ARRAY  [200 .. 231 ] OF //de: Meldungen
   BOOL ;	
  END_STRUCT ;	
  OutA201Nom : STRUCT 	//A201: Nominal values are read from DFCA : Status for visualisation
   Pressenstrom : INT ;	//de: Pressenstrom
   DosingSpeed : INT ;	//de: Dosierdrehzahl
   DosingSpeed2 : INT ;	//de: Dosierdrehzahl Stufe 2
   DosingSpeed3 : INT ;	//de: Dosierdrehzahl Stufe 3
   ConditionerTemperature : INT ;	//de: Konditioniertemperatur
   Liquid1 : INT ;	//de: Fluessigkeit 1
   Liquid2 : INT ;	//de: Fluessigkeit 2
   Liquid3 : INT ;	//de: Fluessigkeit 3
   WorkingGap : INT ;	//de: Arbeitsspalt
   ParameterSet : INT ;	//de: Parametersatz
   CapacityCooler : INT ;	//de: DurchsatzKuehler
   Reserve24 : INT ;	
   PelletDiameter : INT ;	//de: Wuerfeldurchmesser
   Capacity100Pc : DINT ;	//de: Durchsatz100Pc
  END_STRUCT ;	
  OutA301Ac : STRUCT 	//A103: Actual values are read from DFCA : Alarmstatus
   Alarms : ARRAY  [1 .. 15 ] OF //de: Alarme
   STRUCT 	
    Number : BYTE ;	
    Status : BYTE ;	
   END_STRUCT ;	
  END_STRUCT ;	
  InB101Nom : STRUCT 	//B101: Nominal values are sent to DFCA : Part 1
   PressCurrent : INT ;	//de: Pressenstrom
   DosingSpeed : INT ;	//de: Dosierdrehzahl
   DosingSpeed2 : INT ;	//de: Dosierdrehzahl Stufe 2
   DosingSpeed3 : INT ;	//de: Dosierdrehzahl Stufe 3
   ConditionerTemperature : INT ;	//de: Konditioniertemperatur
   Liquid1 : INT ;	//de: Fluessigkeit 1
   Liquid2 : INT ;	//de: Fluessigkeit 2
   Liquid3 : INT ;	//de: Fluessigkeit 3
   WorkingGap : INT ;	//de: Arbeitsspalt
   CapacityCooler : INT ;	//de: DurchsatzKuehler
   ParameterSet : INT ;	//de: Parametersatz
   Reserve24 : INT ;	
   PelletDiameter : INT ;	//de: Wuerfeldurchmesser
   Capacity100Pc : DINT ;	//de: Durchsatz100Pc
  END_STRUCT ;	
  InB102Nom : STRUCT 	//B102: Nominal values are sent to DFCA : Part 2
   FormName : ARRAY  [1 .. 10 ] OF CHAR ;	
   RecipeIdent : ARRAY  [1 .. 16 ] OF //de:Rezeptident
   CHAR ;	
   Spare28 : INT ;	
   Spare30 : INT ;	
  END_STRUCT ;	
  InB103Nom : STRUCT 	//B103: Nominal values are sent to DFCA : Part 3
   RecipeName : ARRAY  [1 .. 30 ] OF //de: Rezeptname
   CHAR ;	
  END_STRUCT ;	
  OutAnswerB101 : ARRAY  [1 .. 15 ] OF STRUCT 	
   AKN : BYTE ;	
   ErrorCode : BYTE ;	
  END_STRUCT ;	
  Step : INT ;	//Step Communication: Internal used
  SendIdent : WORD ;	
  RetValueSFCRead : INT ;	//Parameter RET_VAL of SFC14: DPRD_DAT
  RetValueSFCSend : INT ;	//Parameter RET_VAL of SFC15: DPWR_DAT
  OutTimeOut : INT ;	//Timeout communication
  Par : STRUCT 	
   ParObjectNo : INT ;	
   ParMsgType : INT ;	
   ParHornCode : INT ;	//Not Used
  END_STRUCT ;	
END_VAR
VAR_TEMP
  tmpAddr : WORD ;	
  tmpRetVal : INT ;	
  tmpErrorSnd : BOOL ;	
  tmpFound : BOOL ;	
  ComData : STRUCT 	//Receive Data Block
   Ident : WORD ;	
   Data : ARRAY  [2 .. 16 ] OF INT ;	
  END_STRUCT ;	
  tmpCnt : INT ;	
  tmpAlarm : WORD ;	
END_VAR
BEGIN
NETWORK
TITLE =



NETWORK
TITLE =PC Control
//=== Test Object number =====================
      L     "SYS_DirectCtrlDB".PC.ObjectNo; //PCControl.ObjectNo
      L     #Par.ParObjectNo; //Input ObjectNo
      <>I   ; 
      SPB   Ctrl; 
//=== Test OType      ========================
      L     1073; //Input object type
      L     "SYS_DirectCtrlDB".PC.ObjectType; //PCControl.ObjectType
      <>I   ; 
      SPB   Ctrl; 
//=== Test Handshake =========================
      U     "SYS_DirectCtrlDB".PC.Hsk.FExamine; //Handshake.FExamine
      SPB   pExa; 
      U     "SYS_DirectCtrlDB".PC.Hsk.FDefineCmd; //Handshake.FDefineCmd
      SPB   pCmd; 
      U     "SYS_DirectCtrlDB".PC.Hsk.FDefinePara; //Handshake.FDefinePara
      SPB   pPar; 
      SPA   Ctrl; //[1.1]
pIni: L     2; //FcInit
      UC    "SYS_DirectCtrl"; 
      SPA   Ctrl; 
//Write parameter 
pPar: NOP   0; 
//Write new commands from PC to Interface of group 
pCmd: NOP   0; 
      SPA   pIni; 
pExa: TAR2  ; //Address dynamic datas
      T     "SYS_DirectCtrlDB".GC.InDynAddr; //-->PCControl.GC.InDynAddr
      L     330; //Length dynamic datas
      T     "SYS_DirectCtrlDB".GC.InDynLen; //-->PCControl.GC.InDynLen
      L     0; //Address parameter data
      T     "SYS_DirectCtrlDB".GC.InParaAddr; //-->PCControl.GC.InParaAddr
      T     "SYS_DirectCtrlDB".GC.InParaLen; //-->PCControl.GC.InParaLen
      L     1; //Code FcExamine
      UC    "SYS_DirectCtrl"; 
Ctrl: NOP   0; 
NETWORK
TITLE =Read Data

      L     #IOByteNr; 
      T     #tmpAddr; 
      U     #In.Simulation; 
      SPB   r001; 
//read from DFCA
      CALL "SIEMENS_DPRD_DAT" (
           LADDR                    := #tmpAddr,
           RET_VAL                  := #RetValueSFCRead,
           RECORD                   := #ComData);
      SPA   r002; 
//read from Simulation
r001: L     0; 
      T     #RetValueSFCSend; 
      T     #RetValueSFCRead; 
      U     "GCS7_Gr_SimDPtoImage"; 
      L     P#E 0.0; 
      SPB   rDB1; 
      AUF   "TEST_IOSimuliInput"; 
      L     P#DBX 0.0; 
rDB1: L     #IOByteNr; 
      SLD   3; 
      +D    ; 
      LAR1  ; 
      L     W [AR1,P#0.0]; 
      T     #ComData.Ident; 
      L     W [AR1,P#2.0]; 
      T     #ComData.Data[2]; 
      L     W [AR1,P#4.0]; 
      T     #ComData.Data[3]; 
      L     W [AR1,P#6.0]; 
      T     #ComData.Data[4]; 
      L     W [AR1,P#8.0]; 
      T     #ComData.Data[5]; 
      L     W [AR1,P#10.0]; 
      T     #ComData.Data[6]; 
      L     W [AR1,P#12.0]; 
      T     #ComData.Data[7]; 
      L     W [AR1,P#14.0]; 
      T     #ComData.Data[8]; 
      L     W [AR1,P#16.0]; 
      T     #ComData.Data[9]; 
      L     W [AR1,P#18.0]; 
      T     #ComData.Data[10]; 
      L     W [AR1,P#20.0]; 
      T     #ComData.Data[11]; 
      L     W [AR1,P#22.0]; 
      T     #ComData.Data[12]; 
      L     W [AR1,P#24.0]; 
      T     #ComData.Data[13]; 
      L     W [AR1,P#26.0]; 
      T     #ComData.Data[14]; 
      L     W [AR1,P#28.0]; 
      T     #ComData.Data[15]; 
      L     W [AR1,P#30.0]; 
      T     #ComData.Data[16]; 
r002: NOP   0; 

NETWORK
TITLE =Store received Data
// Reset Edge commands----------------
// -Test send command
      L     #SendIdent; 
      L     W#16#F000; 
      UW    ; 
      L     W#16#A000; 
      <>I   ; 
      SPB   AJ0; 
// -condition
      L     #ComData.Ident; 
      L     #SendIdent; 
      <>I   ; 
      SPB   AJ0; 
      U     #Out.FlagResetTotal; 
      R     #InCommand.CmdResetTotal; 
// -uncondition
AJ0:  L     #ComData.Ident; 
      L     #SendIdent; 
      <>I   ; 
      U(    ; 
      L     #RetValueSFCSend; 
      L     0; 
      <>I   ; 
      )     ; 
      SPB   AJ1; 
      U     #Out.FlagStartPress; 
      R     #InCommand.StartPress; 
      U     #Out.FlagStopPress; 
      R     #InCommand.StopPress; 
      U     #Out.FlagResetHorn; 
      R     #InCommand.CmdResetHorn; 
      U     #Out.FlagFaultResetSingle; 
      R     #InCommand.CmdFaultResetSingle; 
      U     #Out.FaultResetAll; 
      R     #InCommand.CmdFaultResetAll; 
//---Test Error----------------
AJ1:  L     #RetValueSFCRead; 
      L     0; 
      <>I   ; 
      SPB   BXXX; 
      L     #RetValueSFCSend; 
      L     0; 
      ==I   ; 
      <>I   ; 
      SPB   BXXX; 
//---Store recevie Data--------
//A101------------------------
      L     #ComData.Ident; 
      L     W#16#A101; 
      <>I   ; 
      SPB   A102; 
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #ComData.Data,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #OutA101Ac);
//A102------------------------
A102: L     #ComData.Ident; 
      L     W#16#A102; 
      <>I   ; 
      SPB   A103; 
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #ComData.Data,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #OutA102Ac);
//A103------------------------
A103: L     #ComData.Ident; 
      L     W#16#A103; 
      <>I   ; 
      SPB   A104; 
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #ComData.Data,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #OutA103Ac);
//A104------------------------
A104: L     #ComData.Ident; 
      L     W#16#A104; 
      <>I   ; 
      SPB   A201; 
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #ComData.Data,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #OutA104Ac);
//A201------------------------
A201: L     #ComData.Ident; 
      L     W#16#A201; 
      <>I   ; 
      SPB   A301; 
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #ComData.Data,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #OutA201Nom);
//A301------------------------
A301: L     #ComData.Ident; 
      L     W#16#A301; 
      <>I   ; 
      SPB   B101; 

//===========================================
//Send Alalrm Message to PC
      L     #Par.ParObjectNo; 
      L     0; 
      ==I   ; 
      SPB   alNO; //--> NoAlarm
      L     P##ComData; 
      L     P#2.0; 
      +D    ; 
      LAR1  ; 
      L     B [AR1,P#0.0]; 
      T     #tmpAlarm; 
      L     0; 
      ==I   ; 
      SPB   alNO; //--> NoAlarm
      L     B [AR1,P#1.0]; 
      L     0; 
      ==I   ; 
      SPB   alNO; //--> NoAlarm
      TAR2  ; 
      L     DW#16#FFFFFF; //Clear Memory type
      UD    ; 
      L     P##OutA301Ac; 
      +D    ; 
      LAR1  ; 
      L     15; 
alNx: T     #tmpCnt; 
      L     B [AR1,P#0.0]; 
      L     #tmpAlarm; 
      ==I   ; 
      U(    ; 
      L     B [AR1,P#1.0]; 
      L     0; 
      <>I   ; 
      )     ; 
      SPB   alNO; // If old Alrman the --> NoAlarm
      +AR1  P#2.0; 
      L     #tmpCnt; 
      LOOP  alNx; 
      L     #tmpAlarm; 
      L     W#16#100; //Error Class
      OW    ; 
      T     #tmpAlarm; 
      CALL "GCS7_LogConvert" (// LOG Message Converter / Entry
           ObjectNo                 := #Par.ParObjectNo,
           MsgType                  := #Par.ParMsgType,
           ClassMsgTxt              := #tmpAlarm,//Contains Class and TextNo
           OType                    := 1073);


//============================================
alNO: CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #ComData.Data,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #OutA301Ac);
//B101------------------------
B101: L     #ComData.Ident; 
      L     W#16#EE00; //Error code Wrong Mode
      ==I   ; 
      =     #tmpErrorSnd; 
      L     #SendIdent; 
      L     W#16#B101; 
      <>I   ; 
      SPB   B102; 
      U     #tmpErrorSnd; 
      S     #Out.ErrorSendB101; 
      SPB   B102; 
      L     #ComData.Ident; 
      L     W#16#B101; 
      <>I   ; 
      SPB   B102; 
      R     #In.CmdSendB101; 
      R     #Out.ErrorSendB101; 
//=============================================
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #ComData.Data,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #OutAnswerB101);
//B102------------------------
B102: L     #SendIdent; 
      L     W#16#B102; 
      <>I   ; 
      SPB   B103; 
      U     #tmpErrorSnd; 
      S     #Out.ErrorSendB102; 
      SPB   B103; 
      L     #ComData.Ident; 
      L     W#16#B102; 
      ==I   ; 
      R     #In.CmdSendB102; 
      R     #Out.ErrorSendB102; 
//B103------------------------
B103: L     #SendIdent; 
      L     W#16#B103; 
      <>I   ; 
      SPB   BXXX; 
      U     #tmpErrorSnd; 
      S     #Out.ErrorSendB103; 
      SPB   BXXX; 
      L     #ComData.Ident; 
      L     W#16#B103; 
      ==I   ; 
      R     #In.CmdSendB103; 
      R     #Out.ErrorSendB103; 
BXXX: NOP   0; 

NETWORK
TITLE =Send Data
// ===================================
// Wait for answer telegramm
// ===================================
      UN    "GCS7_Gr_OutTick500ms"; 
      SPB   SnTo; 
      L     #OutTimeOut; 
      +     5; 
      T     #OutTimeOut; 
SnTo: NOP   0; 
      L     #SendIdent; 
      L     #ComData.Ident; 
      <>I   ; 
      U(    ; 
      L     #ComData.Ident; 
      L     W#16#EE01; 
      <>I   ; 
      )     ; 
      U(    ; 
      L     #ComData.Ident; 
      L     W#16#EE00; 
      <>I   ; 
      )     ; 
      U(    ; 
      L     #SendIdent; 
      L     W#16#0; 
      <>I   ; 
      )     ; 
      U(    ; 
      L     #OutTimeOut; 
      L     20; //Egli
      <I    ; 
      )     ; 
      SPB   SnEn; //--> Not Done
//==================================================
//Prepare Data for Next Telegram
LOOP: L     0; 
      T     #OutTimeOut; 
      L     #Step; 
      SPL   SnEs; //Else
      SPA   Sn0; 
      SPA   Sn1; 
      SPA   Sn2; 
      SPA   Sn3; 
      SPA   Sn4; 
      SPA   Sn5; 
      SPA   Sn6; 
      SPA   Sn7; 
      SPA   Sn8; 
//Else: Init Step
SnEs: L     0; 
      T     #Step; 
//A101:Read Betriebswerte:
Sn0:  NOP   0; 
      L     W#16#A101; 
      SPA   Snd1; 
//A102:Read Formname und Rezeptident
Sn1:  NOP   0; 
      L     W#16#A102; 
      SPA   Snd1; 
//A103:Read Rezeptname
Sn2:  NOP   0; 
      L     W#16#A103; 
      SPA   Snd1; 
//A104:Read Visualisierung
Sn3:  NOP   0; 
      L     W#16#A104; 
      SPA   Snd1; 
//A201:Read Eingestellte Sollwerte
Sn4:  NOP   0; 
      L     W#16#A201; 
      SPA   Snd1; 
//A301:Read AlarmStaten
Sn5:  NOP   0; 
      L     W#16#A301; 
      SPA   Snd1; 
//B101:Send Sollwerte Anlage
Sn6:  UN    #In.CmdSendB101; 
      SPB   SnNx; 
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #InB101Nom,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #ComData.Data);
      L     W#16#B101; 
      SPA   Snd2; 
//B102:Send Formname + Rezeptident
Sn7:  UN    #In.CmdSendB102; 
      O     #In.CmdSendB101; 
      SPB   SnNx; 
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #InB102Nom,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #ComData.Data);
      L     W#16#B102; 
      SPA   Snd2; 
//B103:Send Rezeptname
Sn8:  UN    #In.CmdSendB103; 
      O     #In.CmdSendB101; 
      O     #In.CmdSendB102; 
      SPB   SnNx; 
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #InB103Nom,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #ComData.Data);
      L     W#16#B103; 
      SPA   Snd2; 
//Prepare for all Axxx
Snd1: T     #SendIdent; 
      L     #OutA101Ac.State; 
      L     0; 
      <>I   ; 
      O     #InCommand.StopPress; 
      R     #InCommand.StartPress; 
      CALL "SIEMENS_BLKMOV" (
           SRCBLK                   := #InCommand,
           RET_VAL                  := #tmpRetVal,
           DSTBLK                   := #ComData.Data);
      U     #InCommand.StartPress; 
      =     #Out.FlagStartPress; 
      U     #InCommand.StopPress; 
      =     #Out.FlagStopPress; 
      U     #InCommand.CmdResetTotal; 
      =     #Out.FlagResetTotal; 
      U     #InCommand.CmdResetHorn; 
      =     #Out.FlagResetHorn; 
      U     #InCommand.CmdFaultResetSingle; 
      =     #Out.FlagFaultResetSingle; 
      U     #InCommand.CmdFaultResetAll; 
      =     #Out.FaultResetAll; 
      SPA   Snd3; 
//Prepare for all Bxxx
//Send
Snd2: T     #SendIdent; 
Snd3: L     #SendIdent; 
      T     #ComData.Ident; 
//write to DFCA
      U     #In.Simulation; 
      SPB   Snd4; 
      CALL "SIEMENS_DPWR_DAT" (
           LADDR                    := #tmpAddr,
           RECORD                   := #ComData,
           RET_VAL                  := #RetValueSFCSend);
      SPA   Snd5; 
//write to Simulation
Snd4: U     "GCS7_Gr_SimDPtoImage"; 
      L     P#A 0.0; 
      SPB   rDB2; 
      AUF   "TEST_IOSimuliOutput"; 
      L     P#DBX 0.0; 
rDB2: L     #IOByteNr; 
      SLD   3; 
      +D    ; 
      LAR1  ; 
      L     #ComData.Ident; 
      T     W [AR1,P#0.0]; 
      L     #ComData.Data[2]; 
      T     W [AR1,P#2.0]; 
      L     #ComData.Data[3]; 
      T     W [AR1,P#4.0]; 
      L     #ComData.Data[4]; 
      T     W [AR1,P#6.0]; 
      L     #ComData.Data[5]; 
      T     W [AR1,P#8.0]; 
      L     #ComData.Data[6]; 
      T     W [AR1,P#10.0]; 
      L     #ComData.Data[7]; 
      T     W [AR1,P#12.0]; 
      L     #ComData.Data[8]; 
      T     W [AR1,P#14.0]; 
      L     #ComData.Data[9]; 
      T     W [AR1,P#16.0]; 
      L     #ComData.Data[10]; 
      T     W [AR1,P#18.0]; 
      L     #ComData.Data[11]; 
      T     W [AR1,P#20.0]; 
      L     #ComData.Data[12]; 
      T     W [AR1,P#22.0]; 
      L     #ComData.Data[13]; 
      T     W [AR1,P#24.0]; 
      L     #ComData.Data[14]; 
      T     W [AR1,P#26.0]; 
      L     #ComData.Data[15]; 
      T     W [AR1,P#28.0]; 
      L     #ComData.Data[16]; 
      T     W [AR1,P#30.0]; 
//=Next Telegramm===========================
Snd5: L     #Step; 
      +     1; 
      T     #Step; 
      SPA   SnEn; 
//Next Telegram
SnNx: L     #Step; 
      +     1; 
      T     #Step; 
      SPA   LOOP; 
SnEn: NOP   0; 
NETWORK
TITLE =DPFault

      O(    ; 
      L     #RetValueSFCRead; 
      L     0; 
      <>I   ; 
      )     ; 
      O(    ; 
      L     #RetValueSFCSend; 
      L     0; 
      <>I   ; 
      )     ; 
      =     #Out.DPFault; 

NETWORK
TITLE =Heavy Startup

      L     #OutA104Ac.MotorPress; 
      L     2; 
      ==I   ; 
      UN    "GCS7_Gr_InHeavyStartup"; 
      S     #Out.FlagHeavyStartup; 
      S     "GCS7_Gr_InHeavyStartup"; 
      <>I   ; 
      U     #Out.FlagHeavyStartup; 
      R     #Out.FlagHeavyStartup; 
      R     "GCS7_Gr_InHeavyStartup"; 

END_FUNCTION_BLOCK

