	ORGANIZATION_BLOCK OB1
	TITLE =Main program
	//COPYRIGHT:    (C) 2000 BY  BUHLER AG
	//                           CH-9240 UZWIL  SWITZERLAND
	//
	//PRODUCT:      GEN_MU664
	//
	//TITLE:        Main program
	//              
	//              Hauptprogramm
	//
	//
	//--------------------------------------------------------------------------------
	//
	//IDENT:        AUTHOR:             DEPT:                              DATE:
	//
	//GEN_OB001.1.0 A.Egli              CT3                                07-Feb-2003
	//              Redesign GCS7 Release 2
	//
	//--------------------------------------------------------------------------------
	//
	//PURPOSE:      
	//
	//RESTRICTIONS: 
	//
	//--------------------------------------------------------------------------------
	//
	//
	//
	AUTHOR  : KR      
	FAMILY  : GEN
	VERSION : 9.9
	
	
	VAR_TEMP
	  OB1_EV_CLASS : BYTE ; //Bits 0-3 = 1 (Coming event), Bits 4-7 = 1 (Event class 1)
	  OB1_SCAN_1 : BYTE ;   //1 (Cold restart scan 1 of OB 1), 3 (Scan 2-n of OB 1)
	  OB1_PRIORITY : BYTE ; //1 (Priority of 1 is lowest)
	  OB1_OB_NUMBR : BYTE ; //1 (Organization block 1, OB1)
	  OB1_RESERVED_1 : BYTE ;   //Reserved for system
	  OB1_RESERVED_2 : BYTE ;   //Reserved for system
	  OB1_PREV_CYCLE : INT ;    //Cycle time of previous OB1 scan (milliseconds)
	  OB1_MIN_CYCLE : INT ; //Minimum cycle time of OB1 (milliseconds)
	  OB1_MAX_CYCLE : INT ; //Maximum cycle time of OB1 (milliseconds)
	  OB1_DATE_TIME : DATE_AND_TIME ;   //Date and time OB1 started
	  tmpInt : INT ; 
	  tmpWord : WORD ; 
	END_VAR
	BEGIN
	NETWORK
	TITLE =TF General test functions
	
	      U     "PLC_SystemDB".GlobalFlags.StaTest; 
	      SPBN  TF00; 
	// 
	//-Test functions ON---
	      SET   ; 
	      R     "PLC_SystemDB".GlobalFlags.StaProtocolling; 
	      R     "PLC_SystemDB".TestFunctions.StaIOManager; 
	      R     "PLC_SystemDB".TestFunctions.StaHardware; 
	      S     "PLC_SystemDB".TestFunctions.StaCWASimulation; 
	      S     "PLC_SystemDB".TestFunctions.StaIOSimuli; 
	      S     "PLC_SystemDB".TestFunctions.StaDPSimulation;
	      S     "PLC_SystemDB".TestFunctions.StaPCDirectInhouseTest; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC1ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC2ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC3ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC4ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC5ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaWinAC; 
	
	// 
	//-Temporary test functions....... (0000)---
	      U     "PLC_SystemDB".TestFunctions.StaIOManager; 
	      SPBN  TF02; 
	      CALL "TEST_Functions" (
	           code_no                  := 0000,
	           RET_VAL                  := #tmpWord);
	TF02: NOP   0; 
	// 
	//-............................... (1001)---
	      U     "PLC_SystemDB".TestFunctions.StaIOSimuli; 
	      SPBN  TF03; 
	      CALL "TEST_Functions" (
	           code_no                  := 1001,
	           RET_VAL                  := #tmpWord);
	TF03: NOP   0; 
	// 
	//-Test functions OFF---
	TF00: NOP   0; 
	NETWORK
	TITLE =IN Initialisation
	
	      NOP   0; 
	//-Standard software---
	      CALL  FC    52;
	// 
	//-Simulation with DB---
	      SET   ; 
	      =     M     11.1; 
	// 
	//-User software---
	      SET   ; 
	      =     M     50.0; 
	      U     "PLC_SystemDB".GlobalFlags.StaProtocolling; 
	      =     M     12.6; 
	      U     "PLC_SystemDB".GlobalFlags.StaLogOff;
	      =     M     11.2; 
	// 
	//-Testfunctionality for WinAC---
	      U     "PLC_SystemDB".TestFunctions.StaWinAC; 
	      SPBN  IN01; 
	      CALL SFC   39 (
	           MODE                     := B#16#2,
	           OB_NR                    := 80,
	           RET_VAL                  := #tmpInt);
	      SPA   IN02; 
	IN01: CALL SFC   40 (
	           MODE                     := B#16#2,
	           OB_NR                    := 80,
	           RET_VAL                  := #tmpInt);
	IN02: NOP   0; // 
	// 
	//-Testfunctions---
	      UN    "PLC_SystemDB".GlobalFlags.StaTest; 
	      R     "PLC_SystemDB".GlobalFlags.StaProtocolling; 
	      R     "PLC_SystemDB".TestFunctions.StaIOManager; 
	      R     "PLC_SystemDB".TestFunctions.StaHardware; 
	      R     "PLC_SystemDB".TestFunctions.StaCWASimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaIOSimuli; 
	      R     "PLC_SystemDB".TestFunctions.StaDPSimulation;
	      R     "PLC_SystemDB".TestFunctions.StaPCDirectInhouseTest; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC1ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC2ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC3ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC4ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaPLC5ComSimulation; 
	      R     "PLC_SystemDB".TestFunctions.StaWinAC; 
	// 
	//-Startup warning time
	      L     10;
	      T     "SYS_StartWarningDB".ParStartwarningTime;
	      L     120;
	      T     "SYS_StartWarningDB".ParStartReleaseTimeL;
	      L     20;
	      T     "SYS_StartWarningDB".ParStartReleaseTimeH;
	NETWORK
	TITLE =GF Plant general functions
	           CALL  "SYS_GeneralFunctions" ;
	           CALL  FC81 ;
	//         CALL  "HW_PanelLamps";
	NETWORK
	TITLE =GR General, way and machine groups
	           CALL  FC     9
	
	NETWORK
	TITLE =LS Lines and sections
	
	         CALL  FC    11(
	             InCode:=0);
	
	NETWORK
	TITLE =PC PCC Direct control
	
	      NOP   0; 
	//-LC Parameter Upload---
	      L     10; 
	      AUF   DB     10; 
	      UC    FC     3; 
	//-General control---
	      L     0; 
	      AUF   DB     3; 
	      UC    FC     3; 
	// 
	//-Control for process control system main---
	      L     5; 
	      AUF   DB    40; 
	      UC    FC     3; 
	// 
	//-Control for process control system clients---
	//  ---Client 1 --- 
	      L     5;
	      AUF   DB41;
	      UC    FC3;
	//  ---Client 2 --- 
	      L     5;
	      AUF   DB42;
	      UC    FC3;
	//  ---Client 3 --- 
	      L     5;
	      AUF   DB43;
	      UC    FC3;
	//  ---Client 4 --- 
	      L     5;
	      AUF   DB44;
	      UC    FC3;
	//  ---Client 5 --- 
	      L     5;
	      AUF   DB45;
	      UC    FC3;
	//  ---Client 6 --- 
	      L     5;
	      AUF   DB46;
	      UC    FC3;
	//  ---Client 7 --- 
	      L     5;
	      AUF   DB47;
	      UC    FC3;
	// 
	//-Control for ObjView---
	      L     5; 
	      AUF   DB    48; 
	      UC    FC     3; 
	// 
	//-Control for inhouse test channel---
	      L     5; 
	      U     "PLC_SystemDB".TestFunctions.StaPCDirectInhouseTest; 
	      AUF   DB    49; 
	      CC    FC     3; 
	NETWORK
	TITLE =CB Changebit
	//-> ParBlockLen: 128 for VISControl; 220 for MELE; 220 for WinCoS
	//-> For more changebit-interface-DB's add the new DB and set the pointer
	//   ParNextDB in the previous DB. A call of additional method is not necessary.
	      NOP   0; 
	//-Init Changebit---
	      L     220; 
	      T     DB2.DBW   12; 
	// 
	//-Copy change bits from object DB to change bit DB---
	      AUF DB2;
	      L     1; 
	      CALL  FC     2 ;                   //Read/copy
	// 
	//-Reset change bit CBit[x], if PC has accepted---
	      AUF DB2;
	      L     2; 
	      CALL  FC     2 ;                   //Update
	
	NETWORK
	TITLE =MQ message queues
	
	      NOP   0; 
	//-LOG-Queue---
	       CALL  FC    47(
	         DBNumber:=5);
	NETWORK
	TITLE =CM Communication
	
	      NOP   0; 
	//-->
	      U     "ENG_RemarkFlag"; 
	//--Check that the InModeRetry is set to 1, if newtronics are connected on a loop
	      =     "ENG_RemarkFlag"; 
	//<--
	// 
	//-PLC to PLC Communication---
	//      CALL  "COM_PLCtoPLC"
	NETWORK
	TITLE =GD Graphic display data (PLC=>PCC)
	
	// Copy cycle time
	      L     #OB1_MIN_CYCLE; 
	      T     "ASW_GraphicData".General.CycleTime[1].Minimal; 
	      L     #OB1_PREV_CYCLE; 
	      T     "ASW_GraphicData".General.CycleTime[1].Actual; 
	      L     #OB1_MAX_CYCLE; 
	      T     "ASW_GraphicData".General.CycleTime[1].Maximal; 
	// Call Funtion
	     CALL FC80; 
	
	NETWORK
	TITLE =DP Diagnosis
	
	      NOP   0; 
	//-Detailed profibus DP---
	        CALL  FC    75;
	// 
	//-PLCLiveValue for the PCC---
	      CALL  "STD_PLCLiveValue"(
	       LiveValue:="SYS_CBitDB".PLCLifeValue);
	      CALL  "STD_PLCLiveValue"(
	       LiveValue:="SYS_PC1CBitDB".PLCLifeValue);
	      CALL  "STD_PLCLiveValue"(
	       LiveValue:="SYS_PC2CBitDB".PLCLifeValue);
	NETWORK
	TITLE =Heavy Startup M13.6
	//This flag must be reseted, when is is to long high
	//(This is happen after downloading the DB)
	      U     GCS7_Gr_InHeavyStartup; 
	      L     S5T#1M; 
	      SE    "SYS_DelayHeavyStartup"; 
	      U     "SYS_DelayHeavyStartup"; 
	      R     GCS7_Gr_InHeavyStartup; 
	NETWORK
	TITLE =BrakeMill M12.0
	//This flag must be reseted, when is is to long high
	//(This is happen after downloading the DB)
	      U     GCS7_Gr_InBrakeMill; 
	      L     S5T#1M; 
	      SE    "SYS_DelayBrakeMill"; 
	      U     "SYS_DelayBrakeMill"; 
	      R     GCS7_Gr_InBrakeMill; 
	NETWORK
	TITLE =Reset startup flags
	      SET   ; 
	      R     "PLC_SystemDB".Startup.CmdRestartMan; 
	      R     "PLC_SystemDB".Startup.CmdRestartAuto; 
	NETWORK
	TITLE =End of block
	
	      BE    ; 
	
	END_ORGANIZATION_BLOCK
